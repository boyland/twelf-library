%%%% Map addition

%{%
#define MAP_JOIN 1
%}%


%%% Definition of join


join : map -> map -> map -> type.


join/L : join map/0 M M.

join/R : join M map/0 M.

join/= : join (map/+ N1 D1 M1) (map/+ N2 D2 M2) (map/+ N1 D3 M3)
    <- nat`eq N1 N2
    <- data`join D1 D2 D3
    <- join M1 M2 M3.

join/< : join (map/+ N1 D1 M1) (map/+ N2 D2 M2) (map/+ N1 D1 M3)
    <- nat`plus (s N0) N1 N2
    <- join M1 (map/+ N0 D2 M2) M3.

join/> : join (map/+ N1 D1 M1) (map/+ N2 D2 M2) (map/+ N2 D2 M3)
    <- nat`plus (s N3) N2 N1
    <- join (map/+ N3 D1 M1) M2 M3.



%%% Theorems about join


%theorem false-implies-join :
	forall* {M1} {M2} {M3}
	forall {F:void}
	exists {D:join M1 M2 M3}
	true.

%worlds (WORLDS) (false-implies-join _ _).
%total {} (false-implies-join _ _).


%theorem join-respects-eq :
	forall* {M1} {M2} {M3} {M1P} {M2P} {M3P}
	forall {A:join M1 M2 M3} {E1:eq M1 M1P} {E2:eq M2 M2P} {E3:eq M3 M3P}
	exists {AP:join M1P M2P M3P}
	true.

- : join-respects-eq A eq/ eq/ eq/ A.

%worlds (WORLDS) (join-respects-eq _ _ _ _ _).
%total {} (join-respects-eq _ _ _ _ _).
%reduces A = AP (join-respects-eq A _ _ _ AP).


%% A way to get a join fact.

%theorem can-construct-unit-join:
	forall*	{N} {D} {M1} {M}
	forall	{S: shift N M1 M}
	exists	{A: join M (map/+ N D map/0) (map/+ N D M1)}
	true.

- : can-construct-unit-join shift/0 join/L.

- : can-construct-unit-join (shift/+ P) (join/> join/R PPP)
    <- plus-swap-succ P PP
    <- plus-commutative PP PPP.

%worlds (WORLDS) (can-construct-unit-join _ _).
%total { } (can-construct-unit-join _ _).


%% Inversion lemmas for join

%theorem join/=-inversion :
	forall* {N1} {D1} {M1} {N2} {D2} {M2} {M} 
	forall {A:join (map/+ N1 D1 M1) (map/+ N2 D2 M2) M}
	       {G:nat`eq N1 N2}
	exists {D3} {M3} 
	       {D:data`join D1 D2 D3}
               {AP:join M1 M2 M3}
               {E:eq M (map/+ N1 D3 M3)}
	true.

- : join/=-inversion (join/= MM DD nat`eq/) _ _ _ DD MM eq/.

- : join/=-inversion (join/< (JP:join _ (map/+ N0 D2 M2) M3) N0+1+N=N) nat`eq/ 
                     D2 M3 DJ MJ ME
    <- plus-implies-gt N0+1+N=N nat`eq/ N>N
    <- gt-anti-reflexive N>N F
    <- data`false-implies-join F DJ
    <- false-implies-eq F (M022=M2:eq (map/+ N0 D2 M2) M2)
    <- join-respects-eq JP eq/ M022=M2 eq/ MJ
    <- false-implies-eq F ME.

- : join/=-inversion (join/> (JP:join (map/+ _ D1 M1) M2 M3) N3+1+N=N) nat`eq/
                     D1 M3 DJ MJ ME
    <- nat`plus-implies-gt N3+1+N=N nat`eq/ N>N
    <- nat`gt-anti-reflexive N>N F
    <- data`false-implies-join F DJ
    <- false-implies-eq F (M311=M1:eq (map/+ N3 D1 M1) M1)
    <- join-respects-eq JP M311=M1 eq/ eq/ MJ
    <- false-implies-eq F ME.

%worlds (WORLDS) (join/=-inversion _ _ _ _ _ _ _).
%total {} (join/=-inversion _ _ _ _ _ _ _).
%reduces JP < J (join/=-inversion J _ _ _ _ JP _).


%theorem join/<-inversion :
	forall* {N1} {D1} {M1} {N2} {D2} {M2} {M} {N0}
	forall {A:join (map/+ N1 D1 M1) (map/+ N2 D2 M2) M}
               {P:plus (s N0) N1 N2} 	       
	exists {M3} 
               {AP:join M1 (map/+ N0 D2 M2) M3}
               {E:eq M (map/+ N1 D1 M3)}
	true.

- : join/<-inversion (join/< J P) PP _ JP eq/
    <- nat`plus-right-cancels P PP nat`eq/ nat`eq/ N0+1=N0P+1
    <- succ-cancels N0+1=N0P+1 N0=N0P
    <- map/+-preserves-eq N0=N0P data`eq/ eq/ M022=M022P
    <- join-respects-eq J eq/ M022=M022P eq/ JP.

- : join/<-inversion (join/= JP _ nat`eq/) N0+1+N=N M3 J E
    <- plus-implies-gt N0+1+N=N nat`eq/ N>N
    <- gt-anti-reflexive N>N F
    <- false-implies-eq F M2=M022
    <- join-respects-eq JP eq/ M2=M022 eq/ J
    <- false-implies-eq F E.

- : join/<-inversion (join/> JP N3+1+N2=N1) N0+1+N1=N2 M3 J E
    <- nat`plus-implies-gt N3+1+N2=N1 nat`eq/ N1>N2
    <- nat`plus-implies-gt N0+1+N1=N2 nat`eq/ N2>N1
    <- nat`gt-anti-symmetric N1>N2 N2>N1 F
    <- false-implies-eq F M311=M1
    <- false-implies-eq F M2=M022
    <- join-respects-eq JP M311=M1 M2=M022 eq/ J
    <- false-implies-eq F E.

%worlds (WORLDS) (join/<-inversion _ _ _ _ _).
%total {}  (join/<-inversion _ _ _ _ _).
%reduces JP < J (join/<-inversion J _ _ JP _).


%theorem join/>-inversion :
	forall* {N1} {D1} {M1} {N2} {D2} {M2} {N3} {M} 
	forall {A:join (map/+ N1 D1 M1) (map/+ N2 D2 M2) M}
	       {P:plus (s N3) N2 N1}
	exists {M3} 
               {AP:join (map/+ N3 D1 M1) M2 M3}
               {E:eq M (map/+ N2 D2 M3)}
	true.

- : join/>-inversion (join/> J P) PP _ JP eq/
    <- nat`plus-right-cancels P PP nat`eq/ nat`eq/ N3+1=N3P+1
    <- succ-cancels N3+1=N3P+1 N3=N3P
    <- map/+-preserves-eq N3=N3P data`eq/ eq/ M311=M311P
    <- join-respects-eq J M311=M311P eq/ eq/ JP.

- : join/>-inversion (join/= JP _ nat`eq/) N3+1+N=N M3 J E
    <- nat`plus-implies-gt N3+1+N=N nat`eq/ N>N
    <- gt-anti-reflexive N>N F
    <- false-implies-eq F M1=M311
    <- join-respects-eq JP M1=M311 eq/ eq/ J
    <- false-implies-eq F E.

- : join/>-inversion (join/< JP N0+1+N1=N2) N3+1+N2=N1 M3 J E
    <- nat`plus-implies-gt N3+1+N2=N1 nat`eq/ N1>N2
    <- nat`plus-implies-gt N0+1+N1=N2 nat`eq/ N2>N1
    <- nat`gt-anti-symmetric N1>N2 N2>N1 F
    <- false-implies-eq F M1=M311
    <- false-implies-eq F M022=M2
    <- join-respects-eq JP M1=M311 M022=M2 eq/ J
    <- false-implies-eq F E.

%worlds (WORLDS) (join/>-inversion _ _ _ _ _).
%total {} (join/>-inversion _ _ _ _ _).
%reduces JP < J (join/>-inversion J _ _ JP _).


%theorem false-implies-join-reduces:
	forall*	{M1} {M2} {M3} {M4} {M5} {M6}
	forall	{F: void}
		{J1: join M1 M2 M3}
		{J2: join M4 M5 M6}
	true.

%worlds () (false-implies-join-reduces _ _ _).
%total { } (false-implies-join-reduces _ _ _).
%reduces J2 < J1 (false-implies-join-reduces _ J1 J2).


%theorem join/<-inversion*:
	forall*	{N1} {D1} {M1}
		{N2} {D2} {M2}
		{N3} {D3} {M3}
	forall	{J: join (map/+ N1 D1 M1) (map/+ N2 D2 M2) (map/+ N3 D3 M3)}
		{NE: nat`ne N2 N3}
	exists	{N0}
		{EQN: nat`eq N1 N3}
		{EQD: data`eq D1 D3}
		{P: plus (s N0) N3 N2}
		{J0: join M1 (map/+ N0 D2 M2) M3}
	true.

- : join/<-inversion* (join/< J0 P) _ _ nat`eq/ data`eq/ P J0.

- : join/<-inversion* J NE nat`z EQN EQD P J0
    <- nat`eq-ne-implies-false nat`eq/ NE F
    <- nat`false-implies-eq F EQN
    <- data`false-implies-eq F EQD
    <- nat`false-implies-plus F P
    <- false-implies-join F J0
    <- false-implies-join-reduces F J J0.

%worlds (WORLDS) (join/<-inversion* _ _ _ _ _ _ _).
%total { } (join/<-inversion* _ _ _ _ _ _ _).
%reduces JP < J (join/<-inversion* J _ _ _ _ _ JP).


%theorem join/>-inversion*:
	forall*	{N1} {D1} {M1}
		{N2} {D2} {M2}
		{N3} {D3} {M3}
	forall	{J: join (map/+ N1 D1 M1) (map/+ N2 D2 M2) (map/+ N3 D3 M3)}
		{NE: nat`ne N1 N3}
	exists	{N0}
		{EQN: nat`eq N2 N3}
		{EQD: data`eq D2 D3}
		{P: plus (s N0) N3 N1}
		{J0: join (map/+ N0 D1 M1) M2 M3}
	true.

- : join/>-inversion* (join/> J0 P) _ _ nat`eq/ data`eq/ P J0.

- : join/>-inversion* J NE nat`z EQN EQD P J0
    <- nat`eq-ne-implies-false nat`eq/ NE F
    <- nat`false-implies-eq F EQN
    <- data`false-implies-eq F EQD
    <- nat`false-implies-plus F P
    <- false-implies-join F J0
    <- false-implies-join-reduces F J J0.

%worlds (WORLDS) (join/>-inversion* _ _ _ _ _ _ _).
%total { } (join/>-inversion* _ _ _ _ _ _ _).
%reduces JP < J (join/>-inversion* J _ _ _ _ _ JP).

%{%
#ifdef DATA_JOIN_DETERMINISTIC
%}%

%theorem join-deterministic :
	forall* {M1} {M2} {M3} {M1P} {M2P} {M3P}
	forall {A:join M1 M2 M3} {AP:join M1P M2P M3P}
               {E1:eq M1 M1P} {E2:eq M2 M2P}
	exists {E3:eq M3 M3P}
	true.

%abbrev join-unique = join-deterministic.

- : join-deterministic join/L join/L eq/ eq/ eq/.

- : join-deterministic join/L join/R eq/ eq/ eq/.

- : join-deterministic join/R join/L eq/ eq/ eq/.

- : join-deterministic join/R join/R eq/ eq/ eq/.

- : join-deterministic (join/= M1+M2=M3 D1+D2=D3 nat`eq/)
                      (join/= M1+M2=M3P D1+D2=D3P nat`eq/) 
		      eq/ eq/ M=MP
    <- data`join-deterministic D1+D2=D3 D1+D2=D3P data`eq/ data`eq/ D3=D3P
    <- join-deterministic M1+M2=M3 M1+M2=M3P eq/ eq/ M3=M3P
    <- map/+-preserves-eq nat`eq/ D3=D3P M3=M3P M=MP.

- : join-deterministic (join/< M1+MT=M3 N0+1+N1=N2)
                      (join/< M1+MTP=M3P N0P+1+N1=N2)
		      eq/ eq/ M=MP
    <- plus-right-cancels N0+1+N1=N2 N0P+1+N1=N2 nat`eq/ nat`eq/ N0+1=N0P+1
    <- succ-cancels N0+1=N0P+1 N0=N0P
    <- map/+-preserves-eq N0=N0P data`eq/ eq/ MT=MTP
    <- join-deterministic M1+MT=M3 M1+MTP=M3P eq/ MT=MTP M3=M3P
    <- map/+-preserves-eq nat`eq/ data`eq/ M3=M3P M=MP.

- : join-deterministic (join/> MT+M2=M3 N3+1+N2=N1)
                      (join/> MTP+M2=M3P N3P+1+N2=N1) eq/ eq/ E
    <- plus-right-cancels N3+1+N2=N1 N3P+1+N2=N1 nat`eq/ nat`eq/ N3+1=N3P+1
    <- succ-cancels N3+1=N3P+1 N3=N3P
    <- map/+-preserves-eq N3=N3P data`eq/ eq/ MT=MTP
    <- join-deterministic MT+M2=M3 MTP+M2=M3P MT=MTP eq/ M3=M3P
    <- map/+-preserves-eq nat`eq/ data`eq/ M3=M3P E.

%% contradiction cases:

- : join-deterministic (join/= _ _ nat`eq/) (join/< _ NP+1+N=N) eq/ eq/ E
    <- plus-implies-gt NP+1+N=N nat`eq/ N>N
    <- gt-anti-reflexive N>N F
    <- false-implies-eq F E.

- : join-deterministic (join/= _ _ nat`eq/) (join/> _ NP+1+N=N) eq/ eq/ E
    <- plus-implies-gt NP+1+N=N nat`eq/ N>N
    <- gt-anti-reflexive N>N F
    <- false-implies-eq F E.

- : join-deterministic (join/< _ NP+1+N=N) (join/= _ _ nat`eq/) eq/ eq/ E
    <- plus-implies-gt NP+1+N=N nat`eq/ N>N
    <- gt-anti-reflexive N>N F
    <- false-implies-eq F E.

- : join-deterministic (join/< _ N0+1+N1=N2) (join/> _ N3+1+N2=N1) eq/ eq/ E
    <- nat`plus-implies-gt N3+1+N2=N1 nat`eq/ N1>N2
    <- nat`plus-implies-gt N0+1+N1=N2 nat`eq/ N2>N1
    <- nat`gt-anti-symmetric N1>N2 N2>N1 F
    <- false-implies-eq F E.

- : join-deterministic (join/> _ NP+1+N=N) (join/= _ _ nat`eq/) eq/ eq/ E
    <- plus-implies-gt NP+1+N=N nat`eq/ N>N
    <- gt-anti-reflexive N>N F
    <- false-implies-eq F E.

- : join-deterministic (join/> _ N3+1+N2=N1) (join/< _ N0+1+N1=N2) eq/ eq/ E
    <- nat`plus-implies-gt N3+1+N2=N1 nat`eq/ N1>N2
    <- nat`plus-implies-gt N0+1+N1=N2 nat`eq/ N2>N1
    <- nat`gt-anti-symmetric N1>N2 N2>N1 F
    <- false-implies-eq F E.

%worlds (WORLDS) (join-deterministic _ _ _ _ _).
%total (A) (join-deterministic A _ _ _ _).

%{%
#endif /* DATA_JOIN_DETERMINISTIC */
%}%


%{%
#ifdef DATA_JOIN_TOTAL_STAR
%}%

%theorem join-total* :
	forall {M1} {M2}
	exists {M3} {A:join M1 M2 M3}
	true.

%% we need some lemmas
%% We need them to ensure termination because
%% join substitutes new maps on recursive calls which
%% makes it hard to prove the arguments get smaller.

%theorem join-map/+-M-total* :
	forall {N1} {D1} {M1} {M2}
        exists {M3} {A:join (map/+ N1 D1 M1) M2 M3}
	true.

%theorem join-M-map/+-total* :
	forall {M1} {N2} {D2} {M2}
        exists {M3} {A:join M1 (map/+ N2 D2 M2) M3}
	true.

%theorem join-map/+-map/+-total* :
	forall {N1} {D1} {M1} {N2} {D2} {M2} {C} {CMP:nat`compare N1 N2 C}
        exists {M3} 
               {A:join (map/+ N1 D1 M1) (map/+ N2 D2 M2) M3}
	true.

- : join-total* map/0 M M join/L.

- : join-total* M map/0 M join/R.

- : join-total* (map/+ N1 D1 M1) (map/+ N2 D2 M2) M3 A
    <- nat`compare-total* N1 N2 C CMP
    <- join-map/+-map/+-total* N1 D1 M1 N2 D2 M2 C CMP M3 A.               

- : join-map/+-map/+-total* N1 D1 M1 N2 D2 M2 equal CMP (map/+ N1 D3 M3)
                           (join/= M1+M2=M3 D1+D2=D3 N1=N2)
    <- equal-implies-eq CMP N1=N2
    <- data`join-total* D1 D2 D3 D1+D2=D3
    <- join-total* M1 M2 M3 M1+M2=M3.

- : join-map/+-map/+-total* N1 D1 M1 N2 D2 M2 less CMP (map/+ N1 D1 M3)
                           (join/< M1+T=M3 N0+1+N1=N2)
    <- less-implies-lt CMP N2>N1
    <- gt-implies-plus N2>N1 _ N0+1+N1=N2
    <- join-M-map/+-total* M1 N0 D2 M2 M3 M1+T=M3.

- : join-map/+-map/+-total* N1 D1 M1 N2 D2 M2 greater CMP (map/+ N2 D2 M3)
                           (join/> T+M2=M3 N3+1+N2=N1)
    <- greater-implies-gt CMP N1>N2
    <- gt-implies-plus N1>N2 _ N3+1+N2=N1
    <- join-map/+-M-total* N3 D1 M1 M2 M3 T+M2=M3.

- : join-M-map/+-total* map/0 N2 D2 M2 (map/+ N2 D2 M2) join/L.

- : join-M-map/+-total* (map/+ N1 D1 M1) N2 D2 M2 M3 A
    <- nat`compare-total* N1 N2 C CMP
    <- join-map/+-map/+-total* N1 D1 M1 N2 D2 M2 C CMP M3 A.

- : join-map/+-M-total* N1 D1 M1 map/0 (map/+ N1 D1 M1) join/R.

- : join-map/+-M-total* N1 D1 M1 (map/+ N2 D2 M2) M3 A
    <- nat`compare-total* N1 N2 C CMP
    <- join-map/+-map/+-total* N1 D1 M1 N2 D2 M2 C CMP M3 A.

%worlds (WORLDS) (join-total* _ _ _ _)
           (join-M-map/+-total* _ _ _ _ _ _)
           (join-map/+-M-total* _ _ _ _ _ _)
           (join-map/+-map/+-total* _ _ _ _ _ _ _ _ _ _).

%total [ (M1a M1b M1c M1d) (M2a M2b M2c M2d) ]
        (join-total* M1d M2d _ _)
        (join-M-map/+-total* M1c _ _ M2c _ _)
        (join-map/+-M-total* _ _ M1b M2b _ _)
	(join-map/+-map/+-total* _ _ M1a _ _ M2a _ _ _ _).
               
%abbrev join-total = join-total* _ _ _.

%{%
#endif /* DATA_JOIN_TOTAL_STAR */ 
%}%

%theorem disjoint-join-total :
	forall* {M1} {M2} 
	forall {D:disjoint M1 M2}
	exists {M3} {A:join M1 M2 M3}
	true.

- : disjoint-join-total disjoint/L _ join/L. 

- : disjoint-join-total disjoint/R _ join/R.

- : disjoint-join-total (disjoint/< D P) _ (join/< J P)
    <- disjoint-join-total D _ J.

- : disjoint-join-total (disjoint/> D P) _ (join/> J P)
    <- disjoint-join-total D _ J.

%worlds (WORLDS) (disjoint-join-total _ _ _).
%total (D) (disjoint-join-total D _ _).


%theorem disjoint-join-commutative :
        forall* {M1} {M2} {M3}
        forall  {D: disjoint M1 M2}
                {J: join M1 M2 M3}
        exists  {J: join M2 M1 M3}
        true.

- : disjoint-join-commutative _ join/L join/R.

- : disjoint-join-commutative _ join/R join/L.

- : disjoint-join-commutative D (join/= _ _ nat`eq/) J
    <- disjoint/=-contradiction D nat`eq/ F
    <- false-implies-join F J.

- : disjoint-join-commutative D (join/< J1 P) (join/> J1P P)
    <- disjoint/<-inversion D P D1
    <- disjoint-join-commutative D1 J1 J1P.

- : disjoint-join-commutative D (join/> J1 P) (join/< J1P P)
    <- disjoint/>-inversion D P D1
    <- disjoint-join-commutative D1 J1 J1P.

%worlds () (disjoint-join-commutative _ _ _).
%total (J) (disjoint-join-commutative _ J _).


%theorem join-associative :
	forall* {M1} {M2} {M3} {M4} {M7}
	forall {A12:join M1 M2 M3} {A34:join M3 M4 M7}
	exists {M6} {A24:join M2 M4 M6} {A16:join M1 M6 M7}
	true.

%theorem join-empty-implies-empty :
	forall* {M1} {M2}
	forall {A:join M1 M2 map/0}
	exists {E1:eq M1 map/0} {E2:eq M2 map/0}
	true.

- : join-empty-implies-empty join/L eq/ eq/.

- : join-empty-implies-empty join/R eq/ eq/.

%worlds (WORLDS) (join-empty-implies-empty _ _ _).
%total { } (join-empty-implies-empty _ _ _).


%theorem join-preserves-disjoint* :
	forall* {M1} {M2} {M3} {M4}
	forall {D1:disjoint M1 M4} {D2:disjoint M2 M4}
	       {A:join M1 M2 M3}
	exists {D3:disjoint M3 M4}
	true.

% a lemma that counts the size of maps to help prove termination
%theorem join-preserves-disjoint*/L :
	forall* {M1} {M2} {M3} {M4}
	forall {S1} {S2} {SZ1:size M1 S1} {SZ2:size M2 S2}
               {D1:disjoint M1 M4} {D2:disjoint M2 M4}
	       {A:join M1 M2 M3}
	exists {D3:disjoint M3 M4}
	true.

- : join-preserves-disjoint* D1 D2 J D3
    <- size-total SZ1
    <- size-total SZ2
    <- join-preserves-disjoint*/L _ _ SZ1 SZ2 D1 D2 J D3.

- : join-preserves-disjoint*/L _ _ _ _ disjoint/R _ _ disjoint/R.

- : join-preserves-disjoint*/L _ _ _ _ _ disjoint/R _ disjoint/R.

- : join-preserves-disjoint*/L _ _ _ _ _ D join/L D.

- : join-preserves-disjoint*/L _ _ _ _ D _ join/R D.

- : join-preserves-disjoint*/L (s S1) (s S2) (size/+ SZ1) (size/+ SZ2)
                               (disjoint/< D1 N5+1+N1=N4) D2X 
                               (join/= J _ nat`eq/)
                               (disjoint/< D3 N5+1+N1=N4)
    <- disjoint/<-inversion D2X N5+1+N1=N4 D2
    <- join-preserves-disjoint*/L S1 S2 SZ1 SZ2 D1 D2 J D3.

- : join-preserves-disjoint*/L (s S1) (s S2) (size/+ SZ1) (size/+ SZ2)
                               (disjoint/< D1 N5+1+N1=N4) D2X
                               (join/> J N3+1+N2=N1)
                               (disjoint/< D3 N6+1+N2=N4)
    <- plus-swap-succ N3+1+N2=N1 N3+N2+1=N1
    <- plus-associative-converse N3+N2+1=N1 N5+1+N1=N4 N6 N5+1+N3=N6 N6+N2+1=N4
    <- plus-swap-succ-converse N6+N2+1=N4 N6+1+N2=N4
    <- disjoint/<-inversion D2X N6+1+N2=N4 D2
    <- plus-swap-succ N5+1+N3=N6 N5+N3+1=N6
    <- plus-commutative N5+N3+1=N6 N3+1+N5=N6
    <- shift-right-preserves-disjoint D1 (shift/+ N3+1+N5=N6) D1<<N3
    <- join-preserves-disjoint*/L (s S1) S2 (size/+ SZ1) SZ2 D1<<N3 D2 J D3.

- : join-preserves-disjoint*/L (s S1) (s S2) (size/+ SZ1) (size/+ SZ2)
                               (disjoint/> D1 N5+1+N4=N1) D2X 
                               (join/= J DJ nat`eq/)
                               (disjoint/> D3 N5+1+N4=N1)
    <- disjoint/>-inversion D2X N5+1+N4=N1 D2
    <- join-preserves-disjoint*/L _ _ (size/+ SZ1) (size/+ SZ2) 
                                  D1 D2 (join/= J DJ nat`eq/) D3.

- : join-preserves-disjoint*/L (s S1) (s S2) (size/+ SZ1) (size/+ SZ2)
                               (disjoint/> D1 N5+1+N4=N1) D2X
                               (join/< J N0+1+N1=N2)
                               (disjoint/> D3 N5+1+N4=N1)
    <- plus-swap-succ N5+1+N4=N1 N5+N4+1=N1
    <- plus-associative-converse N5+N4+1=N1 N0+1+N1=N2 N6 N0+1+N5=N6 N6+N4+1=N2
    <- plus-swap-succ-converse N6+N4+1=N2 N6+1+N4=N2
    <- disjoint/>-inversion D2X N6+1+N4=N2 D2
    <- join-preserves-disjoint*/L _ _ (size/+ SZ1) (size/+ SZ2) 
                                  D1 D2 (join/< J N0+1+N5=N6) D3.

- : join-preserves-disjoint*/L (s S1) (s S2) (size/+ SZ1) (size/+ SZ2)
                               D1X (disjoint/< D2 N6+1+N2=N4) 
                               (join/< J N0+1+N1=N2)
                               (disjoint/< D3 N5+1+N1=N4)
    <- plus-swap-succ N0+1+N1=N2 N0+N1+1=N2
    <- plus-associative-converse N0+N1+1=N2 N6+1+N2=N4 N5 N6+1+N0=N5 N5+N1+1=N4
    <- plus-swap-succ-converse N5+N1+1=N4 N5+1+N1=N4
    <- disjoint/<-inversion D1X N5+1+N1=N4 D1
    <- plus-swap-succ N6+1+N0=N5 N6+N0+1=N5
    <- plus-commutative N6+N0+1=N5 N0+1+N6=N5
    <- shift-right-preserves-disjoint D2 (shift/+ N0+1+N6=N5) D2<<N0
    <- join-preserves-disjoint*/L S1 (s S2) SZ1 (size/+ SZ2) D1 D2<<N0 J D3.

- : join-preserves-disjoint*/L (s S1) (s S2) (size/+ SZ1) (size/+ SZ2)
                               D1X (disjoint/> D2 N6+1+N4=N2)
                               (join/> J N3+1+N2=N1)
                               (disjoint/> D3 N6+1+N4=N2)
    <- plus-swap-succ N6+1+N4=N2 N6+N4+1=N2
    <- plus-associative-converse N6+N4+1=N2 N3+1+N2=N1 N5 N3+1+N6=N5 N5+N4+1=N1
    <- plus-swap-succ-converse N5+N4+1=N1 N5+1+N4=N1
    <- disjoint/>-inversion D1X N5+1+N4=N1 D1
    <- join-preserves-disjoint*/L _ _ (size/+ SZ1) (size/+ SZ2) 
                                  D1 D2 (join/> J N3+1+N6=N5) D3.

- : join-preserves-disjoint*/L (s S1) (s S2) (size/+ SZ1) (size/+ SZ2)
                               (disjoint/< D1 N5+1+N1=N4)
                               (disjoint/> D2 N6+1+N4=N2) JX D3X
    <- plus-swap-succ N5+1+N1=N4 N5+N1+1=N4
    <- plus-associative-converse N5+N1+1=N4 N6+1+N4=N2 N0 N6+1+N5=N0 N0+N1+1=N2
    <- plus-swap-succ-converse N0+N1+1=N2 N0+1+N1=N2
    <- join/<-inversion JX N0+1+N1=N2 _ J M=M113
    <- eq-symmetric M=M113 M113=M
    <- join-preserves-disjoint*/L S1 (s S2) SZ1 (size/+ SZ2)
                                  D1 (disjoint/> D2 N6+1+N5=N0) J D3
    <- disjoint-respects-eq (disjoint/< D3 N5+1+N1=N4) M113=M eq/ D3X.

- : join-preserves-disjoint*/L (s S1) (s S2) (size/+ SZ1) (size/+ SZ2)
                               (disjoint/> D1 N5+1+N4=N1)
                               (disjoint/< D2 N6+1+N2=N4) JX D3X
    <- plus-swap-succ N6+1+N2=N4 N6+N2+1=N4
    <- plus-associative-converse N6+N2+1=N4 N5+1+N4=N1 N3 N5+1+N6=N3 N3+N2+1=N1
    <- plus-swap-succ-converse N3+N2+1=N1 N3+1+N2=N1
    <- join/>-inversion JX N3+1+N2=N1 _ J M=M223
    <- eq-symmetric M=M223 M223=M
    <- join-preserves-disjoint*/L _ _ (size/+ SZ1) SZ2
                                  (disjoint/> D1 N5+1+N6=N3) D2 J D3
    <- disjoint-respects-eq (disjoint/< D3 N6+1+N2=N4) M223=M eq/ D3X.

%worlds (WORLDS) (join-preserves-disjoint*/L _ _ _ _ _ _ _ _).
%total {S1 S2 D1} (join-preserves-disjoint*/L S1 S2 _ _ D1 _ _ _).

%worlds (WORLDS) (join-preserves-disjoint* _ _ _ _).
%total { } (join-preserves-disjoint* _ _ _ _).


%abbrev join-left-preserves-disjoint* = join-preserves-disjoint*.

%theorem join-right-preserves-disjoint* :
	forall* {M1} {M2} {M3} {M0}
	forall {D1:disjoint M0 M1} {D2:disjoint M0 M2}
	       {A:join M1 M2 M3}
	exists {D3:disjoint M0 M3}
	true.

- : join-right-preserves-disjoint* D1 D2 U D3
    <- disjoint-symmetric D1 D1s
    <- disjoint-symmetric D2 D2s
    <- join-left-preserves-disjoint* D1s D2s U D3s
    <- disjoint-symmetric D3s D3.

%worlds (WORLDS) (join-right-preserves-disjoint* _ _ _ _).
%total { } (join-right-preserves-disjoint* _ _ _ _).


%theorem join-preserves-disjoint-converse:
	forall* {M1} {M2} {M3} {M4}
	forall {D3:disjoint M3 M4}
	       {A:join M1 M2 M3}
	exists {D1:disjoint M1 M4} {D2:disjoint M2 M4}
	true.

- : join-preserves-disjoint-converse
	D join/L disjoint/L D.

- : join-preserves-disjoint-converse
	D join/R D disjoint/L.

- : join-preserves-disjoint-converse
	disjoint/R _
	disjoint/R disjoint/R.

- : join-preserves-disjoint-converse
	(disjoint/< D P)
	(join/= J _ nat`eq/)
	(disjoint/< D1 P) (disjoint/< D2 P)
    <- join-preserves-disjoint-converse D J D1 D2.

- : join-preserves-disjoint-converse
        (disjoint/< D P1)
        (join/< J P2)
	(disjoint/< D1 P1) D2S
    <- join-preserves-disjoint-converse D J D1 D2
    <- plus-swap-succ P1 P1s
    <- plus-commutative P1s P1sc
    <- plus-swap-succ P2 P2s
    <- plus-commutative P2s P2sc
    <- shift-preserves-disjoint D2 (shift/+ P2sc) (shift/+ P1sc) D2S.

- : join-preserves-disjoint-converse
        (disjoint/< D P1)
        (join/> J P2)
	D1S (disjoint/< D2 P1)
    <- join-preserves-disjoint-converse D J D1 D2
    <- plus-swap-succ P1 P1s
    <- plus-commutative P1s P1sc
    <- plus-swap-succ P2 P2s
    <- plus-commutative P2s P2sc
    <- shift-preserves-disjoint D1 (shift/+ P2sc) (shift/+ P1sc) D1S.

- : join-preserves-disjoint-converse
	(disjoint/> D P1)
	(join/= J DE nat`eq/)
	D1S D2S
    <- join-preserves-disjoint-converse D (join/= J DE nat`eq/) D1 D2
    <- plus-swap-succ P1 P1s
    <- plus-commutative P1s P1sc
    <- shift-left-preserves-disjoint D1 (shift/+ P1sc) D1S
    <- shift-left-preserves-disjoint D2 (shift/+ P1sc) D2S.

- : join-preserves-disjoint-converse
	(disjoint/> D P1)
	(join/< J P2)
        D1S D2S
    <- plus-swap-succ P1 P1s
    <- plus-associative-converse P1s P2 _ P3 P4s
    <- join-preserves-disjoint-converse D (join/< J P3) D1 D2
    <- plus-commutative P1s P1sc
    <- plus-commutative P4s P4sc
    <- shift-left-preserves-disjoint D1 (shift/+ P1sc) D1S
    <- shift-left-preserves-disjoint D2 (shift/+ P4sc) D2S.

- : join-preserves-disjoint-converse
	(disjoint/> D P1)
	(join/> J P2)
	D1S D2S
    <- plus-swap-succ P1 P1s
    <- plus-associative-converse P1s P2 _ P3 P4s
    <- join-preserves-disjoint-converse D (join/> J P3) D1 D2
    <- plus-commutative P1s P1sc
    <- plus-commutative P4s P4sc
    <- shift-left-preserves-disjoint D1 (shift/+ P4sc) D1S
    <- shift-left-preserves-disjoint D2 (shift/+ P1sc) D2S.

%worlds (WORLDS) (join-preserves-disjoint-converse _ _ _ _).
%total (D) (join-preserves-disjoint-converse D _ _ _).


%theorem disjoint-join-right-cancels :
        forall* {A1} {B1} {C1} {A2} {B2} {C2}
        forall  {D1: disjoint A1 B1}
                {J1: join A1 B1 C1}
                {D2: disjoint A2 B2}
                {J2: join A2 B2 C2}
                {EB: eq B1 B2}
                {EC: eq C1 C2}
        exists  {EA: eq A1 A2}
        true.

%theorem disjoint-join-right-cancels/L :
	forall*	{A} {B} {C}
	forall	{D: disjoint A B}
		{J: join A B C}
		{E: eq B C}
	exists	{E: eq map/0 A}
	true.

- : disjoint-join-right-cancels/L A^B A+B=B eq/ Z=A
    <- disjoint-symmetric A^B B^A
    <- join-preserves-disjoint-converse B^A A+B=B A^A _
    <- disjoint-anti-reflexive A^A Z=A.

%worlds (WORLDS) (disjoint-join-right-cancels/L _ _ _ _).
%total { } (disjoint-join-right-cancels/L _ _ _ _).

- : disjoint-join-right-cancels
        disjoint/L J1 A2^B J2 eq/ eq/ Z=A2
    <- join-deterministic J1 join/L eq/ eq/ C=B
    <- disjoint-join-right-cancels/L A2^B J2 C=B Z=A2.

- : disjoint-join-right-cancels
        A1^B J1 disjoint/L J2 eq/ eq/ A1=Z
    <- join-deterministic J2 join/L eq/ eq/ C=B
    <- disjoint-join-right-cancels/L A1^B J1 C=B Z=A1
    <- eq-symmetric Z=A1 A1=Z.

- : disjoint-join-right-cancels
        disjoint/R J1 _ J2 eq/ eq/ A1=A2
    <- join-deterministic J1 join/R eq/ eq/ A1=C
    <- join-deterministic join/R J2 eq/ eq/ C=A2
    <- eq-transitive A1=C C=A2 A1=A2.

- : disjoint-join-right-cancels
        (disjoint/< D1P P1) J1 (disjoint/< D2P P2) J2 
        eq/ eq/ A111=A222
    <- join/<-inversion J1 P1 _ J1P EQ1
    <- join/<-inversion J2 P2 _ J2P EQ2
    <- eq-symmetric EQ1 EQ1s
    <- eq-transitive EQ1s EQ2 C114=C225
    <- map/+-preserves-eq-converse C114=C225 N1=N2 D1=D2 C4=C5
    <- plus-right-cancels P1 P2 N1=N2 nat`eq/ SN4=SN5
    <- succ-cancels SN4=SN5 N4=N5
    <- map/+-preserves-eq N4=N5 data`eq/ eq/ B4=B5
    <- disjoint-join-right-cancels D1P J1P D2P J2P B4=B5 C4=C5 A1=A2
    <- map/+-preserves-eq N1=N2 D1=D2 A1=A2 A111=A222.

- : disjoint-join-right-cancels
        (disjoint/< D1P P1) J1 (disjoint/> D2P P2) J2 
        eq/ eq/ EQ
    <- join/<-inversion J1 P1 _ J1P EQ1
    <- join/>-inversion J2 P2 _ J2P EQ2
    <- eq-symmetric EQ1 EQ1s
    <- eq-transitive EQ1s EQ2 C114=C225
    <- map/+-preserves-eq-converse C114=C225 N1=N2 _ _
    <- plus-implies-gt P1 nat`eq/ N2>N1
    <- gt-respects-eq N2>N1 nat`eq/ N1=N2 N2>N2
    <- gt-anti-reflexive N2>N2 F
    <- false-implies-eq F EQ.

- : disjoint-join-right-cancels
        (disjoint/> D1P P1) J1 (disjoint/< D2P P2) J2 
        eq/ eq/ EQ
    <- join/>-inversion J1 P1 _ J1P EQ1
    <- join/<-inversion J2 P2 _ J2P EQ2
    <- eq-symmetric EQ1 EQ1s
    <- eq-transitive EQ1s EQ2 C114=C225
    <- map/+-preserves-eq-converse C114=C225 N1=N2 _ _
    <- plus-implies-gt P2 nat`eq/ N1>N2
    <- gt-respects-eq N1>N2 N1=N2 nat`eq/ N2>N2
    <- gt-anti-reflexive N2>N2 F
    <- false-implies-eq F EQ.

- : disjoint-join-right-cancels
        (disjoint/> D1P P1) J1 (disjoint/> D2P P2) J2 
        eq/ eq/ A111=A222
    <- join/>-inversion J1 P1 _ J1P EQ1
    <- join/>-inversion J2 P2 _ J2P EQ2
    <- eq-symmetric EQ1 EQ1s
    <- eq-transitive EQ1s EQ2 C114=C115
    <- map/+-preserves-eq-converse C114=C115 _ _ C4=C5
    <- disjoint-join-right-cancels D1P J1P D2P J2P eq/ C4=C5 A411=A511
    <- plus-swap-succ P1 P1s
    <- plus-commutative P1s P1sc
    <- plus-swap-succ P2 P2s
    <- plus-commutative P2s P2sc
    <- shift-deterministic 
        (shift/+ P1sc) (shift/+ P2sc) nat`eq/ A411=A511 A111=A222.

%worlds () (disjoint-join-right-cancels _ _ _ _ _ _ _).
%total (D) (disjoint-join-right-cancels D _ _ _ _ _ _).


%theorem disjoint-join-left-cancels :
        forall* {A1} {B1} {C1} {A2} {B2} {C2}
        forall  {D1: disjoint A1 B1}
                {J1: join A1 B1 C1}
                {D2: disjoint A2 B2}
                {J2: join A2 B2 C2}
                {EB: eq A1 A2}
                {EC: eq C1 C2}
        exists  {EA: eq B1 B2}
        true.

- : disjoint-join-left-cancels D1 J1 D2 J2 EB EC EA
    <- disjoint-join-commutative D1 J1 J1c
    <- disjoint-join-commutative D2 J2 J2c
    <- disjoint-symmetric D1 D1s
    <- disjoint-symmetric D2 D2s
    <- disjoint-join-right-cancels D1s J1c D2s J2c EB EC EA.

%worlds () (disjoint-join-left-cancels _ _ _ _ _ _ _).
%total { } (disjoint-join-left-cancels _ _ _ _ _ _ _).


%theorem shift-left-preserves-join :
	forall* {N} {D} {M1} {M2} {M3} {SM1}
	forall {A:join M1 M2 M3} {S1:shift N M1 SM1}
        exists {SA:join SM1 (map/+ N D M2) (map/+ N D M3)}
        true.

- : shift-left-preserves-join join/L shift/0 join/L.

- : shift-left-preserves-join join/R shift/0 join/L.

- : shift-left-preserves-join M111+M2=M3 (shift/+ N+1+N1=N1P)
                              (join/> M111+M2=M3 N1+1+N=N1P)
    <- plus-swap-succ N+1+N1=N1P N+N1+1=N1P
    <- plus-commutative N+N1+1=N1P N1+1+N=N1P.

%worlds (WORLDS) (shift-left-preserves-join _ _ _).
%total { } (shift-left-preserves-join _ _ _).


%theorem shift-left-preserves-join-converse :
	forall* {N} {D} {M1} {M2} {SM1} {SM3}
	forall {SA:join SM1 (map/+ N D M2) SM3} {S1:shift N M1 SM1}
        exists {M3} {A:join M1 M2 M3} {E:eq (map/+ N D M3) SM3}
	true.

- : shift-left-preserves-join-converse join/L shift/0 _ join/L eq/.

- : shift-left-preserves-join-converse M111+M222=SM3 (shift/+ N2+1+N3=N1) M3
                                       M311+M2=M3 M223=SM3
    <- plus-swap-succ N2+1+N3=N1 N2+N3+1=N1
    <- plus-commutative N2+N3+1=N1 N3+1+N2=N1
    <- join/>-inversion M111+M222=SM3 N3+1+N2=N1 M3 M311+M2=M3 SM3=M223
    <- eq-symmetric SM3=M223 M223=SM3.

%worlds (WORLDS) (shift-left-preserves-join-converse _ _ _ _ _).
%total { } (shift-left-preserves-join-converse _ _ _ _ _).


%theorem shift-right-preserves-join :
	forall* {N} {D} {M1} {M2} {M3} {SM2}
	forall {A:join M1 M2 M3} {S2:shift N M2 SM2}
        exists {SA:join (map/+ N D M1) SM2 (map/+ N D M3)}
	true.

- : shift-right-preserves-join join/L shift/0 join/R.

- : shift-right-preserves-join join/R shift/0 join/R.

- : shift-right-preserves-join M1+M222=M3 (shift/+ N+1+N2=N2P) 
                               (join/< M1+M222=M3 N2+1+N=N2P)
    <- plus-swap-succ N+1+N2=N2P N+N2+1=N2P
    <- plus-commutative N+N2+1=N2P N2+1+N=N2P.

%worlds (WORLDS) (shift-right-preserves-join _ _ _).
%total { } (shift-right-preserves-join _ _ _).


%theorem shift-right-preserves-join-converse :
	forall* {N} {D} {M1} {M2} {SM2} {SM3}
	forall {SA:join (map/+ N D M1) SM2 SM3} {S2:shift N M2 SM2}
        exists {M3} {A:join M1 M2 M3} {E:eq (map/+ N D M3) SM3}
	true.

- : shift-right-preserves-join-converse join/R shift/0 _ join/R eq/.

- : shift-right-preserves-join-converse M111+M322=SM3 (shift/+ N1+1+N2=N3)
                                        M3 M1+M222=M3 M133=SM3
    <- plus-swap-succ N1+1+N2=N3 N1+N2+1=N3
    <- plus-commutative N1+N2+1=N3 N2+1+N1=N3
    <- join/<-inversion M111+M322=SM3 N2+1+N1=N3 M3 M1+M222=M3 SM3=M133
    <- eq-symmetric SM3=M133 M133=SM3.

%worlds (WORLDS) (shift-right-preserves-join-converse _ _ _ _ _).
%total { } (shift-right-preserves-join-converse _ _ _ _ _).


%theorem shift-preserves-join* :
	forall* {N} {M1} {M2} {M3} {SM1} {SM2} {SM3}
	forall {A:join M1 M2 M3} 
               {S1:shift N M1 SM1} {S2:shift N M2 SM2} {S3:shift N M3 SM3}
	exists {SA:join SM1 SM2 SM3}
	true.

- : shift-preserves-join* join/L shift/0 M2<<N=SM2 M2<<N=SM3 Z+SM2=SM3
    <- shift-deterministic M2<<N=SM2 M2<<N=SM3 nat`eq/ eq/ SM2=SM3
    <- join-respects-eq join/L eq/ eq/ SM2=SM3 Z+SM2=SM3.

- : shift-preserves-join* join/R M1<<N=SM1 shift/0 M1<<N=SM3 SM1+0=SM3
    <- shift-deterministic M1<<N=SM1 M1<<N=SM3 nat`eq/ eq/ SM1=SM3
    <- join-respects-eq join/R eq/ eq/ SM1=SM3 SM1+0=SM3.

- : shift-preserves-join* (join/= M1+M2=M3 D1+D2=D3 nat`eq/) 
                          (shift/+ N+1+N1=N4) (shift/+ N+1+N1=N5) 
                          (shift/+ N+1+N1=N6)
                          M411+M522=M633
    <- plus-deterministic N+1+N1=N4 N+1+N1=N5 nat`eq/ nat`eq/ N4=N5
    <- plus-deterministic N+1+N1=N4 N+1+N1=N6 nat`eq/ nat`eq/ N4=N6
    <- map/+-preserves-eq N4=N6 data`eq/ eq/ M433=M633
    <- join-respects-eq (join/= M1+M2=M3 D1+D2=D3 N4=N5) eq/ eq/ M433=M633
                        M411+M522=M633.

- : shift-preserves-join* (join/< M1+M022=M3 N0+1+N1=N2)
                          (shift/+ N+1+N1=N4) (shift/+ N+1+N2=N5) 
                          (shift/+ N+1+N1=N6) M411+M522=M613
    <- plus-deterministic N+1+N1=N4 N+1+N1=N6 nat`eq/ nat`eq/ N4=N6
    <- plus-swap-succ N+1+N1=N4 N+N1+1=N4
    <- plus-commutative N+N1+1=N4 N1+1+N=N4
    <- plus-commutative N0+1+N1=N2 N1+N0+1=N2
    <- plus-associative-converse* N1+N0+1=N2 N+1+N2=N5 N+1+N1=N4 N4+N0+1=N5
    <- plus-commutative N4+N0+1=N5 N0+1+N4=N5
    <- map/+-preserves-eq N4=N6 data`eq/ eq/ M433=M613
    <- join-respects-eq (join/< M1+M022=M3 N0+1+N4=N5) eq/ eq/ M433=M613
                        M411+M522=M613.

- : shift-preserves-join* (join/> M311+M2=M3 N3+1+N2=N1)
                          (shift/+ N+1+N1=N4) (shift/+ N+1+N2=N5) 
                          (shift/+ N+1+N2=N6) M411+M522=M623
    <- plus-deterministic N+1+N2=N5 N+1+N2=N6 nat`eq/ nat`eq/ N5=N6
    <- plus-swap-succ N+1+N2=N5 N+N2+1=N5
    <- plus-commutative N+N2+1=N5 N2+1+N=N5
    <- plus-commutative N3+1+N2=N1 N2+N3+1=N1
    <- plus-associative-converse* N2+N3+1=N1 N+1+N1=N4 N+1+N2=N5 N5+N3+1=N4
    <- plus-commutative N5+N3+1=N4 N3+1+N5=N4
    <- map/+-preserves-eq N5=N6 data`eq/ eq/ M523=M623
    <- join-respects-eq (join/> M311+M2=M3 N3+1+N5=N4) eq/ eq/ M523=M623
                        M411+M522=M623.

%worlds (WORLDS) (shift-preserves-join* _ _ _ _ _).
%total { } (shift-preserves-join* _ _ _ _ _).


%theorem shift-preserves-join :
	forall* {N} {M1} {M2} {M3} {SM1} {SM2}
	forall  {A:join M1 M2 M3} 
                {S1:shift N M1 SM1} {S2:shift N M2 SM2} 
	exists	{SM3} 
		{S3:shift N M3 SM3}
		{SA:join SM1 SM2 SM3}
	true.

- : shift-preserves-join J S1 S2 _ S3 JP
    <- shift-total S3
    <- shift-preserves-join* J S1 S2 S3 JP.

%worlds (WORLDS) (shift-preserves-join _ _ _ _ _ _).
%total { } (shift-preserves-join _ _ _ _ _ _).


%theorem shift-preserves-join-converse :
	forall* {N} {M1} {M2} {SM1} {SM2} {SM3}
	forall {SA:join SM1 SM2 SM3}
               {S1:shift N M1 SM1} {S2:shift N M2 SM2} 
	exists {M3} {A:join M1 M2 M3} {S3:shift N M3 SM3}
	true.

- : shift-preserves-join-converse join/L shift/0 M2<<N=SM2 _ join/L M2<<N=SM2.

- : shift-preserves-join-converse join/R M1<<N=SM1 shift/0 _ join/R M1<<N=SM1.

- : shift-preserves-join-converse (join/= M1+M2=M3 D1+D2=D3 nat`eq/) 
                                  (shift/+ N+1+N1=N4) (shift/+ N+1+N1P=N4)
                                  (map/+ N1 D3 M3) M111+M122P=M133
                                  (shift/+ N+1+N1=N4)
    <- plus-left-cancels N+1+N1=N4 N+1+N1P=N4 nat`eq/ nat`eq/ N1=N1P
    <- map/+-preserves-eq N1=N1P data`eq/ eq/ M122=M122P
    <- join-respects-eq (join/= M1+M2=M3 D1+D2=D3 nat`eq/) eq/ M122=M122P eq/
                        M111+M122P=M133.

- : shift-preserves-join-converse (join/< M1+M055=M3 N0+1+N4=N5)
                                  (shift/+ N+1+N1=N4) (shift/+ N+1+N2=N5) _
				  (join/< M1+M055=M3 N0+1+N1=N2)
				  (shift/+ N+1+N1=N4)
    <- plus-commutative N+1+N1=N4 N1+N+1=N4
    <- plus-swap-succ-converse N1+N+1=N4 N1+1+N=N4
    <- plus-associative-converse N1+N+1=N4 N0+1+N4=N5 N2P N0+1+N1=N2P N2P+N+1=N5
    <- plus-commutative N+1+N2=N5 N2+N+1=N5
    <- plus-right-cancels N2P+N+1=N5 N2+N+1=N5 nat`eq/ nat`eq/ N2P=N2
    <- plus-respects-eq N0+1+N1=N2P nat`eq/ nat`eq/ N2P=N2 N0+1+N1=N2.
                                  
- : shift-preserves-join-converse (join/> M611+M2=M3 N6+1+N5=N4)
                                  (shift/+ N+1+N1=N4) (shift/+ N+1+N2=N5) _
                                  (join/> M611+M2=M3 N6+1+N2=N1)
                                  (shift/+ N+1+N2=N5)
    <- plus-commutative N+1+N2=N5 N2+N+1=N5
    <- plus-swap-succ-converse N2+N+1=N5 N2+1+N=N5
    <- plus-associative-converse N2+N+1=N5 N6+1+N5=N4 N1P N6+1+N2=N1P N1P+N+1=N4
    <- plus-commutative N+1+N1=N4 N1+N+1=N4
    <- plus-right-cancels N1P+N+1=N4 N1+N+1=N4 nat`eq/ nat`eq/ N1P=N1
    <- plus-respects-eq N6+1+N2=N1P nat`eq/ nat`eq/ N1P=N1 N6+1+N2=N1.

%worlds (WORLDS) (shift-preserves-join-converse _ _ _ _ _ _).
%total { } (shift-preserves-join-converse _ _ _ _ _ _).


% arguably: this should be the non-starred version
%theorem shift-preserves-join-converse* :
	forall* {N} {M3} {SM1} {SM2} {SM3}
	forall {SA:join SM1 SM2 SM3}  {S3:shift N M3 SM3}
	exists {M1} {M2}
               {S1:shift N M1 SM1} {S2:shift N M2 SM2} 
	       {A:join M1 M2 M3}
	true.

- : shift-preserves-join-converse* U shift/0 _ _ shift/0 shift/0 join/L.

- : shift-preserves-join-converse* join/L S _ _ shift/0 S join/L.

- : shift-preserves-join-converse* join/R S _ _ S shift/0 join/R.

- : shift-preserves-join-converse* (join/= U DJ nat`eq/) (shift/+ P) _ _
	(shift/+ P) (shift/+ P) (join/= U DJ nat`eq/).

- : shift-preserves-join-converse* (join/< U D+1+A=B) 
	(shift/+ N+1+Y=A) _ _
	(shift/+ N+1+Y=A)
	(shift/+ N+1+Z=B)
	(join/< U D+1+Y=Z)
    <- plus-commutative N+1+Y=A Y+N+1=A
    <- plus-associative-converse Y+N+1=A D+1+A=B Z D+1+Y=Z Z+N+1=B
    <- plus-commutative Z+N+1=B N+1+Z=B.

- : shift-preserves-join-converse* (join/> U D+1+B=A)
	(shift/+ N+1+Y=B) _ _
	(shift/+ N+1+Z=A)
	(shift/+ N+1+Y=B)
	(join/> U D+1+Y=Z)
    <- plus-commutative N+1+Y=B Y+N+1=B
    <- plus-associative-converse Y+N+1=B D+1+B=A Z D+1+Y=Z Z+N+1=A
    <- plus-commutative Z+N+1=A N+1+Z=A.

%worlds (WORLDS) (shift-preserves-join-converse* _ _ _ _ _ _ _).
%total { } (shift-preserves-join-converse* _ _ _ _ _ _ _).


%theorem shift-preserves-join-converse** :
	forall* {N} {M1} {M2} {M3} {SM1} {SM2} {SM3}
	forall {SA:join SM1 SM2 SM3}  {S3:shift N M3 SM3}
               {S1:shift N M1 SM1} {S2:shift N M2 SM2} 
	exists {A:join M1 M2 M3}
	true.

- : shift-preserves-join-converse** JP S3 S1 S2 J
    <- shift-preserves-join-converse JP S1 S2 _ JX S3X
    <- shift-cancels S3X S3 nat`eq/ eq/ M3X=M3
    <- join-respects-eq JX eq/ eq/ M3X=M3 J.

%worlds (WORLDS) (shift-preserves-join-converse** _ _ _ _ _).
%total { } (shift-preserves-join-converse** _ _ _ _ _).


%{%
#ifdef DATA_JOIN_COMMUTATIVE
%}%

%theorem join-commutative :
	forall* {M1} {M2} {M3}
	forall {A:join M1 M2 M3}
	exists {AP:join M2 M1 M3}
	true.

- : join-commutative join/L join/R.

- : join-commutative join/R join/L.

- : join-commutative (join/= M1+M2=M3 D1+D2=D3 nat`eq/) 
                     (join/= M2+M1=M3 D2+D1=D3 nat`eq/)
    <- data`join-commutative D1+D2=D3 D2+D1=D3
    <- join-commutative M1+M2=M3 M2+M1=M3.

- : join-commutative (join/< M1+MT=M3 N0+1+N1=N2) (join/> MT+M1=M3 N0+1+N1=N2)
    <- join-commutative M1+MT=M3 MT+M1=M3.

- : join-commutative (join/> MT+M2=M3 N3+1+N2=N1) (join/< M2+MT=M3 N3+1+N2=N1)
    <- join-commutative MT+M2=M3 M2+MT=M3.

%worlds (WORLDS) (join-commutative _ _).
%total (A) (join-commutative A _).

%{%
#endif /* DATA_JOIN_COMMUTATIVE */
%}%


%{%
#ifdef DATA_JOIN_ASSOCIATIVE
%}%

%theorem join-associative :
	forall* {M1} {M2} {M3} {M4} {M7}
	forall {A12:join M1 M2 M3} {A34:join M3 M4 M7}
	exists {M6} {A24:join M2 M4 M6} {A16:join M1 M6 M7}
	true.

%% a lemma

%theorem join-associative-join/<-join/< :
	forall* {N1} {D1} {M1} {N2} {D2} {M2} {N0} {N5} {N4} {D4} {M4} {M6} {M7}
	forall {PLUS012:nat`plus (s N0) N1 N2}
               {PLUS514:nat`plus (s N5) N1 N4}
               {JOIN246:join (map/+ N0 D2 M2) (map/+ N5 D4 M4) M6}
               {JOIN167:join M1 M6 M7}
        exists {M} {JOIN24: join (map/+ N2 D2 M2) (map/+ N4 D4 M4) M}
               {JOIN:join (map/+ N1 D1 M1) M (map/+ N1 D1 M7)}
        true.
               
- : join-associative join/L A _ A join/L.

- : join-associative A join/R _ join/R A.

- : join-associative join/R A _ join/L A.

- : join-associative (join/= M1+M2=M3 D1+D2=D3 nat`eq/)
                    (join/= M3+M4=M7 D3+D4=D7 nat`eq/) (map/+ _ D6 M6)
                    (join/= M2+M4=M6 D2+D4=D6 nat`eq/)
		    (join/= M1+M6=M7 D1+D6=D7 nat`eq/)
    <- data`join-associative D1+D2=D3 D3+D4=D7 D6 D2+D4=D6 D1+D6=D7
    <- join-associative M1+M2=M3 M3+M4=M7 M6 M2+M4=M6 M1+M6=M7.

- : join-associative (join/= M1+M2=M3 D1+D2=D3 nat`eq/)
                    (join/< M3+M044=M7 N0+1+N3=N4) (map/+ _ _ M6) 
                    (join/< M2+M044=M6 N0+1+N3=N4) 
                    (join/= M1+M6=M7 D1+D2=D3 nat`eq/)
    <- join-associative M1+M2=M3 M3+M044=M7 M6 M2+M044=M6 M1+M6=M7.

- : join-associative (join/= M1+M2=M3 D1+D2=D3 nat`eq/)
	            (join/> M533+M4=M7 N5+1+N4=N3) (map/+ _ _ M6)
                    (join/> M522+M4=M6 N5+1+N4=N3)
                    (join/> M511+M6=M7 N5+1+N4=N3)
    <- join-associative (join/= M1+M2=M3 D1+D2=D3 nat`eq/) M533+M4=M7
                       M6 M522+M4=M6 M511+M6=M7.

- : join-associative (join/< M1+M022=M3 N0+1+N1=N2)
                     (join/= M3+M4=M7 D1+D4=D7 nat`eq/) (map/+ _ _ M6)
                     (join/> M022+M3=M6 N0+1+N1=N2)
                     (join/= M1+M6=M7 D1+D4=D7 nat`eq/)
    <- join-associative M1+M022=M3 M3+M4=M7 M6 M022+M3=M6 M1+M6=M7.

%% the hardest of all 11 cases!

- : {M111+M=M117: join (map/+ N1 D1 M1) M (map/+ N1 D1 M7)}
    {M3+M544=M7: join M3 (map/+ N5 D4 M4) M7}
    {M1+M022=M3: join M1 (map/+ N0 D2 M2) M3}
    {M1+M6=M7: join M1 M6 M7}
    join-associative (join/< M1+M022=M3 N0+1+N1=N2)
       		     (join/< M3+M544=M7 N5+1+N1=N4) 
		     M M222+M444=M M111+M=M117
    <- join-associative M1+M022=M3 M3+M544=M7 M6 M022+M544=M6 M1+M6=M7
    <- join-associative-join/<-join/< 
       		N0+1+N1=N2 N5+1+N1=N4 M022+M544=M6 M1+M6=M7 
       		M M222+M444=M M111+M=M117.

- : join-associative-join/<-join/< N+1+N1=N2 N+1+N1=N4
                                   (join/= M2+M4=M6 D2+D4=D6 nat`eq/) 
                                   M1+M066=M7 (map/+ N2 D6 M6)
                                   (join/= M2+M4=M6 D2+D4=D6 N2=N4)
                                   (join/< M1+M066=M7 N+1+N1=N2)
    <- nat`plus-deterministic N+1+N1=N2 N+1+N1=N4 nat`eq/ nat`eq/ N2=N4.

- : join-associative-join/<-join/< N0+1+N1=N2 N5+1+N1=N4
                                   (join/< M2+M744=M6 N7+1+N0=N5)
                                   M1+M026=M7 (map/+ N2 D2 M6)
     				   (join/< M2+M744=M6 N7+1+N2=N4)
                                   (join/< M1+M026=M7 N0+1+N1=N2)
    <- nat`plus-swap-succ N5+1+N1=N4 N5+N1+1=N4
    <- nat`plus-swap-succ N0+1+N1=N2 N0+N1+1=N2
    <- nat`plus-associative* N7+1+N0=N5 N5+N1+1=N4 N0+N1+1=N2 N7+1+N2=N4.

- : join-associative-join/<-join/< N0+1+N1=N2 N5+1+N1=N4
                                   (join/> M722+M4=M6 N7+1+N5=N0)
                                   M1+M546=M7 (map/+ N4 D4 M6)
                                   (join/> M722+M4=M6 N7+1+N4=N2)
                                   (join/< M1+M546=M7 N5+1+N1=N4)
    <- nat`plus-swap-succ N0+1+N1=N2 N0+N1+1=N2
    <- nat`plus-swap-succ N5+1+N1=N4 N5+N1+1=N4
    <- nat`plus-associative* N7+1+N5=N0 N0+N1+1=N2 N5+N1+1=N4 N7+1+N4=N2.

%% and now we return to the main theorem

- : join-associative (join/< M1+M022=M3 N0+1+N1=N2)
		    (join/> M513+M4=M7 N5+1+N3=N1) (map/+ _ _ M6)
                    (join/> M622+M4=M6 N6+1+N3=N2)
                    (join/> M511+M6=M7 N5+1+N3=N1)
    <- nat`plus-swap-succ N5+1+N3=N1 N5+N3+1=N1
    <- nat`plus-associative-converse N5+N3+1=N1 N0+1+N1=N2 N6
                                     N0+1+N5=N6 N6+N3+1=N2
    <- nat`plus-swap-succ-converse N6+N3+1=N2 N6+1+N3=N2
    <- join-associative (join/< M1+M022=M3 N0+1+N5=N6) M513+M4=M7 
                       M6 M622+M4=M6 M511+M6=M7.

- : join-associative (join/> M311+M2=M3 N3+1+N2=N1)
                    (join/= M3+M4=M7 D2+D4=D7 nat`eq/) (map/+ _ _ M6)
                    (join/= M2+M4=M6 D2+D4=D7 nat`eq/)
                    (join/> M311+M6=M7 N3+1+N2=N1)
    <- join-associative M311+M2=M3 M3+M4=M7 M6 M2+M4=M6 M311+M6=M7.

- : join-associative (join/> M311+M2=M3 N3+1+N2=N1)
                    (join/< M3+M044=M7 N0+1+N2=N4) (map/+ _ _ M6)
                    (join/< M2+M044=M6 N0+1+N2=N4)
                    (join/> M311+M6=M7 N3+1+N2=N1)
    <- join-associative M311+M2=M3 M3+M044=M7 M6 M2+M044=M6 M311+M6=M7.

- : join-associative (join/> M311+M2=M3 N3+1+N2=N1)
                    (join/> M523+M4=M7 N5+1+N4=N2) (map/+ _ _ M6)
		    (join/> M522+M4=M6 N5+1+N4=N2)
                    (join/> M711+M6=M7 N7+1+N4=N1)
    <- nat`plus-swap-succ N5+1+N4=N2 N5+N4+1=N2
    <- nat`plus-associative-converse N5+N4+1=N2 N3+1+N2=N1 
                                     N7 N3+1+N5=N7 N7+N4+1=N1
    <- nat`plus-swap-succ-converse N7+N4+1=N1 N7+1+N4=N1
    <- join-associative (join/> M311+M2=M3 N3+1+N5=N7) M523+M4=M7
                       M6 M522+M4=M6 M711+M6=M7.

%worlds (WORLDS) (join-associative-join/<-join/< _ _ _ _ _ _ _).
%total {} (join-associative-join/<-join/< _ _ _ _ _ _ _).

%worlds (WORLDS) (join-associative _ _ _ _ _).
%total (J) (join-associative _ J _ _ _).

%{%
#ifdef DATA_JOIN_DETERMINISTIC
#ifdef DATA_JOIN_COMMUTATIVE
#define EQ eq
#define OPN join
#define OP(X,Y) X+Y
BEGIN_ELF
#include "OPN-assoc.i"
END_ELF
#undef OP
#undef OPN
#undef EQ
#else
/* we can't use the OPN-assoc.i shortcut */
%}%

%theorem join-associative* :
	forall* {M1} {M2} {M3} {M4} {M6} {M7}
	forall {A12:join M1 M2 M3} {A34:join M3 M4 M7} {A24:join M2 M4 M6} 
	exists {A16:join M1 M6 M7}
	true.

- : join-associative* A12 A34 A24 A16
    <- join-associative A12 A34 _ A24P A16P
    <- join-unique A24P A24 eq/ eq/ MP=M
    <- join-respects-eq A16P eq/ MP=M eq/ A16.

%worlds (WORLDS) (join-associative* _ _ _ _).
%total { } (join-associative* _ _ _ _).


%theorem join-associative-converse :
	forall* {M1} {M2} {M4} {M6} {M7}
	forall	{A24:join M2 M4 M6} 
		{A16:join M1 M6 M7}
	exists	{M3}
		{A12:join M1 M2 M3} 
		{A34:join M3 M4 M7}
	true.
%{%
#if DATA_JOIN_TOTAL
/* we can get join-associative-converse easily*/
%}%

- : join-associative-converse A24 A16 _ A12 A34
    <- join-total A12
    <- join-total A34P
    <- join-associative* A12 A34P A24 A16P
    <- join-unique A16P A16 eq/ eq/ MP=M
    <- join-respects-eq A34P eq/ eq/ MP=M A34.

%{%
#else
/* we have to do the entire theorem as join-associative
   and rely on data`join-associative-converse */
%}%

- : join-associative-converse join/L A _ join/R A.

- : join-associative-converse join/R A _ A join/R.

- : join-associative-converse A join/L _ join/L A.

- : join-associative-converse _ join/R _ join/R join/R.

- : join-associative-converse 
        (join/= M2+M4=M6 D2+D4=D6 nat`eq/)
	(join/= M1+M6=M7 D1+D6=D7 nat`eq/) _
	(join/= M1+M2=M3 D1+D2=D3 nat`eq/)
        (join/= M3+M4=M7 D3+D4=D7 nat`eq/) 
    <- data`join-associative-converse D2+D4=D6 D1+D6=D7 _ D1+D2=D3 D3+D4=D7
    <- join-associative-converse M2+M4=M6 M1+M6=M7 _ M1+M2=M3 M3+M4=M7.

- : join-associative-converse 
        (join/< M2+M044=M6 N0+1+N2=N4) 
        (join/= M1+M6=M7 D1+D2=D3 nat`eq/) _
	(join/= M1+M2=M3 D1+D2=D3 nat`eq/)
        (join/< M3+M044=M7 N0+1+N2=N4)
    <- join-associative-converse M2+M044=M6 M1+M6=M7 _ M1+M2=M3 M3+M044=M7.

- : join-associative-converse 
	(join/> M022+M3=M6 N0+1+N1=N2)
	(join/= M1+M6=M7 D1+D4=D7 nat`eq/) _
	(join/< M1+M022=M3 N0+1+N1=N2)
	(join/= M3+M4=M7 D1+D4=D7 nat`eq/)
    <- join-associative-converse M022+M3=M6 M1+M6=M7 _ M1+M022=M3 M3+M4=M7.

- : join-associative-converse 
	(join/= M2+M4=M6 D2+D4=D7 nat`eq/)
	(join/> M311+M6=M7 N3+1+N2=N1) _
	(join/> M311+M2=M3 N3+1+N2=N1)
	(join/= M3+M4=M7 D2+D4=D7 nat`eq/)
    <- join-associative-converse M2+M4=M6 M311+M6=M7 _ M311+M2=M3 M3+M4=M7.

- : join-associative-converse 
	(join/= M2+M4=M6 D2+D4=D6 nat`eq/)
	(join/< M1+M666=M7 N3+1+N2=N1) _
	(join/< M1+M222=M3 N3+1+N2=N1)
	(join/< M3+M444=M7 N3+1+N2=N1)
    <- join-associative-converse (join/= M2+M4=M6 D2+D4=D6 nat`eq/) M1+M666=M7 _
	M1+M222=M3 M3+M444=M7.

- : join-associative-converse 
        (join/< M2+M644=M6 N6+1+N2=N4)
	(join/> M311+M6=M7 N3+1+N1=N2) _
	(join/> M311+M2=M3 N3+1+N1=N2)
	(join/< M3+M644=M7 N6+1+N2=N4)
    <- join-associative-converse M2+M644=M6 M311+M6=M7 _ M311+M2=M3 M3+M644=M7.

- : join-associative-converse 
        (join/< M2+M422=M6 N6+1+N2=N4)
	(join/< M1+M611=M7 N3+1+N1=N2) _
	(join/< M1+M233=M3 N3+1+N1=N2)
        (join/< M3+M477=M7 N7+1+N1=N4)
    <- nat`plus-swap-succ N3+1+N1=N2 N3+N1+1=N2
    <- nat`plus-associative-converse N3+N1+1=N2 N6+1+N2=N4 N7
                                     N6+1+N3=N7 N7+N1+1=N4
    <- nat`plus-swap-succ-converse N7+N1+1=N4 N7+1+N1=N4
    <- join-associative-converse 
	(join/< M2+M422=M6 N6+1+N3=N7) M1+M611=M7 _ M1+M233=M3 M3+M477=M7.

- : join-associative-converse 
        (join/> M622+M4=M6 N6+1+N4=N2)
	(join/< M1+M556=M7 N5+1+N1=N4) _
	(join/< M1+M322=M3 N3+1+N1=N2)
	(join/< M3+M554=M7 N5+1+N1=N4)
    <- nat`plus-swap-succ N5+1+N1=N4 N5+N1+1=N4
    <- nat`plus-associative-converse N5+N1+1=N4 N6+1+N4=N2 N3
                                     N6+1+N5=N3 N3+N1+1=N2
    <- nat`plus-swap-succ-converse N3+N1+1=N2 N3+1+N1=N2
    <- join-associative-converse 
	(join/> M622+M4=M6 N6+1+N5=N3) M1+M556=M7 _ M1+M322=M3 M3+M554=M7.

%theorem join-associative-converse-join/>-join/> :
	forall* {N1} {D1} {M1} {N2} {D2} {M2} {N4} {N5} {N6} {D4} {M3} {M4} {M7}
	forall  {PLUS012:nat`plus (s N5) N4 N1}
                {PLUS514:nat`plus (s N6) N4 N2}
		{JOIN12: join (map/+ N5 D1 M1) (map/+ N6 D2 M2) M3}
                {JOIN34 :join M3 M4 M7}
        exists  {M333}
		{J12: join (map/+ N1 D1 M1) (map/+ N2 D2 M2) M333}
		{J34: join M333 (map/+ N4 D4 M4) (map/+ N4 D4 M7)}
        true.
               
- : join-associative-converse-join/>-join/> 
	(N+1+N4=N1:plus (s N) N4 N1) 
	(N+1+N4=N2:plus (s N) N4 N2)
	(join/= M1+M2=M3 D1+D2=D3 nat`eq/) MX+M4=M7 _
	(join/= M1+M2=M3 D1+D2=D3 N1=N2)
	(join/> MX+M4=M7 N+1+N4=N1)
    <- nat`plus-deterministic N+1+N4=N1 N+1+N4=N2 nat`eq/ nat`eq/ N1=N2.

- : join-associative-converse-join/>-join/>
	(N5+1+N4=N1:plus (s N5) N4 N1) 
	(N6+1+N4=N2:plus (s N6) N4 N2)
	(join/< M1+M322=M3 N3+1+N5=N6) M513+M4=M7 _ 
	(join/< M1+M322=M3 N3+1+N1=N2)
	(join/> M513+M4=M7 N5+1+N4=N1)
    <- plus-swap-succ N5+1+N4=N1 N5+N4+1=N1
    <- plus-swap-succ N6+1+N4=N2 N6+N4+1=N2
    <- plus-associative* N3+1+N5=N6 N6+N4+1=N2 N5+N4+1=N1 N3+1+N1=N2.

- : join-associative-converse-join/>-join/>
	(N5+1+N4=N1:plus (s N5) N4 N1) 
	(N6+1+N4=N2:plus (s N6) N4 N2)
	(join/> M311+M2=M3 N3+1+N6=N5) M623+M4=M7 _ 
	(join/> M311+M2=M3 N3+1+N2=N1)
	(join/> M623+M4=M7 N6+1+N4=N2)
    <- plus-swap-succ N5+1+N4=N1 N5+N4+1=N1
    <- plus-swap-succ N6+1+N4=N2 N6+N4+1=N2
    <- plus-associative* N3+1+N6=N5 N5+N4+1=N1 N6+N4+1=N2 N3+1+N2=N1.

%worlds (WORLDS) (join-associative-converse-join/>-join/> _ _ _ _ _ _ _).
%total {} (join-associative-converse-join/>-join/> _ _ _ _ _ _ _).

- : join-associative-converse 
        (join/> M622+M4=M6 N6+1+N4=N2)
	(join/> M511+M6=M7 N5+1+N4=N1) _
	J12 J34
    <- join-associative-converse M622+M4=M6 M511+M6=M7 _ M511+M622=M3 M3+M4=M7
    <- join-associative-converse-join/>-join/> N5+1+N4=N1 N6+1+N4=N2
	M511+M622=M3 M3+M4=M7 _ J12 J34.

%{%
#endif /* ! DATA_JOIN_TOTAL */
%}%
%worlds (WORLDS) (join-associative-converse _ _ _ _ _).
%total (J) (join-associative-converse _ J _ _ _).


%theorem join-associative-converse* :
        forall* {X1} {X2} {X3} {X4} {X6} {X7}
        forall {OP24:join X2 X4 X6} {OP16:join X1 X6 X7} {OP12:join X1 X2 X3}
        exists {OP34:join X3 X4 X7}
        true.

- : join-associative-converse* X2+X4=X6 X1+X6=X7 X1+X2=X3 X3+X4=X7
    <- join-associative-converse X2+X4=X6 X1+X6=X7 X3P X1+X2=X3P X3P+X4=X7
    <- join-unique X1+X2=X3P X1+X2=X3 eq/ eq/ X3P=X3
    <- join-respects-eq X3P+X4=X7 X3P=X3 eq/ eq/ X3+X4=X7.

%worlds (WORLDS) (join-associative-converse* X2+X4=X6 X1+X6=X7 X1+X2=X3 %{=>}% X3+X4=X7).
%total {} (join-associative-converse* _ _ _ _).

%{%
#endif /* ! DATA_JOIN_COMMUTATIVE */
#endif /* DATA_JOIN_DETERMINISTIC */
#endif /* DATA_JOIN_ASSOCIATIVE */


#ifdef DATA_JOIN_LEFT_CANCELS
#ifdef DATA_JOIN_NO_RIGHT_IDENTITY
%}%

%theorem join-right-identity-converse :
	forall* {M1} {M2} {M3}
	forall {J:join M1 M2 M3} {E:eq M1 M3}
	exists {EP:eq M2 map/0}
	true.

- : join-right-identity-converse join/L eq/ eq/.

- : join-right-identity-converse join/R eq/ eq/.

- : join-right-identity-converse (join/= _ D1+D2=D1 _) eq/ E
    <- data`join-no-right-identity D1+D2=D1 F
    <- false-implies-eq F E.

- : join-right-identity-converse (join/< M1+M022=M1 _) eq/ E
    <- join-right-identity-converse M1+M022=M1 eq/ M022=M0
    <- eq-symmetric M022=M0 M0=M022
    <- eq-contradiction M0=M022 F
    <- false-implies-eq F E.

- : join-right-identity-converse (join/> _ N3+1+N=N) eq/ E
    <- nat`plus-implies-gt N3+1+N=N nat`eq/ N>N
    <- gt-anti-reflexive N>N F
    <- false-implies-eq F E.

%worlds (WORLDS) (join-right-identity-converse _ _ _).
%total (J) (join-right-identity-converse J _ _).


%theorem join-left-cancels :
	forall* {M1} {M2} {M3} {M1P} {M2P} {M3P}
	forall {J:join M1 M2 M3} {JP:join M1P M2P M3P}
	       {E1:eq M1 M1P} {E3:eq M3 M3P}
	exists {E2:eq M2 M2P}
	true.

- : join-left-cancels join/L _ eq/ eq/ eq/.

- : join-left-cancels join/R J eq/ eq/ M0=M2P
    <- join-right-identity-converse J eq/ M2P=M0
    <- eq-symmetric M2P=M0 M0=M2P.
   
- : join-left-cancels J join/R eq/ eq/ M2P=M0
    <- join-right-identity-converse J eq/ M2P=M0.

- : join-left-cancels (join/= M1+M2=M3 D1+D2=D3 nat`eq/)
                      (join/= M1+M2P=M3 D1+D2P=D3 nat`eq/) eq/ eq/
                      M122=M122P
    <- data`join-left-cancels D1+D2=D3 D1+D2P=D3 data`eq/ data`eq/ D2=D2P
    <- join-left-cancels M1+M2=M3 M1+M2P=M3 eq/ eq/ M2=M2P
    <- map/+-preserves-eq nat`eq/ D2=D2P M2=M2P M122=M122P.

- : join-left-cancels (join/< M1+M022=M3 N0+1+N1=N2) 
                      (join/< M1+M022P=M3 N0P+1+N1=N2P) eq/ eq/ M222=M222P
    <- join-left-cancels M1+M022=M3 M1+M022P=M3 eq/ eq/ M022=M022P
    <- map/+-preserves-eq-converse M022=M022P N0=N0P D2=D2P M2=M2P
    <- nat`succ-deterministic N0=N0P N0+1=N0P+1
    <- nat`plus-deterministic N0+1+N1=N2 N0P+1+N1=N2P N0+1=N0P+1 nat`eq/ N2=N2P
    <- map/+-preserves-eq N2=N2P D2=D2P M2=M2P M222=M222P.

- : join-left-cancels (join/> M311+M2=M3 N3+1+N2=N1)
                      (join/> M3P11+M2P=M3 N3P+1+N2=N1) eq/ eq/ M222=M222P
    <- nat`plus-right-cancels N3+1+N2=N1 N3P+1+N2=N1 nat`eq/ nat`eq/ N3+1=N3P+1
    <- nat`succ-cancels N3+1=N3P+1 N3=N3P
    <- map/+-preserves-eq N3=N3P data`eq/ eq/ M311=M3P11
    <- join-left-cancels M311+M2=M3 M3P11+M2P=M3 M311=M3P11 eq/ M2=M2P
    <- map/+-preserves-eq nat`eq/ data`eq/ M2=M2P M222=M222P.

%% contradiction cases

- : join-left-cancels (join/= _ D1+D2=D1 nat`eq/) (join/< _ _) eq/ eq/ E
    <- data`join-no-right-identity D1+D2=D1 F
    <- false-implies-eq F E.

- : join-left-cancels (join/= _ _ nat`eq/) (join/> _ N3+1+N=N) eq/ eq/ E
    <- nat`plus-implies-gt N3+1+N=N nat`eq/ N>N
    <- gt-anti-reflexive N>N F
    <- false-implies-eq F E.

- : join-left-cancels (join/< _ _) (join/= _ D1+D2=D1 nat`eq/) eq/ eq/ E
    <- data`join-no-right-identity D1+D2=D1 F
    <- false-implies-eq F E.

- : join-left-cancels (join/< _ _) (join/> _ N3+1+N=N) eq/ eq/ E
    <- nat`plus-implies-gt N3+1+N=N nat`eq/ N>N
    <- gt-anti-reflexive N>N F
    <- false-implies-eq F E.

- : join-left-cancels (join/> _ N3+1+N=N) (join/= _ _ nat`eq/) eq/ eq/ E
    <- nat`plus-implies-gt N3+1+N=N nat`eq/ N>N
    <- gt-anti-reflexive N>N F
    <- false-implies-eq F E.

- : join-left-cancels (join/> _ N3+1+N=N) (join/< _ _) eq/ eq/ E
    <- nat`plus-implies-gt N3+1+N=N nat`eq/ N>N
    <- gt-anti-reflexive N>N F
    <- false-implies-eq F E.

%worlds (WORLDS) (join-left-cancels _ _ _ _ _).
%total [J JP] (join-left-cancels J JP _ _ _).


%theorem join-associative-cancel :
	forall*	{M1} {M2} {M3} {M4} {M6} {M7}
	forall	{J12: join M1 M2 M3}
		{J34: join M3 M4 M7}
		{J16: join M1 M6 M7}
	exists	{J24: join M2 M4 M6}
	true.

- : join-associative-cancel J12 J34 J16 J24
    <- join-associative J12 J34 M6P J24P J16P
    <- join-left-cancels J16P J16 eq/ eq/ EQ6
    <- join-respects-eq J24P eq/ eq/ EQ6 J24.

%worlds () (join-associative-cancel _ _ _ _).
%total { } (join-associative-cancel _ _ _ _).

%{%
/* Additionally if join is commutative, we define join-right-cancels */
#ifdef DATA_JOIN_COMMUTATIVE
%}%

%theorem join-right-cancels :
	forall* {M1} {M2} {M3} {M1P} {M2P} {M3P}
	forall {J:join M1 M2 M3} {JP:join M1P M2P M3P}
	       {E1:eq M2 M2P} {E3:eq M3 M3P}
	exists {E2:eq M1 M1P}
	true.

- : join-right-cancels M1+M2=M3 M1P+M2P=M3P M2=M2P M3=M3P M1=M1P
    <- join-commutative M1+M2=M3 M2+M1=M3
    <- join-commutative M1P+M2P=M3P M2P+M1P=M3P
    <- join-left-cancels M2+M1=M3 M2P+M1P=M3P M2=M2P M3=M3P M1=M1P.

%worlds (WORLDS) (join-right-cancels _ _ _ _ _).
%total { } (join-right-cancels _ _ _ _ _).


%theorem join-associative-converse-cancel :
	forall*	{M1} {M2} {M3} {M4} {M6} {M7}
	forall	{J24: join M2 M4 M6}
		{J16: join M1 M6 M7}
		{J34: join M3 M4 M7}
	exists	{J12: join M1 M2 M3}
	true.

- : join-associative-converse-cancel J24 J16 J34 J12
    <- join-associative-converse J24 J16 M3P J12P J34P
    <- join-right-cancels J34P J34 eq/ eq/ EQ3
    <- join-respects-eq J12P eq/ eq/ EQ3 J12.

%worlds () (join-associative-converse-cancel _ _ _ _).
%total { } (join-associative-converse-cancel _ _ _ _).

%{%
#endif
#endif
#endif
%}%

%theorem lookup-implies-join :
	forall* {M} {N} {D}
	forall {L:lookup M N D}
	exists {M-} {F:fresh M- N}
	       {A:join (map/+ N D map/0) M- M}
	true.

- : lookup-implies-join (lookup/= nat`eq/) _
                        (fresh/0) (join/R).

- : lookup-implies-join (lookup/= nat`eq/) _
                        (fresh/< N2>N1) 
                        (join/< join/L N0+1+N1=N2)
    <- nat`plus-total N0+1+N1=N2
    <- nat`plus-implies-gt N0+1+N1=N2 nat`eq/ N2>N1.

- : lookup-implies-join (lookup/> L N0+1+N1=N2) _
                        (fresh/> F N0+1+N1=N2)
			(join/> A N0+1+N1=N2)
    <- lookup-implies-join  L _ F A.

%worlds (WORLDS) (lookup-implies-join _ _ _ _).
%total (L) (lookup-implies-join L _ _ _).


%theorem join-joins-lookup :
	forall* {M1} {M2} {M3} {N} {D1} {D2}
	forall {L1:lookup M1 N D1} {L2:lookup M2 N D2}
               {A:join M1 M2 M3}
	exists {D3} {J:data`join D1 D2 D3} {L3:lookup M3 N D3}
	true.

- : join-joins-lookup (lookup/= nat`eq/) (lookup/= nat`eq/) A
                           _ AD L3
    <- join/=-inversion A nat`eq/ _ _ AD _ M=M133
    <- eq-symmetric M=M133 M133=M
    <- lookup-respects-eq (lookup/= nat`eq/) M133=M nat`eq/ data`eq/ L3.

- : join-joins-lookup (lookup/> L1 N0+1+N1=N2)
                           (lookup/= nat`eq/) A _ AD L3P
    <- join/<-inversion A N0+1+N1=N2 M3 M1+M022=M3 M=M113
    <- join-joins-lookup L1 (lookup/= nat`eq/) M1+M022=M3 _ AD L3
    <- eq-symmetric M=M113 M113=M
    <- lookup-respects-eq (lookup/> L3 N0+1+N1=N2) M113=M  nat`eq/ data`eq/ L3P.

- : join-joins-lookup (lookup/= nat`eq/) 
                           (lookup/> L2 N3+1+N2=N1) A _ AD L3P
    <- join/>-inversion A N3+1+N2=N1 M3 M311+M2=M3 M=M223
    <- join-joins-lookup (lookup/= nat`eq/) L2 M311+M2=M3 _ AD L3
    <- eq-symmetric M=M223 M223=M
    <- lookup-respects-eq (lookup/> L3 N3+1+N2=N1) M223=M  nat`eq/ data`eq/ L3P.

- : join-joins-lookup (lookup/> L1P N4+1+N1=N)
                           (lookup/> L2 N5+1+N2=N)
                           (join/= M1+M2=M3 _ nat`eq/) _ AD
                           (lookup/> L N5+1+N2=N)
    <- plus-right-cancels N4+1+N1=N N5+1+N2=N nat`eq/ nat`eq/ N4+1=N5+1
    <- succ-cancels N4+1=N5+1 N4=N5
    <- lookup-respects-eq L1P eq/ N4=N5 data`eq/ L1
    <- join-joins-lookup L1 L2 M1+M2=M3 _ AD L.

- : join-joins-lookup (lookup/> L1 N4+1+N1=N)
                           (lookup/> L2 N5+1+N2=N)
                           (join/< M1+M022=M3 N0+1+N1=N2) _ AD
			   (lookup/> L3 N4+1+N1=N)
    <- plus-swap-succ N0+1+N1=N2 N0+N1+1=N2
    <- plus-associative-converse N0+N1+1=N2 N5+1+N2=N N6 N5+1+N0=N6 N6+N1+1=N
    <- plus-swap-succ N4+1+N1=N N4+N1+1=N
    <- plus-right-cancels N6+N1+1=N N4+N1+1=N nat`eq/ nat`eq/ N6=N4
    <- plus-respects-eq N5+1+N0=N6 nat`eq/ nat`eq/ N6=N4 N5+1+N0=N4
    <- join-joins-lookup 
       L1 (lookup/> L2 N5+1+N0=N4) M1+M022=M3 _ AD L3.

- : join-joins-lookup (lookup/> L1 N4+1+N1=N)
                           (lookup/> L2 N5+1+N2=N)
                           (join/> M311+M2=M3 N3+1+N2=N1) _ AD
                           (lookup/> L3 N5+1+N2=N)
    <- plus-swap-succ N3+1+N2=N1 N3+N2+1=N1
    <- plus-associative-converse N3+N2+1=N1 N4+1+N1=N N6 N4+1+N3=N6 N6+N2+1=N
    <- plus-swap-succ N5+1+N2=N N5+N2+1=N
    <- plus-right-cancels N6+N2+1=N N5+N2+1=N nat`eq/ nat`eq/ N6=N5
    <- plus-respects-eq N4+1+N3=N6 nat`eq/ nat`eq/ N6=N5 N4+1+N3=N5
    <- join-joins-lookup
       (lookup/> L1 N4+1+N3=N5) L2 M311+M2=M3 _ AD L3.

%worlds (WORLDS) (join-joins-lookup _ _ _ _ _ _).
%total (A) (join-joins-lookup _ _ A _ _ _).

%{%
#ifdef DATA_JOIN_DETERMINISTIC
%}%

%theorem join-joins-lookup* :
	forall* {M1} {M2} {M3} {N} {D1} {D2} {D3}
	forall {L1:lookup M1 N D1} {L2:lookup M2 N D2}
               {A:join M1 M2 M3}
	       {J:data`join D1 D2 D3} 
	exists {L3:lookup M3 N D3}
	true.

- : join-joins-lookup* L1 L2 A J L3
    <- join-joins-lookup L1 L2 A _ JP L3P
    <- data`join-deterministic JP J data`eq/ data`eq/ D3P=D3
    <- lookup-respects-eq L3P eq/ nat`eq/ D3P=D3 L3.

%worlds (WORLDS) (join-joins-lookup* _ _ _ _ _).
%total { } (join-joins-lookup* _ _ _ _ _).

%{%
#endif
%}%

%theorem join-commutes-lookup :
	forall* {M1} {M2} {M3} {N} {D1} {D2} {D3}
	forall {A:join M1 M2 M3}
	       {L1:lookup M1 N D1} {L2:lookup M2 N D2}
               {L3:lookup M3 N D3}	       
	exists {J:data`join D1 D2 D3} 
	true.

- : join-commutes-lookup M1+M2=M3 L1 L2 L3 D1+D2=D3
    <- join-joins-lookup L1 L2 M1+M2=M3 D3X D1+D2=D3X L3X
    <- lookup-unique L3X L3 eq/ nat`eq/ D3X=D3
    <- data`join-respects-eq D1+D2=D3X data`eq/ data`eq/ D3X=D3 D1+D2=D3.

%worlds () (join-commutes-lookup _ _ _ _ _).
%total { } (join-commutes-lookup _ _ _ _ _).


%theorem join-preserves-fresh* :
	forall* {M1} {M2} {M3} {N}
	forall {F1:fresh M1 N} {F2:fresh M2 N}
               {A:join M1 M2 M3}
	exists {F3:fresh M3 N}
	true.
	
- : join-preserves-fresh* _ F join/L F.

- : join-preserves-fresh* F _ join/R F.

- : join-preserves-fresh* (fresh/< N<N1) (fresh/< _) (join/= _ _ _) 
                          (fresh/< N<N1).

- : join-preserves-fresh* (fresh/< N<N1) (fresh/< _) (join/< _ _) 
                          (fresh/< N<N1).

- : join-preserves-fresh* (fresh/< _) (fresh/< N<N2) (join/> _ _) 
                          (fresh/< N<N2).

- : join-preserves-fresh* (fresh/< N<N1) (fresh/> F2 N4+1+N2=N) AX F3X
    <- gt-implies-plus N<N1 N0 N0+1+N=N1
    <- plus-swap-succ N4+1+N2=N N4+N2+1=N
    <- plus-associative-converse N4+N2+1=N N0+1+N=N1 N3 N0+1+N4=N3 N3+N2+1=N1
    <- plus-swap-succ-converse N3+N2+1=N1 N3+1+N2=N1
    <- join/>-inversion AX N3+1+N2=N1 M3 A M=M223
    <- plus-implies-gt N0+1+N4=N3 nat`eq/ N4<N3
    <- join-preserves-fresh* (fresh/< N4<N3) F2 A F3
    <- eq-symmetric M=M223 M223=M
    <- fresh-respects-eq (fresh/> F3 N4+1+N2=N) M223=M nat`eq/ F3X.

- : join-preserves-fresh* (fresh/> F1 N3+1+N1=N) (fresh/< N<N2) AX F3X
    <- gt-implies-plus N<N2 N4 N4+1+N=N2
    <- plus-swap-succ N3+1+N1=N N2+N1+1=N
    <- plus-associative-converse N2+N1+1=N N4+1+N=N2 N0 N4+1+N2=N0 N0+N1+1=N2
    <- plus-swap-succ-converse N0+N1+1=N2 N0+1+N1=N2
    <- join/<-inversion AX N0+1+N1=N2 M3 A M=M113
    <- plus-implies-gt N4+1+N2=N0 nat`eq/ N2<N0
    <- join-preserves-fresh* F1 (fresh/< N2<N0) A F3
    <- eq-symmetric M=M113 M113=M
    <- fresh-respects-eq (fresh/> F3 N3+1+N1=N) M113=M nat`eq/ F3X.

- : join-preserves-fresh* (fresh/> F1 N3+1+N1=N) (fresh/> F2P N4+1+N2=N) 
                          (join/= A _ N1=N2) (fresh/> F3 N3+1+N1=N)
    <- plus-right-cancels N3+1+N1=N N4+1+N2=N N1=N2 nat`eq/ N3+1=N4+1
    <- succ-cancels N3+1=N4+1 N3=N4
    <- nat`eq-symmetric N3=N4 N4=N3
    <- fresh-respects-eq F2P eq/ N4=N3 F2
    <- join-preserves-fresh* F1 F2 A F3.

- : join-preserves-fresh* (fresh/> F1 N3+1+N1=N) (fresh/> F2 N4+1+N2=N) 
                          (join/< A N0+1+N1=N2) (fresh/> F3 N3+1+N1=N)
    <- plus-swap-succ N0+1+N1=N2 N0+N1+1=N2
    <- plus-associative-converse N0+N1+1=N2 N4+1+N2=N N6 N4+1+N0=N6 N6+N1+1=N
    <- plus-swap-succ N3+1+N1=N N3+N1+1=N
    <- plus-right-cancels N6+N1+1=N N3+N1+1=N nat`eq/ nat`eq/ N6=N3
    <- plus-respects-eq N4+1+N0=N6 nat`eq/ nat`eq/ N6=N3 N4+1+N0=N3
    <- join-preserves-fresh* F1 (fresh/> F2 N4+1+N0=N3) A F3.

- : join-preserves-fresh* (fresh/> F1 N3+1+N1=N) (fresh/> F2 N4+1+N2=N) 
                          (join/> A N0+1+N2=N1) (fresh/> F3 N4+1+N2=N)
    <- plus-swap-succ N0+1+N2=N1 N0+N2+1=N1
    <- plus-associative-converse N0+N2+1=N1 N3+1+N1=N N6 N3+1+N0=N6 N6+N2+1=N
    <- plus-swap-succ N4+1+N2=N N4+N2+1=N
    <- plus-right-cancels N6+N2+1=N N4+N2+1=N nat`eq/ nat`eq/ N6=N4
    <- plus-respects-eq N3+1+N0=N6 nat`eq/ nat`eq/ N6=N4 N3+1+N0=N4
    <- join-preserves-fresh* (fresh/> F1 N3+1+N0=N4) F2 A F3.

%worlds (WORLDS) (join-preserves-fresh* _ _ _ _).
%total (A) (join-preserves-fresh* _ _ A _).


%theorem fresh-join-left-preserves-lookup* :
	forall* {M1} {M2} {M3} {N} {D}
	forall {F1:fresh M1 N} {L2:lookup M2 N D} 
               {A:join M1 M2 M3}
	exists {L3:lookup M3 N D}
	true.

- : fresh-join-left-preserves-lookup* _ L join/L L.

- : fresh-join-left-preserves-lookup* (fresh/< N2<N1) (lookup/= nat`eq/) AX L3X
    <- gt-implies-plus N2<N1 N3 N3+1+N2=N1
    <- join/>-inversion AX N3+1+N2=N1 M3 A M=M223
    <- eq-symmetric M=M223 M223=M
    <- lookup-respects-eq (lookup/= nat`eq/) M223=M nat`eq/ data`eq/ L3X.

- : fresh-join-left-preserves-lookup* (fresh/< N<N1) (lookup/> L2 N4+1+N2=N)
                                      AX L3X
    <- gt-implies-plus N<N1 N0 N0+1+N=N1
    <- plus-swap-succ N4+1+N2=N N4+N2+1=N
    <- plus-associative-converse N4+N2+1=N N0+1+N=N1 N3 N0+1+N4=N3 N3+N2+1=N1
    <- plus-swap-succ-converse N3+N2+1=N1 N3+1+N2=N1
    <- join/>-inversion AX N3+1+N2=N1 M3 A M=M223
    <- eq-symmetric M=M223 M223=M
    <- plus-implies-gt N0+1+N4=N3 nat`eq/ N3>N4
    <- fresh-join-left-preserves-lookup* (fresh/< N3>N4) L2 A L3
    <- lookup-respects-eq (lookup/> L3 N4+1+N2=N) M223=M nat`eq/ data`eq/ L3X.
 
- : fresh-join-left-preserves-lookup* (fresh/> F1 N0+1+N1=N2) 
                                      (lookup/= nat`eq/) AX L3X
    <- join/<-inversion AX N0+1+N1=N2 M3 A M=M113
    <- eq-symmetric M=M113 M113=M
    <- fresh-join-left-preserves-lookup* F1 (lookup/= nat`eq/) A L3
    <- lookup-respects-eq (lookup/> L3 N0+1+N1=N2) M113=M nat`eq/ data`eq/ L3X.

- : fresh-join-left-preserves-lookup* (fresh/> F1P N4+1+N1=N)
                                      (lookup/> L2 N5+1+N1=N)
                                      (join/= A _ nat`eq/) 
                                      (lookup/> L3 N5+1+N1=N)
    <- plus-right-cancels N4+1+N1=N N5+1+N1=N nat`eq/ nat`eq/ N4+1=N5+1
    <- succ-cancels N4+1=N5+1 N4=N5
    <- fresh-respects-eq F1P eq/ N4=N5 F1
    <- fresh-join-left-preserves-lookup* F1 L2 A L3.

- : fresh-join-left-preserves-lookup* (fresh/> F1 N4+1+N1=N)
                                      (lookup/> L2 N5+1+N2=N)
                                      (join/< A N0+1+N1=N2)
                                      (lookup/> L3 N4+1+N1=N)
    <- plus-swap-succ N0+1+N1=N2 N0+N1+1=N2
    <- plus-associative-converse N0+N1+1=N2 N5+1+N2=N N3 N5+1+N0=N3 N3+N1+1=N
    <- plus-swap-succ N4+1+N1=N N4+N1+1=N
    <- plus-right-cancels N3+N1+1=N N4+N1+1=N nat`eq/ nat`eq/ N3=N4
    <- plus-respects-eq N5+1+N0=N3 nat`eq/ nat`eq/ N3=N4 N5+1+N0=N4
    <- fresh-join-left-preserves-lookup* F1 (lookup/> L2 N5+1+N0=N4) A L3.

- : fresh-join-left-preserves-lookup* (fresh/> F1 N4+1+N1=N)
                                      (lookup/> L2 N5+1+N2=N)
                                      (join/> A N3+1+N2=N1)
                                      (lookup/> L3 N5+1+N2=N)
    <- plus-swap-succ N3+1+N2=N1 N3+N2+1=N1
    <- plus-associative-converse N3+N2+1=N1 N4+1+N1=N N6 N4+1+N3=N6 N6+N2+1=N
    <- plus-swap-succ N5+1+N2=N N5+N2+1=N
    <- plus-right-cancels N6+N2+1=N N5+N2+1=N nat`eq/ nat`eq/ N6=N5
    <- plus-respects-eq N4+1+N3=N6 nat`eq/ nat`eq/ N6=N5 N4+1+N3=N5
    <- fresh-join-left-preserves-lookup* (fresh/> F1 N4+1+N3=N5) L2 A L3.

%worlds (WORLDS) (fresh-join-left-preserves-lookup* _ _ _ _).
%total (A) (fresh-join-left-preserves-lookup* _ _ A _).


%theorem fresh-join-left-preserves-lookup-converse :
	forall* {M1} {M2} {M3} {N} {D}
	forall {F1:fresh M1 N} {L3:lookup M3 N D} 
               {A:join M1 M2 M3}
	exists {L2:lookup M2 N D}
	true.

%theorem fresh-join-left-preserves-lookup-converse/L :
	forall* {M1} {M2} {M3} {N} {D} {B}
	forall {F1:fresh M1 N} {L3:lookup M3 N D} 
               {A:join M1 M2 M3}
               {D?:domain? M2 N B}
	exists {L2:lookup M2 N D}
	true.

- : fresh-join-left-preserves-lookup-converse F1 L3 A L2
    <- domain?-total D?
    <- fresh-join-left-preserves-lookup-converse/L F1 L3 A D? L2.

- : fresh-join-left-preserves-lookup-converse/L 
     F1 L3 A (domain?/in L2P) L2
    <- fresh-join-left-preserves-lookup* F1 L2P A L3P
    <- lookup-deterministic L3P L3 eq/ nat`eq/ DP=D
    <- lookup-respects-eq L2P eq/ nat`eq/ DP=D L2.

- : fresh-join-left-preserves-lookup-converse/L
     F1 L3 A (domain?/out F2) L2
    <- join-preserves-fresh* F1 F2 A F3
    <- fresh-lookup-not-equal F3 L3 N<>N
    <- nat`ne-anti-reflexive N<>N F
    <- false-implies-lookup F L2.

%worlds (WORLDS) (fresh-join-left-preserves-lookup-converse/L _ _ _ _ _).
%total { } (fresh-join-left-preserves-lookup-converse/L _ _ _ _ _).

%worlds (WORLDS) (fresh-join-left-preserves-lookup-converse _ _ _ _).
%total { } (fresh-join-left-preserves-lookup-converse _ _ _ _).


%theorem join-left-affects-lookup :
	forall* {M1} {N} {D2} {M2} {M3}
	forall {L:lookup M2 N D2}
	       {A:join M1 M2 M3}
	exists {D3}
               {N:lookup M3 N D3}
	true.

%theorem join-left-affects-lookup/L :
	forall* {M1} {N} {D2} {M2} {M3} {B}
	forall {L:lookup M2 N D2}
	       {A:join M1 M2 M3}
	       {D:domain? M1 N B}
	exists {D3}
               {N:lookup M3 N D3}
	true.

- : join-left-affects-lookup/L L2 A (domain?/in L1) _ L3
    <- join-joins-lookup L1 L2 A _ _ L3.

- : join-left-affects-lookup/L L2 A (domain?/out F1) _ L3
    <- fresh-join-left-preserves-lookup* F1 L2 A L3.

%worlds (WORLDS) (join-left-affects-lookup/L _ _ _ _ _).
%total { } (join-left-affects-lookup/L _ _ _ _ _).

- : join-left-affects-lookup L2 A _ L3
    <- domain?-total D
    <- join-left-affects-lookup/L L2 A D _ L3.

%worlds (WORLDS) (join-left-affects-lookup _ _ _ _).
%total { } (join-left-affects-lookup _ _ _ _).


% TODO: "*" should be removed here.

%theorem fresh-join-right-preserves-lookup :
	forall* {M1} {M2} {M3} {N} {D}
	forall {L1:lookup M1 N D} {F2:fresh M2 N}
               {A:join M1 M2 M3}
	exists {L3:lookup M3 N D}
	true.

% deprecated
%abbrev fresh-join-right-preserves-lookup* = fresh-join-right-preserves-lookup.

- : fresh-join-right-preserves-lookup L _ join/R L.

- : fresh-join-right-preserves-lookup (lookup/= nat`eq/) (fresh/< N2<N1) AX L3X
    <- gt-implies-plus N2<N1 N3 N3+1+N2=N1
    <- join/<-inversion AX N3+1+N2=N1 M3 A M=M223
    <- eq-symmetric M=M223 M223=M
    <- lookup-respects-eq (lookup/= nat`eq/) M223=M nat`eq/ data`eq/ L3X.

- : fresh-join-right-preserves-lookup (lookup/> L2 N4+1+N2=N) (fresh/< N<N1)
                                      AX L3X
    <- gt-implies-plus N<N1 N0 N0+1+N=N1
    <- plus-swap-succ N4+1+N2=N N4+N2+1=N
    <- plus-associative-converse N4+N2+1=N N0+1+N=N1 N3 N0+1+N4=N3 N3+N2+1=N1
    <- plus-swap-succ-converse N3+N2+1=N1 N3+1+N2=N1
    <- join/<-inversion AX N3+1+N2=N1 M3 A M=M223
    <- eq-symmetric M=M223 M223=M
    <- plus-implies-gt N0+1+N4=N3 nat`eq/ N3>N4
    <- fresh-join-right-preserves-lookup L2 (fresh/< N3>N4) A L3
    <- lookup-respects-eq (lookup/> L3 N4+1+N2=N) M223=M nat`eq/ data`eq/ L3X.
 
- : fresh-join-right-preserves-lookup
	(lookup/= nat`eq/) (fresh/> F1 N0+1+N1=N2) 
	AX L3X
    <- join/>-inversion AX N0+1+N1=N2 M3 A M=M113
    <- eq-symmetric M=M113 M113=M
    <- fresh-join-right-preserves-lookup (lookup/= nat`eq/) F1 A L3
    <- lookup-respects-eq (lookup/> L3 N0+1+N1=N2) M113=M nat`eq/ data`eq/ L3X.

- : fresh-join-right-preserves-lookup (lookup/> L2 N5+1+N1=N)
				      (fresh/> F1P N4+1+N1=N)
                                      (join/= A _ nat`eq/) 
                                      (lookup/> L3 N5+1+N1=N)
    <- plus-right-cancels N4+1+N1=N N5+1+N1=N nat`eq/ nat`eq/ N4+1=N5+1
    <- succ-cancels N4+1=N5+1 N4=N5
    <- fresh-respects-eq F1P eq/ N4=N5 F1
    <- fresh-join-right-preserves-lookup L2 F1 A L3.

- : fresh-join-right-preserves-lookup (lookup/> L2 N5+1+N2=N)
                                      (fresh/> F1 N4+1+N1=N)
                                      (join/> A N0+1+N1=N2)
                                      (lookup/> L3 N4+1+N1=N)
    <- plus-swap-succ N0+1+N1=N2 N0+N1+1=N2
    <- plus-associative-converse N0+N1+1=N2 N5+1+N2=N N3 N5+1+N0=N3 N3+N1+1=N
    <- plus-swap-succ N4+1+N1=N N4+N1+1=N
    <- plus-right-cancels N3+N1+1=N N4+N1+1=N nat`eq/ nat`eq/ N3=N4
    <- plus-respects-eq N5+1+N0=N3 nat`eq/ nat`eq/ N3=N4 N5+1+N0=N4
    <- fresh-join-right-preserves-lookup (lookup/> L2 N5+1+N0=N4) F1 A L3.

- : fresh-join-right-preserves-lookup (lookup/> L2 N5+1+N2=N)
				      (fresh/> F1 N4+1+N1=N)
                                      (join/< A N3+1+N2=N1)
                                      (lookup/> L3 N5+1+N2=N)
    <- plus-swap-succ N3+1+N2=N1 N3+N2+1=N1
    <- plus-associative-converse N3+N2+1=N1 N4+1+N1=N N6 N4+1+N3=N6 N6+N2+1=N
    <- plus-swap-succ N5+1+N2=N N5+N2+1=N
    <- plus-right-cancels N6+N2+1=N N5+N2+1=N nat`eq/ nat`eq/ N6=N5
    <- plus-respects-eq N4+1+N3=N6 nat`eq/ nat`eq/ N6=N5 N4+1+N3=N5
    <- fresh-join-right-preserves-lookup L2 (fresh/> F1 N4+1+N3=N5) A L3.

%worlds (WORLDS) (fresh-join-right-preserves-lookup _ _ _ _).
%total (A) (fresh-join-right-preserves-lookup _ _ A _).


%theorem fresh-join-right-preserves-lookup-converse :
	forall* {M1} {M2} {M3} {N} {D}
	forall {L3:lookup M3 N D} {F2:fresh M2 N}
               {A:join M1 M2 M3}
	exists {L1:lookup M1 N D}
	true.

%theorem fresh-join-right-preserves-lookup-converse/L :
	forall* {M1} {M2} {M3} {N} {D} {B}
	forall {L3:lookup M3 N D} {F2:fresh M2 N}
               {A:join M1 M2 M3}
	       {D?:domain? M1 N B}
	exists {L1:lookup M1 N D}
	true.

- : fresh-join-right-preserves-lookup-converse L3 F2 A L1
    <- domain?-total D?
    <- fresh-join-right-preserves-lookup-converse/L L3 F2 A D? L1.

- : fresh-join-right-preserves-lookup-converse/L 
     L3 F2 A (domain?/in L1P) L1
    <- fresh-join-right-preserves-lookup* L1P F2 A L3P
    <- lookup-deterministic L3P L3 eq/ nat`eq/ DP=D
    <- lookup-respects-eq L1P eq/ nat`eq/ DP=D L1.

- : fresh-join-right-preserves-lookup-converse/L
     L3 F2 A (domain?/out F1) L1
    <- join-preserves-fresh* F1 F2 A F3
    <- fresh-lookup-not-equal F3 L3 N<>N
    <- nat`ne-anti-reflexive N<>N F
    <- false-implies-lookup F L1.

%worlds (WORLDS) (fresh-join-right-preserves-lookup-converse/L _ _ _ _ _).
%total { } (fresh-join-right-preserves-lookup-converse/L _ _ _ _ _).

%worlds (WORLDS) (fresh-join-right-preserves-lookup-converse _ _ _ _).
%total { } (fresh-join-right-preserves-lookup-converse _ _ _ _).


%theorem join-right-affects-lookup :
	forall* {M1} {N} {D1} {M2} {M3}
	forall {L:lookup M1 N D1}
	       {A:join M1 M2 M3}
	exists {D3}
               {N:lookup M3 N D3}
	true.

%theorem join-right-affects-lookup/L :
	forall* {M1} {N} {D1} {M2} {M3} {B}
	forall {L:lookup M1 N D1}
	       {A:join M1 M2 M3}
	       {D:domain? M2 N B}
	exists {D3}
               {N:lookup M3 N D3}
	true.

- : join-right-affects-lookup/L L1 A (domain?/in L2) _ L3
    <- join-joins-lookup L1 L2 A _ _ L3.

- : join-right-affects-lookup/L L1 A (domain?/out F2) _ L3
    <- fresh-join-right-preserves-lookup* L1 F2 A L3.

%worlds (WORLDS) (join-right-affects-lookup/L _ _ _ _ _).
%total { } (join-right-affects-lookup/L _ _ _ _ _).

- : join-right-affects-lookup L1 A _ L3
    <- domain?-total D
    <- join-right-affects-lookup/L L1 A D _ L3.

%worlds (WORLDS) (join-right-affects-lookup _ _ _ _).
%total { } (join-right-affects-lookup _ _ _ _).

%{%
#ifdef DATA_JOIN_NO_RIGHT_IDENTITY
%}%

%theorem join-right-preserves-lookup-implies-fresh  :
	forall* {M1} {M2} {M3} {N} {D}
	forall {L1:lookup M1 N D}
               {A:join M1 M2 M3}
	       {L3:lookup M3 N D} 
	exists {F2:fresh M2 N} 
	true.

%theorem join-right-preserves-lookup-implies-fresh/L  :
	forall* {M1} {M2} {M3} {N} {D} {B}
	forall {L1:lookup M1 N D}
               {A:join M1 M2 M3}
	       {L3:lookup M3 N D} 
	       {D?:domain? M2 N B}
	exists {F2:fresh M2 N} 
	true.

- : join-right-preserves-lookup-implies-fresh L1 A L3 F2
    <- domain?-total D?
    <- join-right-preserves-lookup-implies-fresh/L L1 A L3 D? F2.

- : join-right-preserves-lookup-implies-fresh/L _ _ _ (domain?/out F2) F2.

- : join-right-preserves-lookup-implies-fresh/L L1 A L3 (domain?/in L2) F2
    <- join-joins-lookup L1 L2 A D12 D1+D2=D12 L12
    <- lookup-deterministic L12 L3 eq/ nat`eq/ D12=D1
    <- data`join-respects-eq D1+D2=D12 data`eq/ data`eq/ D12=D1 D1+D2=D1
    <- data`join-no-right-identity D1+D2=D1 F
    <- false-implies-fresh F F2.

%worlds (WORLDS) (join-right-preserves-lookup-implies-fresh/L _ _ _ _ _).
%total { } (join-right-preserves-lookup-implies-fresh/L _ _ _ _ _).

%worlds (WORLDS) (join-right-preserves-lookup-implies-fresh _ _ _ _).
%total { } (join-right-preserves-lookup-implies-fresh _ _ _ _).

%{%
#endif
%}%

% TODO deprecate *
%theorem join-preserves-fresh-converse* :
	forall* {M1} {M2} {M3} {N}
	forall {F3:fresh M3 N}
               {A:join M1 M2 M3}
	exists {F1:fresh M1 N} {F2:fresh M2 N}
	true.

%theorem join-preserves-fresh-converse/L :
	forall* {M1} {M2} {M3} {N} {B1} {B2}
	forall {F3:fresh M3 N}
               {A:join M1 M2 M3}
               {D1:domain? M1 N B1}
               {D2:domain? M2 N B2}
	exists {F1:fresh M1 N} {F2:fresh M2 N}
	true.

- : join-preserves-fresh-converse* F3 A F1 F2
    <- domain?-total D1
    <- domain?-total D2
    <- join-preserves-fresh-converse/L F3 A D1 D2 F1 F2.

- : join-preserves-fresh-converse/L _ _
     (domain?/out F1) (domain?/out F2) F1 F2.

- : join-preserves-fresh-converse/L F3 A
     (domain?/out F1) (domain?/in L2) F1 F2
    <- fresh-join-left-preserves-lookup* F1 L2 A L3
    <- fresh-lookup-not-equal F3 L3 N<>N
    <- nat`ne-anti-reflexive N<>N F
    <- false-implies-fresh F F2.

- : join-preserves-fresh-converse/L F3 A
     (domain?/in L1) (domain?/out F2) F1 F2
    <- fresh-join-right-preserves-lookup* L1 F2 A L3
    <- fresh-lookup-not-equal F3 L3 N<>N
    <- nat`ne-anti-reflexive N<>N F
    <- false-implies-fresh F F1.

- : join-preserves-fresh-converse/L F3 A
     (domain?/in L1) (domain?/in L2) F1 F2
    <- join-joins-lookup L1 L2 A _ _ L3
    <- fresh-lookup-not-equal F3 L3 N<>N
    <- nat`ne-anti-reflexive N<>N F
    <- false-implies-fresh F F1
    <- false-implies-fresh F F2.

%worlds (WORLDS) (join-preserves-fresh-converse/L _ _ _ _ _ _).
%total { } (join-preserves-fresh-converse/L _ _ _ _ _ _).

%worlds (WORLDS) (join-preserves-fresh-converse* _ _ _ _).
%total { } (join-preserves-fresh-converse* _ _ _ _).


%theorem disjoint-join-cross-comparable :
	forall* {C} {C1} {C2} {C3} {C4}
	forall	{D12:disjoint C1 C2}
		{A12:join C1 C2 C} 
		{D34:disjoint C3 C4}
		{A34:join C3 C4 C}
	exists {C13} {C14} {C23} {C24}
	       {A1:join C13 C14 C1} {A2:join C23 C24 C2}
	       {A3:join C13 C23 C3} {A4:join C14 C24 C4}
	true.

%{%
NB: The client can easily prove all pieces are mutually disjoint.
For example C13 ^ C14 because C13 <= C3 and C14 <= C4 and C3^C4
            C13 ^ C23 because C13 <= C1 and C23 <= C2 and C1^C2
%}%

- : disjoint-join-cross-comparable _ join/L _ C3+C4=C2 _ _ _ _ join/L C3+C4=C2 join/L join/L.

- : disjoint-join-cross-comparable _ join/R _ C3+C4=C1 _ _ _ _ C3+C4=C1 join/R join/R join/R.

- : disjoint-join-cross-comparable _ C1+C2=C4 _ join/L _ _ _ _ join/L join/L join/L C1+C2=C4.

- : disjoint-join-cross-comparable _ C1+C2=C3 _ join/R _ _ _ _ join/R join/R C1+C2=C3 join/R.

- : disjoint-join-cross-comparable D (join/= _ _ nat`eq/) _ _ _ _ _ _
	join/L join/L J1 J2
    <- disjoint/=-contradiction D nat`eq/ F
    <- false-implies-join F J1
    <- false-implies-join F J2.

- : disjoint-join-cross-comparable _ _ D (join/= _ _ nat`eq/) _ _ _ _
	join/L join/L J1 J2
    <- disjoint/=-contradiction D nat`eq/ F
    <- false-implies-join F J1
    <- false-implies-join F J2.

- : disjoint-join-cross-comparable 
	D12 (join/< C1+CX2=C N0+1+N1=N2)
	D34 (join/< C3+CX4=C N5+1+N3=N4) _ _ _ _ % NB N1=N3=N
	CY1Y3+CY14=CY1 C2Y3+C24=C2
	CY1Y3+C2Y3=CY3 CY14+C24=C4
    <- disjoint/<-inversion D12 N0+1+N1=N2 D12P
    <- disjoint/<-inversion D34 N5+1+N3=N4 D34P
    <- disjoint-join-cross-comparable D12P C1+CX2=C D34P C3+CX4=C 
	C13 C1X4 CX23 CX2X4
	C13+C1X4=C1 CX23+CX2X4=CX2 C13+CX23=C3 C1X4+CX2X4=CX4
    <- shift-total C1X4<<N=CY14
    <- shift-total CX23<<N=C2Y3
    <- shift-total CX2X4<<N=C24
    <- plus-swap-succ N0+1+N1=N2 N0+N1+1=N2
    <- plus-commutative N0+N1+1=N2 N1+1+N0=N2
    <- shift-right-preserves-join C13+C1X4=C1 C1X4<<N=CY14 CY1Y3+CY14=CY1
    <- shift-preserves-join* CX23+CX2X4=CX2 
	CX23<<N=C2Y3 CX2X4<<N=C24 (shift/+ N1+1+N0=N2) C2Y3+C24=C2
    <- shift-right-preserves-join C13+CX23=C3 CX23<<N=C2Y3 CY1Y3+C2Y3=CY3
    <- plus-swap-succ N5+1+N3=N4 N5+N3+1=N4
    <- plus-commutative N5+N3+1=N4 N3+1+N5=N4
    <- shift-preserves-join* C1X4+CX2X4=CX4
	C1X4<<N=CY14 CX2X4<<N=C24 (shift/+ N3+1+N5=N4) CY14+C24=C4.

- : disjoint-join-cross-comparable 
	D12 (join/< C1+CX2=C N0+1+N1=N2)
	D34 (join/> CX3+C4=C N5+1+N4=N3) _ _ _ _ % NB N1=N4=N
	CY13+CY1Y4=CY1 C23+C2Y4=C2 CY13+C23=C3 CY1Y4+C2Y4=CY4
    <- disjoint/<-inversion D12 N0+1+N1=N2 D12P
    <- disjoint/>-inversion D34 N5+1+N4=N3 D34P
    <- disjoint-join-cross-comparable D12P C1+CX2=C D34P CX3+C4=C 
	C1X3 C14 CX2X3 CX24
	C1X3+C14=C1 CX2X3+CX24=CX2 C1X3+CX2X3=CX3 C14+CX24=C4
    <- shift-total C1X3<<N=CY13
    <- shift-total CX2X3<<N=C23
    <- shift-total CX24<<N=C2Y4
    <- shift-left-preserves-join C1X3+C14=C1 C1X3<<N=CY13 CY13+CY1Y4=CY1
    <- plus-swap-succ N0+1+N1=N2 N0+N1+1=N2
    <- plus-commutative N0+N1+1=N2 N1+1+N0=N2
    <- shift-preserves-join* CX2X3+CX24=CX2 
	CX2X3<<N=C23 CX24<<N=C2Y4 (shift/+ N1+1+N0=N2) C23+C2Y4=C2
    <- plus-swap-succ N5+1+N4=N3 N5+N4+1=N3
    <- plus-commutative N5+N4+1=N3 N4+1+N5=N3
    <- shift-preserves-join* C1X3+CX2X3=CX3
	C1X3<<N=CY13 CX2X3<<N=C23 (shift/+ N4+1+N5=N3) CY13+C23=C3
    <- shift-right-preserves-join C14+CX24=C4 CX24<<N=C2Y4 CY1Y4+C2Y4=CY4.

- : disjoint-join-cross-comparable 
	D12 (join/> CX1+C2=C N0+1+N2=N1)
        D34 (join/< C3+CX4=C N5+1+N3=N4) _ _ _ _ % NB N2=N3=N
 	C1Y3+C14=C1 CY2Y3+CY24=CY2 C1Y3+CY2Y3=CY3 C14+CY24=C4
    <- disjoint/>-inversion D12 N0+1+N2=N1 D12P
    <- disjoint/<-inversion D34 N5+1+N3=N4 D34P
    <- disjoint-join-cross-comparable D12P CX1+C2=C D34P C3+CX4=C 
	CX13 CX1X4 C23 C2X4
	CX13+CX1X4=CX1 C23+C2X4=C2 CX13+C23=C3 CX1X4+C2X4=CX4
    <- shift-total CX13<<N=C1Y3
    <- shift-total CX1X4<<N=C14
    <- shift-total C2X4<<N=CY24
    <- plus-swap-succ N0+1+N2=N1 N0+N2+1=N1
    <- plus-commutative N0+N2+1=N1 N2+1+N0=N1
    <- shift-preserves-join* CX13+CX1X4=CX1 
	CX13<<N=C1Y3 CX1X4<<N=C14 (shift/+ N2+1+N0=N1) C1Y3+C14=C1
    <- shift-right-preserves-join C23+C2X4=C2 C2X4<<N=CY24 CY2Y3+CY24=CY2
    <- shift-left-preserves-join CX13+C23=C3 CX13<<N=C1Y3 C1Y3+CY2Y3=CY3
    <- plus-swap-succ N5+1+N3=N4 N5+N3+1=N4
    <- plus-commutative N5+N3+1=N4 N3+1+N5=N4
    <- shift-preserves-join* CX1X4+C2X4=CX4
	CX1X4<<N=C14 C2X4<<N=CY24 (shift/+ N3+1+N5=N4) C14+CY24=C4.

- : disjoint-join-cross-comparable 
	D12 (join/> CX1+C2=C N0+1+N2=N1)
	D34 (join/> CX3+C4=C N5+1+N4=N3) _ _ _ _ % NB N2=N4=N
	C13+C1Y4=C1 CY23+CY2Y4=CY2 C13+CY23=C3 C1Y4+CY2Y4=CY4
    <- disjoint/>-inversion D12 N0+1+N2=N1 D12P
    <- disjoint/>-inversion D34 N5+1+N4=N3 D34P
    <- disjoint-join-cross-comparable D12P CX1+C2=C D34P CX3+C4=C 
	CX1X3 CX14 C2X3 C2C4
	CX1X3+CX14=CX1 C2X3+C24=C2 CX1X3+C2X3=CX3 CX14+C24=C4
    <- shift-total CX1X3<<N=C13
    <- shift-total CX14<<N=C1Y4
    <- shift-total C2X3<<N=CY23
    <- plus-swap-succ N0+1+N2=N1 N0+N2+1=N1
    <- plus-commutative N0+N2+1=N1 N2+1+N0=N1
    <- shift-preserves-join* CX1X3+CX14=CX1 
	CX1X3<<N=C13 CX14<<N=C1Y4 (shift/+ N2+1+N0=N1) C13+C1Y4=C1
    <- shift-left-preserves-join C2X3+C24=C2 C2X3<<N=CY23 CY23+CY2Y4=CY2
    <- plus-swap-succ N5+1+N4=N3 N5+N4+1=N3
    <- plus-commutative N5+N4+1=N3 N4+1+N5=N3
    <- shift-preserves-join* CX1X3+C2X3=CX3
	CX1X3<<N=C13 C2X3<<N=CY23 (shift/+ N4+1+N5=N3) C13+CY23=C3
    <- shift-left-preserves-join CX14+C24=C4 CX14<<N=C1Y4 C1Y4+CY2Y4=CY4.

%worlds (WORLDS) (disjoint-join-cross-comparable _ _ _ _ _ _ _ _ _ _ _ _).
%total (A) (disjoint-join-cross-comparable _ A _ _ _ _ _ _ _ _ _ _).


%theorem disjoint-join-left-preserves-lookup* :
	forall* {M1} {M2} {M3} {N} {D}
	forall {L2:lookup M2 N D}
               {X:disjoint M1 M2}
               {A:join M1 M2 M3}
	exists {L3:lookup M3 N D}
	true.

%theorem disjoint-join-left-preserves-lookup*/L :
	forall* {M1} {M2} {M3} {N} {D} {B}
	forall {L2:lookup M2 N D}
               {X:disjoint M1 M2}
               {A:join M1 M2 M3}
               {D?:domain? M1 N B}
	exists {L3:lookup M3 N D}
	true.

- : disjoint-join-left-preserves-lookup* L2 X A L3
    <- domain?-total D?
    <- disjoint-join-left-preserves-lookup*/L L2 X A D? L3.

- : disjoint-join-left-preserves-lookup*/L L2 X A (domain?/in L1) L3
    <- disjoint-lookup-contradiction X L1 L2 F
    <- false-implies-lookup F L3.

- : disjoint-join-left-preserves-lookup*/L L2 X A (domain?/out F1) L3
    <- fresh-join-left-preserves-lookup* F1 L2 A L3.

%worlds (WORLDS) (disjoint-join-left-preserves-lookup*/L _ _ _ _ _).
%total { } (disjoint-join-left-preserves-lookup*/L _ _ _ _ _).

%worlds (WORLDS) (disjoint-join-left-preserves-lookup* _ _ _ _).
%total { } (disjoint-join-left-preserves-lookup* _ _ _ _).


%theorem disjoint-join-right-preserves-lookup* :
	forall* {M1} {M2} {M3} {N} {D}
	forall {L1:lookup M1 N D}
               {X:disjoint M1 M2}
               {A:join M1 M2 M3}
	exists {L3:lookup M3 N D}
	true.

%theorem disjoint-join-right-preserves-lookup*/L :
	forall* {M1} {M2} {M3} {N} {D} {B}
	forall {L1:lookup M1 N D}
               {X:disjoint M1 M2}
               {A:join M1 M2 M3}
               {D?:domain? M2 N B}
	exists {L3:lookup M3 N D}
	true.

- : disjoint-join-right-preserves-lookup* L1 X A L3
    <- domain?-total D?
    <- disjoint-join-right-preserves-lookup*/L L1 X A D? L3.

- : disjoint-join-right-preserves-lookup*/L L1 X A (domain?/in L2) L3
    <- disjoint-lookup-contradiction X L1 L2 F
    <- false-implies-lookup F L3.

- : disjoint-join-right-preserves-lookup*/L L1 X A (domain?/out F2) L3
    <- fresh-join-right-preserves-lookup* L1 F2 A L3.

%worlds (WORLDS) (disjoint-join-right-preserves-lookup*/L _ _ _ _ _).
%total { } (disjoint-join-right-preserves-lookup*/L _ _ _ _ _).

%worlds (WORLDS) (disjoint-join-right-preserves-lookup* _ _ _ _).
%total { } (disjoint-join-right-preserves-lookup* _ _ _ _).

%{%
#ifdef DATA_JOIN_CROSS_COMPARABLE
%}%

%theorem join-cross-comparable :
	forall* {C} {C1} {C2} {C3} {C4}
	forall {A12:join C1 C2 C} {A34:join C3 C4 C}
	exists {C13} {C14} {C23} {C24}
	       {A1:join C13 C14 C1} {A2:join C23 C24 C2}
	       {A3:join C13 C23 C3} {A4:join C14 C24 C4}
	true.

- : join-cross-comparable join/L C3+C4=C2 _ _ _ _ join/L C3+C4=C2 join/L join/L.

- : join-cross-comparable join/R C3+C4=C1 _ _ _ _ C3+C4=C1 join/R join/R join/R.

- : join-cross-comparable C1+C2=C4 join/L _ _ _ _ join/L join/L join/L C1+C2=C4.

- : join-cross-comparable C1+C2=C3 join/R _ _ _ _ join/R join/R C1+C2=C3 join/R.

- : join-cross-comparable (join/= C1+C2=C Q1+Q2=Q nat`eq/)
                         (join/= C3+C4=C Q3+Q4=Q nat`eq/) _ _ _ _
                         (join/= C13+C14=C1 Q13+Q14=Q1 nat`eq/)
                         (join/= C23+C24=C2 Q23+Q24=Q2 nat`eq/)
                         (join/= C13+C23=C3 Q13+Q23=Q3 nat`eq/)
                         (join/= C14+C24=C4 Q14+Q24=Q4 nat`eq/)
    <- join-cross-comparable C1+C2=C C3+C4=C _ _ _ _ 
                            C13+C14=C1 C23+C24=C2 C13+C23=C3 C14+C24=C4
    <- data`join-cross-comparable Q1+Q2=Q Q3+Q4=Q _ _ _ _ 
                                Q13+Q14=Q1 Q23+Q24=Q2 Q13+Q23=Q3 Q14+Q24=Q4.

- : join-cross-comparable (join/= C1+C2=C Q1+Q2=Q nat`eq/)
                         (join/< C3+CX4=C N5+1+N3=N4) _ _ _ _
                         CY1Y3+CY14=CY1
                         CY2Y3+CY24=CY2
			 (join/= C13+C23=C3 Q1+Q2=Q nat`eq/)
			 CY14+CY24=C4
    <- join-cross-comparable C1+C2=C C3+CX4=C C13 C1X4 C23 C2X4 
                            C13+C1X4=C1 C23+C2X4=C2 C13+C23=C3 C1X4+C2X4=CX4
    <- shift-total C1X4<<N=CY14
    <- shift-total C2X4<<N=CY24
    <- plus-swap-succ N5+1+N3=N4 N5+N3+1=N4
    <- plus-commutative N5+N3+1=N4 N3+1+N5=N4
    <- shift-right-preserves-join C13+C1X4=C1 C1X4<<N=CY14 CY1Y3+CY14=CY1
    <- shift-right-preserves-join C23+C2X4=C2 C2X4<<N=CY24 CY2Y3+CY24=CY2
    <- shift-preserves-join* C1X4+C2X4=CX4 C1X4<<N=CY14 C2X4<<N=CY24 
                             (shift/+ N3+1+N5=N4) CY14+CY24=C4.

- : join-cross-comparable (join/= C1+C2=C Q1+Q2=Q nat`eq/)
                         (join/> CX3+C4=C N5+1+N4=N3) _ _ _ _
                         CY13+CY1Y4=CY1
                         CY23+CY2Y4=CY2
			 CY13+CY23=C3
			 (join/= C14+C24=C4 Q1+Q2=Q nat`eq/)
    <- join-cross-comparable C1+C2=C CX3+C4=C C1X3 C14 C2X3 C24 
                            C1X3+C14=C1 C2X3+C24=C2 C1X3+C2X3=CX3 C14+C24=C4
    <- shift-total C1X3<<N=CY13
    <- shift-total C2X3<<N=CY23
    <- plus-swap-succ N5+1+N4=N3 N5+N4+1=N3
    <- plus-commutative N5+N4+1=N3 N4+1+N5=N3
    <- shift-left-preserves-join C1X3+C14=C1 C1X3<<N=CY13 CY13+CY1Y4=CY1
    <- shift-left-preserves-join C2X3+C24=C2 C2X3<<N=CY23 CY23+CY2Y4=CY2
    <- shift-preserves-join* C1X3+C2X3=CX3 C1X3<<N=CY13 C2X3<<N=CY23 
                             (shift/+ N4+1+N5=N3) CY13+CY23=C3.


- : join-cross-comparable (join/< C1+CX2=C N0+1+N1=N2)
			 (join/= C3+C4=C Q3+Q4=Q nat`eq/) _ _ _ _
			 (join/= C13+C14=C1 Q3+Q4=Q nat`eq/)
                         C23+C24=C2
                         CY1Y3+C2Y3=CY3
                         CY1Y4+C2Y4=CY4
    <- join-cross-comparable C1+CX2=C C3+C4=C C13 C14 CX23 CX24 
                            C13+C14=C1 CX23+CX24=CX2 C13+CX23=C3 C14+CX24=C4
    <- shift-total CX23<<N=C2Y3
    <- shift-total CX24<<N=C2Y4
    <- plus-swap-succ N0+1+N1=N2 N0+N1+1=N2
    <- plus-commutative N0+N1+1=N2 N1+1+N0=N2
    <- shift-right-preserves-join C13+CX23=C3 CX23<<N=C2Y3 CY1Y3+C2Y3=CY3
    <- shift-right-preserves-join C14+CX24=C4 CX24<<N=C2Y4 CY1Y4+C2Y4=CY4
    <- shift-preserves-join* CX23+CX24=CX2 CX23<<N=C2Y3 CX24<<N=C2Y4 
			     (shift/+ N1+1+N0=N2) C23+C24=C2.

- : join-cross-comparable (join/> CX1+C2=C N0+1+N2=N1)
			 (join/= C3+C4=C Q3+Q4=Q nat`eq/) _ _ _ _
                         C13+C14=C1
			 (join/= C23+C24=C2 Q3+Q4=Q nat`eq/)
			 C1Y3+CY2Y3=CY3
                         C1Y4+CY2Y4=CY4
    <- join-cross-comparable CX1+C2=C C3+C4=C _ _ _ _ 
                            CX13+CX14=CX1 C23+C24=C2 CX13+C23=C3 CX14+C24=C4
    <- shift-total CX13<<N=C1Y3
    <- shift-total CX14<<N=C1Y4
    <- plus-swap-succ N0+1+N2=N1 N0+N2+1=N1
    <- plus-commutative N0+N2+1=N1 N2+1+N0=N1
    <- shift-left-preserves-join CX13+C23=C3 CX13<<N=C1Y3 C1Y3+CY2Y3=CY3
    <- shift-left-preserves-join CX14+C24=C4 CX14<<N=C1Y4 C1Y4+CY2Y4=CY4
    <- shift-preserves-join* CX13+CX14=CX1 CX13<<N=C1Y3 CX14<<N=C1Y4 
			     (shift/+ N2+1+N0=N1) C13+C14=C1.

- : join-cross-comparable (join/< C1+CX2=C N0+1+N1=N2)
                         (join/< C3+CX4=C N5+1+N3=N4) _ _ _ _ % NB N1=N3=N
                         CY1Y3+CY14=CY1
			 C2Y3+C24=C2
			 CY1Y3+C2Y3=CY3
			 CY14+C24=C4
    <- join-cross-comparable C1+CX2=C C3+CX4=C C13 C1X4 CX23 CX2X4
	C13+C1X4=C1 CX23+CX2X4=CX2 C13+CX23=C3 C1X4+CX2X4=CX4
    <- shift-total C1X4<<N=CY14
    <- shift-total CX23<<N=C2Y3
    <- shift-total CX2X4<<N=C24
    <- plus-swap-succ N0+1+N1=N2 N0+N1+1=N2
    <- plus-commutative N0+N1+1=N2 N1+1+N0=N2
    <- shift-right-preserves-join C13+C1X4=C1 C1X4<<N=CY14 CY1Y3+CY14=CY1
    <- shift-preserves-join* CX23+CX2X4=CX2 
	CX23<<N=C2Y3 CX2X4<<N=C24 (shift/+ N1+1+N0=N2) C2Y3+C24=C2
    <- shift-right-preserves-join C13+CX23=C3 CX23<<N=C2Y3 CY1Y3+C2Y3=CY3
    <- plus-swap-succ N5+1+N3=N4 N5+N3+1=N4
    <- plus-commutative N5+N3+1=N4 N3+1+N5=N4
    <- shift-preserves-join* C1X4+CX2X4=CX4
	C1X4<<N=CY14 CX2X4<<N=C24 (shift/+ N3+1+N5=N4) CY14+C24=C4.

- : join-cross-comparable (join/< C1+CX2=C N0+1+N1=N2)
                         (join/> CX3+C4=C N5+1+N4=N3) _ _ _ _ % NB N1=N4=N
                         CY13+CY1Y4=CY1
			 C23+C2Y4=C2
			 CY13+C23=C3
			 CY1Y4+C2Y4=CY4
    <- join-cross-comparable C1+CX2=C CX3+C4=C C1X3 C14 CX2X3 CX24
	C1X3+C14=C1 CX2X3+CX24=CX2 C1X3+CX2X3=CX3 C14+CX24=C4
    <- shift-total C1X3<<N=CY13
    <- shift-total CX2X3<<N=C23
    <- shift-total CX24<<N=C2Y4
    <- shift-left-preserves-join C1X3+C14=C1 C1X3<<N=CY13 CY13+CY1Y4=CY1
    <- plus-swap-succ N0+1+N1=N2 N0+N1+1=N2
    <- plus-commutative N0+N1+1=N2 N1+1+N0=N2
    <- shift-preserves-join* CX2X3+CX24=CX2 
	CX2X3<<N=C23 CX24<<N=C2Y4 (shift/+ N1+1+N0=N2) C23+C2Y4=C2
    <- plus-swap-succ N5+1+N4=N3 N5+N4+1=N3
    <- plus-commutative N5+N4+1=N3 N4+1+N5=N3
    <- shift-preserves-join* C1X3+CX2X3=CX3
	C1X3<<N=CY13 CX2X3<<N=C23 (shift/+ N4+1+N5=N3) CY13+C23=C3
    <- shift-right-preserves-join C14+CX24=C4 CX24<<N=C2Y4 CY1Y4+C2Y4=CY4.


- : join-cross-comparable (join/> CX1+C2=C N0+1+N2=N1)
                         (join/< C3+CX4=C N5+1+N3=N4) _ _ _ _ % NB N2=N3=N
			 C1Y3+C14=C1
                         CY2Y3+CY24=CY2
			 C1Y3+CY2Y3=CY3
			 C14+CY24=C4
    <- join-cross-comparable CX1+C2=C C3+CX4=C CX13 CX1X4 C23 C2X4
	CX13+CX1X4=CX1 C23+C2X4=C2 CX13+C23=C3 CX1X4+C2X4=CX4
    <- shift-total CX13<<N=C1Y3
    <- shift-total CX1X4<<N=C14
    <- shift-total C2X4<<N=CY24
    <- plus-swap-succ N0+1+N2=N1 N0+N2+1=N1
    <- plus-commutative N0+N2+1=N1 N2+1+N0=N1
    <- shift-preserves-join* CX13+CX1X4=CX1 
	CX13<<N=C1Y3 CX1X4<<N=C14 (shift/+ N2+1+N0=N1) C1Y3+C14=C1
    <- shift-right-preserves-join C23+C2X4=C2 C2X4<<N=CY24 CY2Y3+CY24=CY2
    <- shift-left-preserves-join CX13+C23=C3 CX13<<N=C1Y3 C1Y3+CY2Y3=CY3
    <- plus-swap-succ N5+1+N3=N4 N5+N3+1=N4
    <- plus-commutative N5+N3+1=N4 N3+1+N5=N4
    <- shift-preserves-join* CX1X4+C2X4=CX4
	CX1X4<<N=C14 C2X4<<N=CY24 (shift/+ N3+1+N5=N4) C14+CY24=C4.

- : join-cross-comparable (join/> CX1+C2=C N0+1+N2=N1)
                         (join/> CX3+C4=C N5+1+N4=N3) _ _ _ _ % NB N2=N4=N
			 C13+C1Y4=C1
                         CY23+CY2Y4=CY2
			 C13+CY23=C3
			 C1Y4+CY2Y4=CY4
    <- join-cross-comparable CX1+C2=C CX3+C4=C CX1X3 CX14 C2X3 C2C4
	CX1X3+CX14=CX1 C2X3+C24=C2 CX1X3+C2X3=CX3 CX14+C24=C4
    <- shift-total CX1X3<<N=C13
    <- shift-total CX14<<N=C1Y4
    <- shift-total C2X3<<N=CY23
    <- plus-swap-succ N0+1+N2=N1 N0+N2+1=N1
    <- plus-commutative N0+N2+1=N1 N2+1+N0=N1
    <- shift-preserves-join* CX1X3+CX14=CX1 
	CX1X3<<N=C13 CX14<<N=C1Y4 (shift/+ N2+1+N0=N1) C13+C1Y4=C1
    <- shift-left-preserves-join C2X3+C24=C2 C2X3<<N=CY23 CY23+CY2Y4=CY2
    <- plus-swap-succ N5+1+N4=N3 N5+N4+1=N3
    <- plus-commutative N5+N4+1=N3 N4+1+N5=N3
    <- shift-preserves-join* CX1X3+C2X3=CX3
	CX1X3<<N=C13 C2X3<<N=CY23 (shift/+ N4+1+N5=N3) C13+CY23=C3
    <- shift-left-preserves-join CX14+C24=C4 CX14<<N=C1Y4 C1Y4+CY2Y4=CY4.

%worlds (WORLDS) (join-cross-comparable _ _ _ _ _ _ _ _ _ _).
%total (A) (join-cross-comparable A _ _ _ _ _ _ _ _ _).

%{%
#endif /* DATA_JOIN_CROSS_COMPARABLE */
%}%

%theorem fresh-update-implies-unit-join:
	forall*	{M} {N} {D} {MP}
	forall	{F: fresh M N}
		{U: update M N D MP}
	exists	{J: join M (map/+ N D map/0) MP}
	true.

- : fresh-update-implies-unit-join
	fresh/0 update/0 join/L.

- : fresh-update-implies-unit-join
	(fresh/< N1>N2) U J
    <- nat`gt-implies-plus N1>N2 N3 SN3+N2=N1
    <- update/<-inversion U SN3+N2=N1 EQ
    <- join-respects-eq  (join/> join/R SN3+N2=N1) eq/ eq/ EQ J.

- : fresh-update-implies-unit-join
	(fresh/> F SN0+N1=N2) U J
    <- update/>-inversion U SN0+N1=N2 M1 U1 EQ
    <- fresh-update-implies-unit-join F U1 JP
    <- join-respects-eq (join/< JP SN0+N1=N2) eq/ eq/ EQ J.

%worlds (WORLDS) (fresh-update-implies-unit-join _ _ _).
%total (F) (fresh-update-implies-unit-join F _ _).


%theorem lookup-update-left-implies-unit-join:
	forall*	{M1} {N} {D1} {D2} {D3} {M3}
	forall	{L: lookup M1 N D1}
		{D: data`join D1 D2 D3}
		{U: update M1 N D3 M3}
	exists	{J: join M1 (map/+ N D2 map/0) M3}
	true.

- : lookup-update-left-implies-unit-join
	(lookup/= nat`eq/) DJ U JP
    <- update/=-inversion U nat`eq/ EQ3
    <- join-respects-eq (join/= join/R DJ nat`eq/) eq/ eq/ EQ3 JP.

- : lookup-update-left-implies-unit-join
	(lookup/> L0 P) DJ U JP
    <- update/>-inversion U P _ U0 EQ3
    <- lookup-update-left-implies-unit-join L0 DJ U0 J0P
    <- join-respects-eq (join/< J0P P) eq/ eq/ EQ3 JP.

%worlds (WORLDS) (lookup-update-left-implies-unit-join _ _ _ _).
%total (L) (lookup-update-left-implies-unit-join L _ _ _).


%theorem lookup-update-right-implies-unit-join:
	forall*	{M2} {N} {D1} {D2} {D3} {M3}
	forall	{L: lookup M2 N D2}
		{D: data`join D1 D2 D3}
		{U: update M2 N D3 M3}
	exists	{J: join (map/+ N D1 map/0) M2 M3}
	true.

- : lookup-update-right-implies-unit-join
	(lookup/= nat`eq/) DJ U JP
    <- update/=-inversion U nat`eq/ EQ3
    <- join-respects-eq (join/= join/L DJ nat`eq/) eq/ eq/ EQ3 JP.

- : lookup-update-right-implies-unit-join
	(lookup/> L0 P) DJ U JP
    <- update/>-inversion U P _ U0 EQ3
    <- lookup-update-right-implies-unit-join L0 DJ U0 J0P
    <- join-respects-eq (join/> J0P P) eq/ eq/ EQ3 JP.

%worlds (WORLDS) (lookup-update-right-implies-unit-join _ _ _ _).
%total (L) (lookup-update-right-implies-unit-join L _ _ _).


%theorem fresh-unit-join-implies-update:
	forall*	{M} {N} {D} {MP}
	forall	{F: fresh M N}
		{J: join M (map/+ N D map/0) MP}
	exists	{U: update M N D MP}
	true.

- : fresh-unit-join-implies-update _ join/L update/0.

- : fresh-unit-join-implies-update
	FR (join/= _ _ nat`eq/) U
    <- fresh-contradiction FR F
    <- false-implies-update F U.

- : fresh-unit-join-implies-update
	F (join/< J1 P) (update/> U1 P)
    <- fresh/>-inversion F P F1
    <- fresh-unit-join-implies-update F1 J1 U1.

- : fresh-unit-join-implies-update
	F (join/> J1 P) U
    <- join-deterministic join/R J1 eq/ eq/ ME
    <- map/+-preserves-eq nat`eq/ data`eq/ ME ME2
    <- update-respects-eq (update/< P) eq/ nat`eq/ data`eq/ ME2 U.

%worlds () (fresh-unit-join-implies-update _ _ _).
%total (J) (fresh-unit-join-implies-update _ J _).


%theorem join-commute-update-left*:
	forall*	{M1} {M2} {M3} {N} {D} {M1P} {M3P}
	forall	{J: join M1 M2 M3}
		{U1: update M1 N D M1P}
		{F2: fresh M2 N}
		{U3: update M3 N D M3P}
	exists	{J: join M1P M2 M3P}
	true.

- : join-commute-update-left*
	join/L update/0 F2 U3 JP
    <- fresh-update-implies-unit-join F2 U3 JPc
    <- fresh-implies-unit-disjoint F2 DJ
    <- disjoint-join-commutative DJ JPc JP.

- : join-commute-update-left*
	join/R U1 fresh/0 U1P J
    <- update-unique U1P U1 eq/ nat`eq/ data`eq/ EQ
    <- join-respects-eq join/R EQ eq/ eq/ J.

- : join-commute-update-left*
	(join/= J0 DJ nat`eq/) U1 F2
	(update/< P) J
    <- update/<-inversion U1 P EQ1
    <- join-respects-eq (join/< (join/= J0 DJ nat`eq/) P) EQ1 eq/ eq/ J.

- : join-commute-update-left*
	(join/= J0 DJ nat`eq/) U1 F2
	(update/= nat`eq/) J
    <- fresh-contradiction F2 F
    <- false-implies-join F J.

- : join-commute-update-left*
	(join/= J0 DJ nat`eq/) U1 F2
	(update/> U30 P) J
    <- update/>-inversion U1 P _ U10 EQ1
    <- fresh/>-inversion F2 P F20
    <- join-commute-update-left* J0 U10 F20 U30 J0P
    <- join-respects-eq (join/= J0P DJ nat`eq/) EQ1 eq/ eq/ J.

- : join-commute-update-left*
	(join/< J0 P0) U1 F2
	(update/< P1) J
    <- update/<-inversion U1 P1 EQ1
    <- plus-swap-succ P1 P1s
    <- plus-commutative P1s P1sc
    <- plus-commutative P0 P0c
    <- plus-associative P1sc P0c D2 P3c P2sc
    <- plus-swap-succ P2sc P2c
    <- plus-commutative P2c P2
    <- plus-commutative P3c P3
    <- join-respects-eq (join/< (join/< J0 P3) P2) EQ1 eq/ eq/ J.

- : join-commute-update-left*
	(join/< J0 P0) U1 F2
	(update/= nat`eq/) J
    <- update/=-inversion U1 nat`eq/ EQ1
    <- join-respects-eq (join/< J0 P0) EQ1 eq/ eq/ J.
    
- : join-commute-update-left*
	(join/< J0 P0) U1 F2
	(update/> U30 P1) J
    <- update/>-inversion U1 P1 _ U10 EQ1
    <- plus-swap-succ P0 P0s
    <- plus-commutative P0s P0sc
    <- plus-swap-succ P1 P1s
    <- plus-commutative P1s P1sc
    <- shift-preserves-fresh-converse* F2 (shift/+ P0sc) P1sc F20
    <- join-commute-update-left* J0 U10 F20 U30 J0P
    <- join-respects-eq (join/< J0P P0) EQ1 eq/ eq/ J.

- : join-commute-update-left*
	(join/> _ _) _ F2
	(update/= nat`eq/) J
    <- fresh-contradiction F2 F
    <- false-implies-join F J.

- : join-commute-update-left*
	(join/> J0 P0) U1 F2
	(update/< P2) JP
    <- plus-swap-succ P2 P2s
    <- plus-commutative P2s P2sc
    <- plus-commutative P0 P0c
    <- plus-associative P2sc P0c D1 P3c P1sc
    <- plus-swap-succ P1sc P1c
    <- plus-commutative P1c P1
    <- plus-commutative P3c P3
    <- update/<-inversion U1 P1 EQ1
    <- join-respects-eq (join/< (join/> J0 P3) P2) EQ1 eq/ eq/ JP.

- : join-commute-update-left*
	(join/> J0 P0) U1 F2
	(update/> U30 P2) J
    <- fresh/>-inversion F2 P2 F20
    <- plus-swap-succ P0 P0s
    <- plus-commutative P0s P0sc
    <- plus-swap-succ P2 P2s
    <- plus-commutative P2s P2sc
    <- shift-preserves-update-converse U1 (shift/+ P0sc) P2sc _ SH1 U10
    <- join-commute-update-left* J0 U10 F20 U30 J0P
    <- shift-left-preserves-join J0P SH1 J.

%worlds (WORLDS) (join-commute-update-left* _ _ _ _ _).
%total (J) (join-commute-update-left* J _ _ _ _).


%theorem join-commute-update-left:
	forall*	{M1} {M2} {M3} {N} {D} {M1P}
	forall	{J: join M1 M2 M3}
		{U1: update M1 N D M1P}
		{F2: fresh M2 N}
	exists	{M3P}
		{U3: update M3 N D M3P}
		{J: join M1P M2 M3P}
	true.

- : join-commute-update-left J U1 F2 _ U3 JP
    <- update-total U3
    <- join-commute-update-left* J U1 F2 U3 JP.

%worlds (WORLDS) (join-commute-update-left _ _ _ _ _ _).
%total { } (join-commute-update-left _ _ _ _ _ _).


%theorem join-commute-update-right*:
	forall*	{M1} {M2} {M3} {N} {D} {M2P} {M3P}
	forall	{J: join M1 M2 M3}
		{F1: fresh M1 N}
		{U2: update M2 N D M2P}
		{U3: update M3 N D M3P}
	exists	{J: join M1 M2P M3P}
	true.

- : join-commute-update-right*
	join/L fresh/0 U2 U2P J
    <- update-unique U2P U2 eq/ nat`eq/ data`eq/ EQ
    <- join-respects-eq join/L eq/ EQ eq/ J.

- : join-commute-update-right*
	join/R F1 update/0 U1 J
    <- fresh-update-implies-unit-join F1 U1 J.

- : join-commute-update-right*
	(join/= J0 DJ nat`eq/) F1 U2
	(update/< P) J
    <- update/<-inversion U2 P EQ2
    <- join-respects-eq (join/> (join/= J0 DJ nat`eq/) P) eq/ EQ2 eq/ J.

- : join-commute-update-right*
	(join/= J0 DJ nat`eq/) F1 U2
	(update/= nat`eq/) J
    <- fresh-contradiction F1 F
    <- false-implies-join F J.

- : join-commute-update-right*
	(join/= J0 DJ nat`eq/) F1 U2
	(update/> U30 P) J
    <- update/>-inversion U2 P _ U20 EQ2
    <- fresh/>-inversion F1 P F10
    <- join-commute-update-right* J0 F10 U20 U30 J0P
    <- join-respects-eq (join/= J0P DJ nat`eq/) eq/ EQ2 eq/ J.

- : join-commute-update-right*
	(join/> J0 P0) F1 U2
	(update/< P2) J
    <- update/<-inversion U2 P2 EQ2
    <- plus-swap-succ P2 P2s
    <- plus-commutative P2s P2sc
    <- plus-commutative P0 P0c
    <- plus-associative P2sc P0c D1 P3c P1sc
    <- plus-swap-succ P1sc P1c
    <- plus-commutative P1c P1
    <- plus-commutative P3c P3
    <- join-respects-eq (join/> (join/> J0 P3) P1) eq/ EQ2 eq/ J.

- : join-commute-update-right*
	(join/> J0 P0) F1 U2
	(update/= nat`eq/) J
    <- update/=-inversion U2 nat`eq/ EQ2
    <- join-respects-eq (join/> J0 P0) eq/ EQ2 eq/ J.
    
- : join-commute-update-right*
	(join/> J0 P0) F1 U2
	(update/> U30 P2) J
    <- update/>-inversion U2 P2 _ U20 EQ2
    <- plus-swap-succ P0 P0s
    <- plus-commutative P0s P0sc
    <- plus-swap-succ P2 P2s
    <- plus-commutative P2s P2sc
    <- shift-preserves-fresh-converse* F1 (shift/+ P0sc) P2sc F10
    <- join-commute-update-right* J0 F10 U20 U30 J0P
    <- join-respects-eq (join/> J0P P0) eq/ EQ2 eq/ J.

- : join-commute-update-right*
	(join/< _ _) F1 _
	(update/= nat`eq/) J
    <- fresh-contradiction F1 F
    <- false-implies-join F J.

- : join-commute-update-right*
	(join/< J0 P0) F1 U2
	(update/< P1) JP
    <- plus-swap-succ P1 P1s
    <- plus-commutative P1s P1sc
    <- plus-commutative P0 P0c
    <- plus-associative P1sc P0c D2 P3c P2sc
    <- plus-swap-succ P2sc P2c
    <- plus-commutative P2c P2
    <- plus-commutative P3c P3
    <- update/<-inversion U2 P2 EQ2
    <- join-respects-eq (join/> (join/< J0 P3) P1) eq/ EQ2 eq/ JP.

- : join-commute-update-right*
	(join/< J0 P0) F1 U2
	(update/> U30 P1) J
    <- fresh/>-inversion F1 P1 F10
    <- plus-swap-succ P0 P0s
    <- plus-commutative P0s P0sc
    <- plus-swap-succ P1 P1s
    <- plus-commutative P1s P1sc
    <- shift-preserves-update-converse U2 (shift/+ P0sc) P1sc _ SH2 U20
    <- join-commute-update-right* J0 F10 U20 U30 J0P
    <- shift-right-preserves-join J0P SH2 J.

%worlds (WORLDS) (join-commute-update-right* _ _ _ _ _).
%total (J) (join-commute-update-right* J _ _ _ _).


%theorem join-commute-update-right:
	forall*	{M1} {M2} {M3} {N} {D} {M2P}
	forall	{J: join M1 M2 M3}
		{F1: fresh M1 N}
		{U2: update M2 N D M2P}
	exists	{M3P}
		{U3: update M3 N D M3P}
		{J: join M1 M2P M3P}
	true.

- : join-commute-update-right J F1 U2 _ U3 JP
    <- update-total U3
    <- join-commute-update-right* J F1 U2 U3 JP.

%worlds (WORLDS) (join-commute-update-right _ _ _ _ _ _).
%total { } (join-commute-update-right _ _ _ _ _ _).


%theorem join-commute-update*:
	forall*	{M1} {M2} {M3} {N} {D1} {M1P} {D2} {D3} {M2P} {M3P}
	forall	{J: join M1 M2 M3}
		{DJ: data`join D1 D2 D3}
		{U1: update M1 N D1 M1P}
		{U2: update M2 N D2 M2P}
		{U3: update M3 N D3 M3P}
	exists	{JP: join M1P M2P M3P}
	true.

- : join-commute-update* join/L DJ update/0 U2 U3 JP
    <- update-implies-lookup U2 L2
    <- update-overwrites-converse U3 U2 U23
    <- lookup-update-right-implies-unit-join L2 DJ U23 JP.

- : join-commute-update* join/R DJ U1 update/0 U3 JP
    <- update-implies-lookup U1 L1
    <- update-overwrites-converse U3 U1 U13
    <- lookup-update-left-implies-unit-join L1 DJ U13 JP.

- : join-commute-update* (join/= J0 DJ0 nat`eq/) DJ U1 U2 (update/< P) JP
    <- update/<-inversion U1 P EQ1
    <- update/<-inversion U2 P EQ2
    <- join-respects-eq 
	(join/= (join/= J0 DJ0 nat`eq/) DJ nat`eq/) EQ1 EQ2 eq/ JP.

- : join-commute-update* (join/= J0 _ nat`eq/) DJ U1 U2 (update/= nat`eq/) JP
    <- update/=-inversion U1 nat`eq/ EQ1
    <- update/=-inversion U2 nat`eq/ EQ2
    <- join-respects-eq (join/= J0 DJ nat`eq/) EQ1 EQ2 eq/ JP.

- : join-commute-update* (join/= J0 DJ0 nat`eq/) DJ U1 U2 (update/> U30 P) JP
    <- update/>-inversion U1 P _ U10 EQ1
    <- update/>-inversion U2 P _ U20 EQ2
    <- join-commute-update* J0 DJ U10 U20 U30 J0P
    <- join-respects-eq (join/= J0P DJ0 nat`eq/) EQ1 EQ2 eq/ JP.

- : join-commute-update* (join/< J0 P0) DJ U1 U2 (update/< P1) JP
    <- update/<-inversion U1 P1 EQ1
    <- plus-swap-succ P1 P1s
    <- plus-commutative P1s P1sc
    <- plus-commutative P0 P0c
    <- plus-associative P1sc P0c D2 P3c P2sc
    <- plus-swap-succ P2sc P2c
    <- plus-commutative P2c P2
    <- plus-commutative P3c P3
    <- update/<-inversion U2 P2 EQ2
    <- join-respects-eq (join/= (join/< J0 P3) DJ nat`eq/) EQ1 EQ2 eq/ JP.

- : join-commute-update* (join/< J0 P0) DJ U1 U2 (update/= nat`eq/) JP
    <- update/=-inversion U1 nat`eq/ EQ1
    <- update/<-inversion U2 P0 EQ2
    <- join-respects-eq (join/= J0 DJ nat`eq/) EQ1 EQ2 eq/ JP.

- : join-commute-update* (join/< J0 P0) DJ U1 U2 (update/> U30 P1) JP
    <- update/>-inversion U1 P1 _ U10 EQ1
    <- plus-swap-succ P0 P0s
    <- plus-commutative P0s P0sc
    <- plus-swap-succ P1 P1s
    <- plus-commutative P1s P1sc
    <- shift-preserves-update-converse U2 (shift/+ P0sc) P1sc _ SH2 U20
    <- join-commute-update* J0 DJ U10 U20 U30 J0P
    <- shift-right-preserves-join J0P SH2 JPP
    <- join-respects-eq JPP EQ1 eq/ eq/ JP.

- : join-commute-update* (join/> J0 P0) DJ U1 U2 (update/< P2) JP
    <- update/<-inversion U2 P2 EQ2
    <- plus-swap-succ P2 P2s
    <- plus-commutative P2s P2sc
    <- plus-commutative P0 P0c
    <- plus-associative P2sc P0c D1 P3c P1sc
    <- plus-swap-succ P1sc P1c
    <- plus-commutative P1c P1
    <- plus-commutative P3c P3
    <- update/<-inversion U1 P1 EQ1
    <- join-respects-eq (join/= (join/> J0 P3) DJ nat`eq/) EQ1 EQ2 eq/ JP.

- : join-commute-update* (join/> J0 P0) DJ U1 U2 (update/= nat`eq/) JP
    <- update/<-inversion U1 P0 EQ1
    <- update/=-inversion U2 nat`eq/ EQ2
    <- join-respects-eq (join/= J0 DJ nat`eq/) EQ1 EQ2 eq/ JP.

- : join-commute-update* (join/> J0 P0) DJ U1 U2 (update/> U30 P2) JP
    <- plus-swap-succ P0 P0s
    <- plus-commutative P0s P0sc
    <- plus-swap-succ P2 P2s
    <- plus-commutative P2s P2sc
    <- shift-preserves-update-converse U1 (shift/+ P0sc) P2sc _ SH1 U10
    <- update/>-inversion U2 P2 _ U20 EQ2
    <- join-commute-update* J0 DJ U10 U20 U30 J0P
    <- shift-left-preserves-join J0P SH1 JPP
    <- join-respects-eq JPP eq/ EQ2 eq/ JP.

%worlds (WORLDS) (join-commute-update* _ _ _ _ _ _).
%total (J) (join-commute-update* J _ _ _ _ _).


%theorem join-commute-update:
	forall*	{M1} {M2} {M3} {N} {D1} {M1P} {D2} {D3} {M2P}
	forall	{J: join M1 M2 M3}
		{DJ: data`join D1 D2 D3}
		{U1: update M1 N D1 M1P}
		{U2: update M2 N D2 M2P}
	exists	{M3P}
		{U3: update M3 N D3 M3P}
		{JP: join M1P M2P M3P}
	true.

- : join-commute-update J DJ U1 U2 _ U3 JP
    <- update-total U3
    <- join-commute-update* J DJ U1 U2 U3 JP.

%worlds (WORLDS) (join-commute-update _ _ _ _ _ _ _).
%total { } (join-commute-update _ _ _ _ _ _ _).


%theorem join-preserves-fresh-update-left: 
	forall* {M1} {M1S} {M2S} {MS} {N} {D} 
	forall	{J: join M1S M2S MS}
		{F1: fresh M1S N}
    		{U1: update M1S N D M1}
    		{F2: fresh M2S N}
    	exists 	{M} 
		{F: fresh MS N}
    		{U: update MS N D M}
    		{J: join M1 M2S M}
    	true.

- : join-preserves-fresh-update-left J F1 U1 F2 _ F3 U3 JP
    <- join-commute-update-left J U1 F2 _ U3 JP
    <- join-preserves-fresh* F1 F2 J F3.

%worlds (WORLDS) (join-preserves-fresh-update-left _ _ _ _ _ _ _ _).
%total {} (join-preserves-fresh-update-left _ _ _ _ _ _ _ _).


%theorem fresh-update-left-preserves-join-converse :
	forall* {M1} {M1P} {M2} {M} {N} {D}
	forall	{F1: fresh M1P N}
    		{U1: update M1P N D M1}
    		{F2: fresh M2 N}
    		{J: join M1 M2 M}
 	exists  {MP} 
		{F: fresh MP N}
		{U: update MP N D M}
		{J: join M1P M2 MP}
	true.

%theorem fresh-update-left-preserves-join-converse/L :
	forall* {N1} {D1} {M1} {M1P} 
		{N2} {D2} {M2} 
		{N3} {D3} {M3} {N} {D} {B}
	forall	{F1: fresh M1P N}
    		{U1: update M1P N D (map/+ N1 D1 M1)}
    		{F2: fresh (map/+ N2 D2 M2) N}
    		{J: join (map/+ N1 D1 M1) (map/+ N2 D2 M2) (map/+ N3 D3 M3)}
		{GE1: nat`ge? N N1 B}
 	exists  {MP} 
		{F: fresh MP N}
		{U: update MP N D (map/+ N3 D3 M3)}
		{J: join M1P (map/+ N2 D2 M2) MP}
	true.

- : fresh-update-left-preserves-join-converse/L F1P U1 _ _
	(nat`ge?/no GT) map/0 F3 U3 J3
    <- update-implies-lookup U1 L1
    <- fresh-lookup-not-equal (fresh/< GT) L1 NE
    <- nat`eq-ne-implies-false nat`eq/ NE F
    <- false-implies-fresh F F3
    <- false-implies-update F U3
    <- false-implies-join F J3.

- : fresh-update-left-preserves-join-converse/L F1P U1 F2
	(join/= J0 DA nat`eq/) (nat`ge?/yes (nat`ge/= nat`eq/)) map/0 F3 U3 J3
    <- fresh-lookup-not-equal F2 (lookup/= nat`eq/) NE
    <- nat`eq-ne-implies-false nat`eq/ NE F
    <- false-implies-fresh F F3
    <- false-implies-update F U3
    <- false-implies-join F J3.

- : fresh-update-left-preserves-join-converse/L F1P U1 F2
	(join/= J0 DA nat`eq/) (nat`ge?/yes (nat`ge/> GT)) _
	(fresh/> F30 P) (update/> U30 P) J
    <- nat`gt-implies-plus GT _ P
    <- fresh-lookup-not-equal F2 (lookup/= nat`eq/) NE
    <- update/>-inversion* U1 P _ U10 (EQ1: eq _ M1)
    <- eq-symmetric EQ1 EQ1s
    <- fresh-respects-eq F1P EQ1s nat`eq/ F1PP
    <- fresh/>-inversion F1PP P F1P0
    <- fresh/>-inversion F2 P F20
    <- fresh-update-left-preserves-join-converse F1P0 U10 F20 J0 _ F30 U30 J0P
    <- join-respects-eq (join/= J0P DA nat`eq/) EQ1 eq/ eq/ J.

- : fresh-update-left-preserves-join-converse/L F1P U1 _
	(join/< J0 P) (nat`ge?/yes (nat`ge/= nat`eq/)) _ F3 U3P J3
    <- fresh-update-implies-shift F1P U1 SH1 D1=D2
    <- plus-swap-succ P Ps
    <- plus-commutative Ps Psc
    <- shift-preserves-join J0 SH1 (shift/+ Psc) _ SH3 J3
    <- shift-implies-fresh SH3 F3
    <- shift-implies-update SH3 U3
    <- update-respects-eq U3 eq/ nat`eq/ D1=D2 eq/ U3P.

- : fresh-update-left-preserves-join-converse/L F1P (U1:update M N D _) F2
	(join/< J0 (P0: plus (s D0) N1 N2))
	(nat`ge?/yes (nat`ge/> (N>N1:gt N N1)) ) _
	(fresh/> F30 P1) 
	(update/> U30 P1)
	J3
    <- gt-implies-plus N>N1 D1 P1
    <- update/>-inversion* U1 P1 _ U10 EQ1
    <- eq-symmetric EQ1 EQ1s
    <- fresh-respects-eq F1P EQ1s nat`eq/ F1PP
    <- fresh/>-inversion F1PP P1 F1P0
    <- plus-swap-succ P0 P0s
    <- plus-swap-succ P1 P1s
    <- plus-commutative P0s P0sc
    <- plus-commutative P1s P1sc
    <- shift-preserves-fresh-converse* F2 (shift/+ P0sc) P1sc F20
    <- fresh-update-left-preserves-join-converse F1P0 U10 (F20:fresh _ D1) J0 _
	 F30 U30 J0P
    <- join-respects-eq (join/< J0P P0) EQ1 eq/ eq/ J3.

- : fresh-update-left-preserves-join-converse/L F1P (U1:update M N _ _) F2
	(join/> J0 (P0: plus (s D0) N2 N1))
	(nat`ge?/yes N>=N1) _
	(fresh/> F30 P2) (update/> U30 P2) J3
    <- ge-implies-plus (N>=N1:nat`ge N N1) D1 P1
    <- plus-swap-succ P0 P0s
    <- plus-commutative P0s P0sc 
    <- plus-commutative P1 P1c
    <- plus-associative P0sc P1c D2 D0+D1=D2 P2sc
    <- plus-swap-succ P2sc P2c
    <- plus-commutative P2c P2
    <- update-commutes-shift-converse* U1 (shift/+ P0sc) P2sc _ SH U10
    <- shift-preserves-fresh-converse* F1P SH P2sc F1P0
    <- fresh/>-inversion F2 P2 F20
    <- fresh-update-left-preserves-join-converse F1P0 U10 (F20:fresh _ D2) J0 _
	 F30 U30 J0P
    <- shift-left-preserves-join J0P SH J3.

- : fresh-update-left-preserves-join-converse F1P U1 _
	join/R _ F1P U1 join/R.

- : fresh-update-left-preserves-join-converse F1P U1 F2 J _ F3 U3 J3
    <- nat`ge?-total GE?
    <- fresh-update-left-preserves-join-converse/L F1P U1 F2 J GE? _ F3 U3 J3.

%worlds (WORLDS) 
	(fresh-update-left-preserves-join-converse _ _ _ _ _ _ _ _)
	(fresh-update-left-preserves-join-converse/L _ _ _ _ _ _ _ _ _).
%total (J1 J2)
	(fresh-update-left-preserves-join-converse/L _ _ _ J1 _ _ _ _ _)
	(fresh-update-left-preserves-join-converse _ _ _ J2 _ _ _ _).


%theorem fresh-update-left-preserves-join-converse* :
	forall* {M1} {M1P} {M2} {M} {MP} {N} {D} 
	forall	{F1: fresh M1P N}
		{U1: update M1P N D M1}
		{F2: fresh M2 N}
		{J: join M1 M2 M}
		{F: fresh MP N}
		{U: update MP N D M}
	exists	{J: join M1P M2 MP}
	true.

- : fresh-update-left-preserves-join-converse*
    N!<M1P M1P+N+D=M1 N!<M2 M1+M2=M N!<MP MP+N+D=M M1P+M2=MP
    <- fresh-update-left-preserves-join-converse
      N!<M1P M1P+N+D=M1 N!<M2 M1+M2=M MQ N!<MQ MQ+N+D=M M1P+M2=MQ
    <- fresh-update-cancels N!<MQ MQ+N+D=M N!<MP MP+N+D=M nat`eq/ eq/ _ ME
    <- join-respects-eq M1P+M2=MQ eq/ eq/ ME M1P+M2=MP.

%worlds (WORLDS) (fresh-update-left-preserves-join-converse* _ _ _ _ _ _ _).
%total {} (fresh-update-left-preserves-join-converse* _ _ _ _ _ _ _).


%theorem fresh-update-right-preserves-join-converse :
	forall* {M1} {M2P} {M2} {M} {N} {D}
	forall	{F1: fresh M1 N}
    		{F2: fresh M2P N}
    		{U2: update M2P N D M2}
    		{J: join M1 M2 M}
 	exists  {MP} 
		{F: fresh MP N}
		{U: update MP N D M}
		{J: join M1 M2P MP}
	true.

%theorem fresh-update-right-preserves-join-converse/L :
	forall* {N1} {D1} {M1} {M2P} 
		{N2} {D2} {M2} {M} {N} {D} {B}
	forall	{F1: fresh (map/+ N1 D1 M1) N}
    		{F2: fresh M2P N}
    		{U2: update M2P N D (map/+ N2 D2 M2)}
    		{J: join (map/+ N1 D1 M1) (map/+ N2 D2 M2) M}
		{GE1: nat`ge? N N2 B}
 	exists  {MP} 
		{F: fresh MP N}
		{U: update MP N D M}
		{J: join (map/+ N1 D1 M1) M2P MP}
	true.

- : fresh-update-right-preserves-join-converse/L F1 F2P U2 _
	(nat`ge?/no GT) map/0 F3 U3 J3
    <- update-implies-lookup U2 L2
    <- fresh-lookup-not-equal (fresh/< GT) L2 NE
    <- nat`eq-ne-implies-false nat`eq/ NE F
    <- false-implies-fresh F F3
    <- false-implies-update F U3
    <- false-implies-join F J3.

- : fresh-update-right-preserves-join-converse/L F1 F2P U2
	(join/= J0 DA nat`eq/) (nat`ge?/yes (nat`ge/= nat`eq/)) map/0 F3 U3 J3
    <- fresh-lookup-not-equal F1 (lookup/= nat`eq/) NE
    <- nat`eq-ne-implies-false nat`eq/ NE F
    <- false-implies-fresh F F3
    <- false-implies-update F U3
    <- false-implies-join F J3.

- : fresh-update-right-preserves-join-converse/L F1 F2P U2
	(join/= J0 DA nat`eq/) (nat`ge?/yes (nat`ge/> GT)) _
	(fresh/> F30 P) (update/> U30 P) J
    <- nat`gt-implies-plus GT _ P
    <- fresh-lookup-not-equal F1 (lookup/= nat`eq/) NE
    <- update/>-inversion* U2 P M22 U20 EQ2
    <- eq-symmetric EQ2 EQ2s
    <- fresh-respects-eq F2P EQ2s nat`eq/ F2PP
    <- fresh/>-inversion F2PP P F2P0
    <- fresh/>-inversion F1 P F10
    <- fresh-update-right-preserves-join-converse F10 F2P0 U20 J0 M0 F30 U30 J0P
    <- join-respects-eq (join/= J0P DA nat`eq/) eq/ EQ2 eq/ J.

- : fresh-update-right-preserves-join-converse/L _ F2P U2
	(join/> J0 P) (nat`ge?/yes (nat`ge/= nat`eq/)) _ F3 U3P J3
    <- fresh-update-implies-shift F2P U2 SH2 D1=D2
    <- plus-swap-succ P Ps
    <- plus-commutative Ps Psc
    <- shift-preserves-join J0 (shift/+ Psc) SH2 _ SH3 J3
    <- shift-implies-fresh SH3 F3
    <- shift-implies-update SH3 U3
    <- update-respects-eq U3 eq/ nat`eq/ D1=D2 eq/ U3P.

- : fresh-update-right-preserves-join-converse/L F1 F2P (U2:update M N D _)
	(join/> J0 (P0: plus (s D0) N2 N1))
	(nat`ge?/yes (nat`ge/> (N>N2:gt N N2)) ) _
	(fresh/> F30 P2) 
	(update/> U30 P2)
	J3
    <- gt-implies-plus N>N2 D2 P2
    <- update/>-inversion* U2 P2 _ U20 EQ2
    <- eq-symmetric EQ2 EQ2s
    <- fresh-respects-eq F2P EQ2s nat`eq/ F2PP
    <- fresh/>-inversion F2PP P2 F2P0
    <- plus-swap-succ P0 P0s
    <- plus-swap-succ P2 P2s
    <- plus-commutative P0s P0sc
    <- plus-commutative P2s P2sc
    <- shift-preserves-fresh-converse* F1 (shift/+ P0sc) P2sc F10
    <- fresh-update-right-preserves-join-converse F10 F2P0 U20 J0 _
	 F30 U30 J0P
    <- join-respects-eq (join/> J0P P0) eq/ EQ2 eq/ J3.

- : fresh-update-right-preserves-join-converse/L F1 F2P (U2:update M N _ _)
	(join/< J0 (P0: plus (s D0) N1 N2))
	(nat`ge?/yes N>=N2) _
	(fresh/> F30 P1) (update/> U30 P1) J3
    <- ge-implies-plus (N>=N2:nat`ge N N2) D2 P2
    <- plus-swap-succ P0 P0s
    <- plus-commutative P0s P0sc 
    <- plus-commutative P2 P2c
    <- plus-associative P0sc P2c D1 D0+D2=D1 P1sc
    <- plus-swap-succ P1sc P1c
    <- plus-commutative P1c P1
    <- update-commutes-shift-converse* U2 (shift/+ P0sc) P1sc _ SH U20
    <- shift-preserves-fresh-converse* F2P SH P1sc F2P0
    <- fresh/>-inversion F1 P1 F10
    <- fresh-update-right-preserves-join-converse F10 F2P0 U20 J0 _
	 F30 U30 J0P
    <- shift-right-preserves-join J0P SH J3.

- : fresh-update-right-preserves-join-converse F1 F2P U2
	join/L _ F2P U2 join/L.

- : fresh-update-right-preserves-join-converse F1 F2P U2 J _ F3 U3 J3
    <- nat`ge?-total GE?
    <- fresh-update-right-preserves-join-converse/L F1 F2P U2 J GE? _ F3 U3 J3.

%worlds (WORLDS) 
	(fresh-update-right-preserves-join-converse _ _ _ _ _ _ _ _)
	(fresh-update-right-preserves-join-converse/L _ _ _ _ _ _ _ _ _).
%total (J1 J2)
	(fresh-update-right-preserves-join-converse/L _ _ _ J1 _ _ _ _ _)
	(fresh-update-right-preserves-join-converse _ _ _ J2 _ _ _ _).


%theorem fresh-update-right-preserves-join-converse* :
	forall* {M1} {M2P} {M2} {M} {N} {D} {MP}
	forall	{F1: fresh M1 N}
    		{F2: fresh M2P N}
    		{U2: update M2P N D M2}
    		{J: join M1 M2 M}
		{F: fresh MP N}
		{U: update MP N D M}
	exists	{J: join M1 M2P MP}
	true.

- : fresh-update-right-preserves-join-converse* F1 F2P U2 J F U JP
    <- fresh-update-right-preserves-join-converse F1 F2P U2 J _ FP UP JPP
    <- fresh-update-cancels FP UP F U nat`eq/ eq/ _ ME
    <- join-respects-eq JPP eq/ eq/ ME JP.

%worlds (WORLDS) (fresh-update-right-preserves-join-converse* _ _ _ _ _ _ _).
%total {} (fresh-update-right-preserves-join-converse* _ _ _ _ _ _ _).


%theorem fresh-update-preserves-join-converse :
	forall* {M1P} {M1} {M2P} {M2} {M} {N} {D1} {D2}
	forall	{F1: fresh M1P N}
		{U1: update M1P N D1 M1}
    		{F2: fresh M2P N}
    		{U2: update M2P N D2 M2}
    		{J: join M1 M2 M}
 	exists  {MP} {D}
		{F: fresh MP N}
		{U: update MP N D M}
		{J: data`join D1 D2 D}
		{J: join M1P M2P MP}
	true.

%theorem fresh-update-preserves-join-converse/L :
	forall* {N1} {D1} {M1} {M1P} 
		{N2} {D2} {M2} {M2P} {M} {N} {D1P} {D2P} {B1} {B2}
	forall	{F1: fresh M1P N}
		{U1: update M1P N D1P (map/+ N1 D1 M1)}
    		{F2: fresh M2P N}
    		{U2: update M2P N D2P (map/+ N2 D2 M2)}
    		{J: join (map/+ N1 D1 M1) (map/+ N2 D2 M2) M}
		{GE1: nat`ge? N N1 B1}
		{GE2: nat`ge? N N2 B2}
 	exists  {MP} {D}
		{F: fresh MP N}
		{U: update MP N D M}
		{J: data`join D1P D2P D}
		{J: join M1P M2P MP}
	true.

%theorem false-implies-data:
	forall	{F: void}
	exists	{D: data}
	true.

%worlds (WORLDS) (false-implies-data _ _).
%total { } (false-implies-data _ _).

- : fresh-update-preserves-join-converse/L F1P U1 F2P U2 _
	(nat`ge?/no GT) _ map/0 D F3 U3 DJ J3
    <- update-implies-lookup U1 L1
    <- fresh-lookup-not-equal (fresh/< GT) L1 NE
    <- nat`eq-ne-implies-false nat`eq/ NE F
    <- false-implies-data F D
    <- false-implies-fresh F F3
    <- false-implies-update F U3
    <- data`false-implies-join F DJ
    <- false-implies-join F J3.

- : fresh-update-preserves-join-converse/L F1P U1 F2P U2 _
	_ (nat`ge?/no GT) map/0 D F3 U3 DJ J3
    <- update-implies-lookup U2 L2
    <- fresh-lookup-not-equal (fresh/< GT) L2 NE
    <- nat`eq-ne-implies-false nat`eq/ NE F
    <- false-implies-data F D
    <- false-implies-fresh F F3
    <- false-implies-update F U3
    <- data`false-implies-join F DJ
    <- false-implies-join F J3.

- : fresh-update-preserves-join-converse/L F1P U1 F2P U2 
	(join/= J0 DJ0 nat`eq/) (nat`ge?/yes (nat`ge/= nat`eq/)) _ _ _ 
	F3 U3 DJ J3
    <- fresh-update-implies-shift F1P U1 SH1 EQ1
    <- fresh-update-implies-shift F2P U2 SH2 EQ2
    <- shift-preserves-join J0 SH1 SH2 _ SH3 J3
    <- shift-implies-fresh SH3 F3
    <- shift-implies-update SH3 U3
    <- data`join-respects-eq DJ0 EQ1 EQ2 data`eq/ DJ.

- : fresh-update-preserves-join-converse/L F1P U1 F2P U2 
	(join/= J0 DJ0 nat`eq/) (nat`ge?/yes (nat`ge/> GT)) _ _ _ 
	(fresh/> F30 P) (update/> U30 P)
	DJ J3
    <- gt-implies-plus GT _ P
    <- update/>-inversion* U1 P _ U10 EQ1
    <- update/>-inversion* U2 P _ U20 EQ2
    <- eq-symmetric EQ1 EQ1s
    <- eq-symmetric EQ2 EQ2s
    <- fresh-respects-eq F1P EQ1s nat`eq/ F1PP
    <- fresh-respects-eq F2P EQ2s nat`eq/ F2PP
    <- fresh/>-inversion F1PP P F1P0
    <- fresh/>-inversion F2PP P F2P0
    <- fresh-update-preserves-join-converse F1P0 U10 F2P0 U20 J0 _ _
	F30 U30 DJ J30
    <- join-respects-eq (join/= J30 DJ0 nat`eq/) EQ1 EQ2 eq/ J3.

- : fresh-update-preserves-join-converse/L F1P U1 F2P U2 
	(join/< J0 (P0:plus (s D0) N1 N2)) _ (nat`ge?/yes (N>=N2:nat`ge N N2)) 
	_ _ 
	(fresh/> F30 P1) 
	(update/> U30 P1) DJ J3
    <- ge-implies-plus (N>=N2:nat`ge N N2) D2 P2
    <- plus-swap-succ P0 P0s
    <- plus-commutative P0s P0sc 
    <- plus-commutative P2 P2c
    <- plus-associative P0sc P2c D1 D0+D2=D1 P1sc
    <- plus-swap-succ P1sc P1c
    <- plus-commutative P1c P1
    <- update/>-inversion* U1 P1 _ U10 EQ1
    <- eq-symmetric EQ1 EQ1s
    <- fresh-respects-eq F1P EQ1s nat`eq/ F1PP
    <- fresh/>-inversion F1PP P1 F1P0
    <- update-commutes-shift-converse* U2 (shift/+ P0sc) P1sc _ SH2 U20
    <- shift-preserves-fresh-converse* F2P SH2 P1sc F2P0
    <- fresh-update-preserves-join-converse F1P0 U10 F2P0 U20 J0 _ _
	F30 U30 DJ J30
    <- shift-right-preserves-join J30 SH2 J3P
    <- join-respects-eq J3P EQ1 eq/ eq/ J3.

- : fresh-update-preserves-join-converse/L F1P U1 F2P U2 
	(join/> J0 (P0:plus (s D0) N2 N1)) (nat`ge?/yes (N>=N1:nat`ge N N1)) _
	_ _ 
	(fresh/> F30 P2) 
	(update/> U30 P2) DJ J3
    <- ge-implies-plus (N>=N1:nat`ge N N1) D1 P1
    <- plus-swap-succ P0 P0s
    <- plus-commutative P0s P0sc 
    <- plus-commutative P1 P1c
    <- plus-associative P0sc P1c D2 D0+D1=D2 P2sc
    <- plus-swap-succ P2sc P2c
    <- plus-commutative P2c P2
    <- update-commutes-shift-converse* U1 (shift/+ P0sc) P2sc _ SH1 U10
    <- shift-preserves-fresh-converse* F1P SH1 P2sc F1P0
    <- update/>-inversion* U2 P2 _ U20 EQ2
    <- eq-symmetric EQ2 EQ2s
    <- fresh-respects-eq F2P EQ2s nat`eq/ F2PP
    <- fresh/>-inversion F2PP P2 F2P0
    <- fresh-update-preserves-join-converse F1P0 U10 F2P0 U20 J0 _ _
	F30 U30 DJ J30
    <- shift-left-preserves-join J30 SH1 J3P
    <- join-respects-eq J3P eq/ EQ2 eq/ J3.

- : fresh-update-preserves-join-converse F1P U1 F2P U2 J _ _ FP UP DJ JP
    <- nat`ge?-total GE1
    <- nat`ge?-total GE2
    <- fresh-update-preserves-join-converse/L F1P U1 F2P U2 J GE1 GE2 _ _ 
	FP UP DJ JP.

%worlds (WORLDS)
	(fresh-update-preserves-join-converse/L _ _ _ _ _ _ _ _ _ _ _ _ _)
	(fresh-update-preserves-join-converse _ _ _ _ _ _ _ _ _ _ _).

%total (J1 J2)
	(fresh-update-preserves-join-converse/L _ _ _ _ J1 _ _ _ _ _ _ _ _)
	(fresh-update-preserves-join-converse _ _ _ _ J2 _ _ _ _ _ _).


%theorem data-eq-symmetric:
	forall*	{D1} {D2}
	forall	{DE: data`eq D1 D2}
	exists	{DE: data`eq D2 D1}
	true.

- : data-eq-symmetric data`eq/ data`eq/.

%worlds (WORLDS) (data-eq-symmetric _ _).
%total { } (data-eq-symmetric _ _).

%theorem fresh-update-preserves-join-converse* :
	forall* {M1P} {M1} {M2P} {M2} {M} {N} {D1} {D2} {MP} {D}
	forall	{F1: fresh M1P N}
		{U1: update M1P N D1 M1}
    		{F2: fresh M2P N}
    		{U2: update M2P N D2 M2}
    		{J: join M1 M2 M}
		{F: fresh MP N}
		{U: update MP N D M}
	exists	{J: data`join D1 D2 D}
		{J: join M1P M2P MP}
	true.

- : fresh-update-preserves-join-converse* F1P U1 F2P U2 J F3 U3 DJ J3
    <- fresh-update-preserves-join-converse F1P U1 F2P U2 J _ _ F3P U3P DJP J3P
    <- fresh-update-cancels F3P U3P F3 U3 nat`eq/ eq/ DE E
    <- data`join-respects-eq DJP data`eq/ data`eq/ DE DJ
    <- join-respects-eq J3P eq/ eq/ E J3.

%worlds (WORLDS) (fresh-update-preserves-join-converse* _ _ _ _ _ _ _ _ _).
%total { } (fresh-update-preserves-join-converse* _ _ _ _ _ _ _ _ _).


%theorem bound-commute-join* :
	forall* {M1} {M2} {M3} {N1} {N2} {N3}
	forall	{B1: bound M1 N1}
		{B2: bound M2 N2}
		{B3: bound M3 N3}
		{J: join M1 M2 M3}
	exists	{MX: nat`max N1 N2 N3}
	true.

- : bound-commute-join* (bound/0) B2 B3 join/L M
    <- bound-unique B2 B3 eq/ N2=N3
    <- ge-zero-always _ N2>=0
    <- nat`le-implies-max N2>=0 MP
    <- nat`max-respects-eq MP nat`eq/ nat`eq/ N2=N3 M.

- : bound-commute-join* B1 (bound/0) B3 join/R M
    <- bound-unique B1 B3 eq/ N1=N3
    <- ge-zero-always _ N1>=0
    <- nat`ge-implies-max N1>=0 MP
    <- nat`max-respects-eq MP nat`eq/ nat`eq/ N1=N3 M.

- : bound-commute-join* 
	(bound/+ P1 B1) (bound/+ P2 B2) (bound/+ P3 B3)
	(join/= J _ nat`eq/) MP
    <- bound-commute-join* B1 B2 B3 J M
    <- plus-left-preserves-max M P1 P2 P3 MP.

- : bound-commute-join*
	(bound/+ P1 B1) (bound/+ P2 B2) (bound/+ P3 B3)
	(join/< J P) MP
    <- plus-swap-succ P Ps
    <- plus-commutative Ps Psc
    <- plus-swap-succ P2 P2s
    <- plus-associative Psc P2s _ P2Ps PP2
    <- plus-swap-succ-converse P2Ps P2P
    <- bound-commute-join* B1 (bound/+ P2P B2) B3 J M
    <- plus-left-preserves-max M P1 PP2 P3 MP.

- : bound-commute-join*
	(bound/+ P1 B1) (bound/+ P2 B2) (bound/+ P3 B3)
	(join/> J P) MP
    <- plus-swap-succ P Ps
    <- plus-commutative Ps Psc
    <- plus-swap-succ P1 P1s
    <- plus-associative Psc P1s _ P1Ps PP1
    <- plus-swap-succ-converse P1Ps P1P
    <- bound-commute-join* (bound/+ P1P B1) B2 B3 J M
    <- plus-left-preserves-max M PP1 P2 P3 MP.

%worlds (WORLDS) (bound-commute-join* _ _ _ _ _).
%total (J) (bound-commute-join* _ _ _ J _).


%theorem size-commute-disjoint-join*:
	forall* {M1} {M2} {M3} {N1} {N2} {N3}
	forall	{B1: size M1 N1}
		{B2: size M2 N2}
		{B3: size M3 N3}
		{DJ: disjoint M1 M2}
		{J: join M1 M2 M3}
	exists	{MX: nat`plus N1 N2 N3}
	true.

- : size-commute-disjoint-join* (size/0) B2 B3 _ join/L P
    <- size-unique B2 B3 eq/ N2=N3
    <- nat`plus-respects-eq plus/z nat`eq/ nat`eq/ N2=N3 P.

- : size-commute-disjoint-join* B1 (size/0) B3 _ join/R P
    <- size-unique B1 B3 eq/ N1=N3
    <- plus-right-identity N1 N1+0=N1
    <- nat`plus-respects-eq N1+0=N1 nat`eq/ nat`eq/ N1=N3 P.

- : size-commute-disjoint-join* _ _ _ DSJ
	(join/= J _ nat`eq/) P
    <- disjoint/=-contradiction DSJ nat`eq/ F
    <- nat`false-implies-plus F P.

- : size-commute-disjoint-join*
	(size/+ S1) (size/+ S2) (size/+ S3) DSJ
	(join/< J1 P1) (plus/s PP1)
    <- disjoint/<-inversion DSJ P1 DSJ1
    <- size-commute-disjoint-join* S1 (size/+ S2) S3 DSJ1 J1 PP1.

- : size-commute-disjoint-join*
	(size/+ S1) (size/+ S2) (size/+ S3) DSJ
	(join/> J1 P1) PP
    <- disjoint/>-inversion DSJ P1 DSJ1
    <- size-commute-disjoint-join* (size/+ S1) S2 S3 DSJ1 J1 PP1
    <- plus-right-increase PP1 PP.

%worlds (WORLDS) (size-commute-disjoint-join* _ _ _ _ _ _).
%total (J) (size-commute-disjoint-join* _ _ _ _ J _).

%{%
#ifdef MAP_LEQ
#ifdef DATA_JOIN_IMPLIES_LEQ
%}%

%theorem join-implies-leq* :
	forall* {M1} {M2} {M3}
	forall {J:join M1 M2 M3}
        exists {L:leq M1 M3}
	true.

- : join-implies-leq* join/L leq/0.

- : join-implies-leq* join/R M2=M2
    <- leq-reflexive _ M2=M2.

- : join-implies-leq* (join/= M1+M2=M3 D1+D2=D3 nat`eq/) 
                      (leq/= M1<=M3 D1<=D3 nat`eq/)
    <- data`join-implies-leq* D1+D2=D3 D1<=D3
    <- join-implies-leq* M1+M2=M3 M1<=M3.

- : join-implies-leq* (join/< M1+_=M3 _) (leq/= M1<=M3 D1<=D1 nat`eq/)
    <- data`leq-reflexive _ D1<=D1
    <- join-implies-leq* M1+_=M3 M1<=M3.	

- : join-implies-leq* (join/> M311+M2=M3 P) (leq/> M311<=M3 P)
    <- join-implies-leq* M311+M2=M3 M311<=M3.

%worlds (WORLDS) (join-implies-leq* _ _).
%total (J) (join-implies-leq* J _).

%{%
#ifdef DATA_JOIN_COMMUTATIVE
/* laziness and want to avoid dependency obn data`join-implies-leq (no star) */
%}%

%theorem join-implies-leq :
	forall* {M1} {M2} {M3}
	forall {A:join M1 M2 M3}
        exists {L1:leq M1 M3} {L2:leq M2 M3}
	true.

- : join-implies-leq M1*M2=M3 M1<=M3 M2<=M3
    <- join-implies-leq* M1*M2=M3 M1<=M3
    <- join-commutative M1*M2=M3 M2*M1=M3
    <- join-implies-leq* M2*M1=M3 M2<=M3.

%worlds (WORLDS) (join-implies-leq _ _ _).
%total { } (join-implies-leq _ _ _).

%{%
#endif
#endif

#ifdef DATA_LEQ_NE_IMPLIES_JOIN
%}%

%theorem leq-implies-join :
	forall* {M1} {M2}
	forall {L:leq M1 M2}
	exists {M0} {A:join M0 M1 M2}
	true.

- : leq-implies-join leq/0 _ join/R.

% lemma for leq/= to test whether equal or not
%theorem leq-implies-join/= :
	forall* {M0} {M1} {M2} {N} {D1} {D2} {B}
	forall {A:join M0 M1 M2} {L:data`leq D1 D2} {E?:data`eq? D1 D2 B}
	exists {M000} {A:join M000 (map/+ N D1 M1) (map/+ N D2 M2)}
	true.

- : leq-implies-join (leq/= M1<=M2 D1<=D2 nat`eq/) _ A
    <- leq-implies-join M1<=M2 _ M0+M1=M2
    <- data`eq?-total D1?D2
    <- leq-implies-join/= M0+M1=M2 D1<=D2 D1?D2 _ A.

- : leq-implies-join/= M0+M1=M2 _ (data`eq?/yes) _ M0<<X+M1X+M2X
    <- shift-total S
    <- shift-left-preserves-join M0+M1=M2 S M0<<X+M1X+M2X.

- : leq-implies-join/= M0+M1=M2 D1<=D2 (data`eq?/no D1<>D2) _
                       (join/= M0+M1=M2 D0+D1=D2 nat`eq/)
    <- data`leq-ne-implies-join D1<=D2 D1<>D2 _ D0+D1=D2.

- : leq-implies-join (leq/> M311<=M2 N3+1+N2=N1) _
                     (join/< M0+M311=M2 N3+1+N2=N1)
    <- leq-implies-join M311<=M2 _ M0+M311=M2.

%worlds (WORLDS) (leq-implies-join/= _ _ _ _ _).
%total { } (leq-implies-join/= _ _ _ _ _).

%worlds (WORLDS) (leq-implies-join _ _ _).
%total (L) (leq-implies-join L _ _).
  
%{%
#define LEQ_IMPLIES_JOIN 1
#define CMP_IFF_OP 1
#endif /* DATA_LEQ_NE_IMPLIES_JOIN */

#ifdef DATA_JOIN_IS_LUB
%}%

%theorem join-is-lub :
	forall* {M1} {M2} {M3} {M4}
	forall {J:join M1 M2 M3}
	       {L1:leq M1 M4} {L2:leq M2 M4}
	exists {L3:leq M3 M4}
	true.

- : join-is-lub join/L _ L L.

- : join-is-lub join/R L _ L.

- : join-is-lub (join/= M1+M2=M3 D1+D2=D3 nat`eq/)
                (leq/= M1<=M4 D1<=D4 nat`eq/)
                (leq/= M2<=M4 D2<=D4 nat`eq/)
                (leq/= M3<=M4 D3<=D4 nat`eq/)
    <- data`join-is-lub D1+D2=D3 D1<=D4 D2<=D4 D3<=D4
    <- join-is-lub M1+M2=M3 M1<=M4 M2<=M4 M3<=M4.

- : join-is-lub (join/= _ _ nat`eq/) (leq/= _ _ nat`eq/) (leq/> _ N3+1+N=N) L
    <- nat`plus-implies-gt N3+1+N=N nat`eq/ N>N
    <- gt-anti-reflexive N>N F
    <- false-implies-leq F L.

- : join-is-lub (join/= _ _ nat`eq/) (leq/> _ N3+1+N=N) (leq/= _ _ nat`eq/) L
    <- nat`plus-implies-gt N3+1+N=N nat`eq/ N>N
    <- gt-anti-reflexive N>N F
    <- false-implies-leq F L.

- : join-is-lub (join/= M1+M2=M3 D1+D2=D3 nat`eq/)
                (leq/> M511<=M4 N5+1+N4=N)
                (leq/> M622<=M4 N6+1+N4=N)
                (leq/> M633<=M4 N6+1+N4=N)
    <- nat`plus-right-cancels N5+1+N4=N N6+1+N4=N nat`eq/ nat`eq/ N5+1=N6+1
    <- nat`succ-cancels N5+1=N6+1 N5=N6
    <- map/+-preserves-eq N5=N6 data`eq/ eq/ M511=M611
    <- leq-respects-eq M511<=M4 M511=M611 eq/ M611<=M4
    <- join-is-lub (join/= M1+M2=M3 D1+D2=D3 nat`eq/) 
                   M611<=M4 M622<=M4 M633<=M4.

- : join-is-lub (join/< _ N0+1+N=N) (leq/= _ _ nat`eq/) (leq/= _ _ nat`eq/) L
    <- nat`plus-implies-gt N0+1+N=N nat`eq/ N>N
    <- gt-anti-reflexive N>N F
    <- false-implies-leq F L.

- : join-is-lub (join/< M1+M022=M3 N0+1+N1=N2)
                (leq/= M1<=M4 D1<=D4 nat`eq/)
                (leq/> M622<=M4 N6+1+N1=N2)
                (leq/= M3<=M4 D1<=D4 nat`eq/)
    <- nat`plus-right-cancels N6+1+N1=N2 N0+1+N1=N2 nat`eq/ nat`eq/ N6+1=N0+1
    <- succ-cancels N6+1=N0+1 N6=N0
    <- map/+-preserves-eq N6=N0 data`eq/ eq/ M622=M022
    <- leq-respects-eq M622<=M4 M622=M022 eq/ M022<=M4
    <- join-is-lub M1+M022=M3 M1<=M4 M022<=M4 M3<=M4.

- : join-is-lub (join/< _ N0+1+N1=N2) (leq/> _ N3+1+N2=N1) (leq/= _ _ nat`eq/) L
    <- nat`plus-implies-gt N3+1+N2=N1 nat`eq/ N1>N2
    <- nat`plus-implies-gt N0+1+N1=N2 nat`eq/ N2>N1
    <- nat`gt-anti-symmetric N1>N2 N2>N1 F
    <- false-implies-leq F L.

- : join-is-lub (join/< M1+M022=M3 N0+1+N1=N2)
                (leq/> M511<=M4 N5+1+N4=N1)
                (leq/> M622<=M4 N6+1+N4=N2)
                (leq/> M513<=M4 N5+1+N4=N1)
    <- nat`plus-swap-succ N5+1+N4=N1 N5+N4+1=N1
    <- nat`plus-associative-converse N5+N4+1=N1 N0+1+N1=N2 
                                     N6P N0+1+N5=N6P N6P+N4+1=N2
    <- nat`plus-swap-succ-converse N6P+N4+1=N2 N6P+1+N4=N2
    <- nat`plus-right-cancels N6P+1+N4=N2 N6+1+N4=N2 nat`eq/ nat`eq/ N6P+1=N6+1
    <- nat`succ-cancels N6P+1=N6+1 N6P=N6
    <- nat`plus-respects-eq N0+1+N5=N6P nat`eq/ nat`eq/ N6P=N6 N0+1+N5=N6
    <- join-is-lub (join/< M1+M022=M3 N0+1+N5=N6) M511<=M4 M622<=M4 M513<=M4.

- : join-is-lub (join/> _ N3+1+N=N) (leq/= _ _ nat`eq/) (leq/= _ _ nat`eq/) L
    <- nat`plus-implies-gt N3+1+N=N nat`eq/ N>N
    <- gt-anti-reflexive N>N F
    <- false-implies-leq F L.

- : join-is-lub (join/> _ N3+1+N2=N1) (leq/= _ _ nat`eq/) (leq/> _ N0+1+N1=N2) L
    <- nat`plus-implies-gt N3+1+N2=N1 nat`eq/ N1>N2
    <- nat`plus-implies-gt N0+1+N1=N2 nat`eq/ N2>N1
    <- nat`gt-anti-symmetric N1>N2 N2>N1 F
    <- false-implies-leq F L.

- : join-is-lub (join/> M311+M2=M3 N3+1+N2=N1)
                (leq/> M511<=M4 N5+1+N2=N1)
                (leq/= M2<=M4 D2<=D4 nat`eq/)
                (leq/= M3<=M4 D2<=D4 nat`eq/)
    <- nat`plus-right-cancels N3+1+N2=N1 N5+1+N2=N1 nat`eq/ nat`eq/ N3+1=N5+1
    <- nat`succ-cancels N3+1=N5+1 N3=N5
    <- map/+-preserves-eq N3=N5 data`eq/ eq/ M311=M511
    <- join-respects-eq M311+M2=M3 M311=M511 eq/ eq/ M511+M2=M3
    <- join-is-lub M511+M2=M3 M511<=M4 M2<=M4 M3<=M4.

- : join-is-lub (join/> M311+M2=M3 N3+1+N2=N1)
                (leq/> M511<=M4 N5+1+N4=N1)
                (leq/> M622<=M4 N6+1+N4=N2)
                (leq/> M623<=M4 N6+1+N4=N2)
    <- nat`plus-swap-succ N6+1+N4=N2 N6+N4+1=N2
    <- nat`plus-associative-converse N6+N4+1=N2 N3+1+N2=N1 
                                     N5P N3+1+N6=N5P N5P+N4+1=N1
    <- nat`plus-swap-succ N5+1+N4=N1 N5+N4+1=N1
    <- nat`plus-right-cancels N5P+N4+1=N1 N5+N4+1=N1 nat`eq/ nat`eq/ N5P=N5
    <- nat`plus-respects-eq N3+1+N6=N5P nat`eq/ nat`eq/ N5P=N5 N3+1+N6=N5
    <- join-is-lub (join/> M311+M2=M3 N3+1+N6=N5) M511<=M4 M622<=M4 M623<=M4.

%worlds (WORLDS) (join-is-lub _ _ _ _).
%total (L) (join-is-lub _ L _ _).


%theorem join-idempotent :
	forall* {S}
	exists {J:join S S S}
	true.

- : join-idempotent S+S=S
    <- join-total S+S=SP
    <- join-implies-leq* S+S=SP S<=SP
    <- leq-reflexive _ S<=S
    <- join-is-lub S+S=SP S<=S S<=S SP<=S
    <- leq-anti-symmetric SP<=S S<=SP SP=S
    <- join-respects-eq S+S=SP eq/ eq/ SP=S S+S=S.

%worlds (WORLDS) (join-idempotent _).
%total { } (join-idempotent _).


%theorem leq-implies-join :
	forall* {M1} {M2}
	forall {L:leq M1 M2}
	exists {J:join M1 M2 M2}
	true.

- : leq-implies-join M1<=M2 M1+M2=M2
    <- join-total M1+M2=M3
    <- leq-reflexive _ M2<=M2
    <- join-is-lub M1+M2=M3 M1<=M2 M2<=M2 M3<=M2
    <- join-implies-leq M1+M2=M3 _ M2<=M3
    <- leq-anti-symmetric M3<=M2 M2<=M3 M3=M2
    <- join-respects-eq M1+M2=M3 eq/ eq/ M3=M2 M1+M2=M2.

%worlds (WORLDS) (leq-implies-join _ _).
%total { } (leq-implies-join _ _).

%{%
#define LEQ_IMPLIES_JOIN 1
#endif /* DATA_JOIN_IS_LUB */
%}%

%theorem disjoint-leq-implies-join-leq* :
	forall* {C1} {C2} {C} {C3}
	forall	{D:disjoint C1 C2}
		{L1:leq C1 C}
		{L2:leq C2 C}
		{J:join C1 C2 C3}
	exists	{L3:leq C3 C}
	true.

- : disjoint-leq-implies-join-leq* disjoint/L _ C2<=C Z+C2=C3 C3<=C
    <- join-deterministic join/L Z+C2=C3 eq/ eq/ C2=C3
    <- leq-respects-eq C2<=C C2=C3 eq/ C3<=C.

- : disjoint-leq-implies-join-leq* disjoint/R C1<=C _ C1+0=C3 C3<=C
    <- join-deterministic join/R C1+0=C3 eq/ eq/ C1=C3
    <- leq-respects-eq C1<=C C1=C3 eq/ C3<=C.

- : disjoint-leq-implies-join-leq* 
	(disjoint/< C1^C022 P) 
	(leq/= C1<=C4 D1<=D4 nat`eq/) 
	C222<=C444 C111+C222=C333 C333<=C444
    <- join/<-inversion C111+C222=C333 P _ C1+C022=C5 C333=C115
    <- leq/>-inversion C222<=C444 P C022<=C4
    <- disjoint-leq-implies-join-leq* 
	C1^C022 C1<=C4 C022<=C4 C1+C022=C5 C5<=C4
    <- eq-symmetric C333=C115 C115=C333
    <- leq-respects-eq 
	(leq/= C5<=C4 D1<=D4 nat`eq/) C115=C333 eq/ C333<=C444.

- : disjoint-leq-implies-join-leq* (disjoint/< C1^C022 N0+1+N1=N2) (leq/> C611<=C4 N6+1+N4=N1) C222<=C444 C111+C222=C333 C333<=C444
    <- join/<-inversion C111+C222=C333 N0+1+N1=N2 _ C1+C022=C5 C333=C115
    <- plus-swap-succ N6+1+N4=N1 N6+N4+1=N1
    <- plus-associative-converse 
	N6+N4+1=N1 N0+1+N1=N2 N7 N0+1+N6=N7 N7+N4+1=N2
    <- plus-swap-succ-converse N7+N4+1=N2 N7+1+N4=N2
    <- leq/>-inversion C222<=C444 N7+1+N4=N2 C722<=C4
    <- disjoint-leq-implies-join-leq* 
	(disjoint/< C1^C022 N0+1+N6=N7) 
	C611<=C4 C722<=C4 
	(join/< C1+C022=C5 N0+1+N6=N7) C615<=C4
    <- eq-symmetric C333=C115 C115=C333
    <- leq-respects-eq 
	(leq/> C615<=C4 N6+1+N4=N1) C115=C333 eq/ C333<=C444.

- : disjoint-leq-implies-join-leq* 
	(disjoint/> C011^C2 P) 
	C111<=C444 (leq/= C2<=C4 D2<=D4 nat`eq/) 
	C111+C222=C333 C333<=C444
    <- join/>-inversion C111+C222=C333 P _ C011+C2=C5 C333=C225
    <- leq/>-inversion C111<=C444 P C011<=C4
    <- disjoint-leq-implies-join-leq* 
	C011^C2 C011<=C4 C2<=C4 C011+C2=C5 C5<=C4
    <- eq-symmetric C333=C225 C225=C333
    <- leq-respects-eq 
	(leq/= C5<=C4 D2<=D4 nat`eq/) C225=C333 eq/ C333<=C444.

- : disjoint-leq-implies-join-leq* 
	(disjoint/> C011^C2 N0+1+N2=N1) 
	C111<=C444 (leq/> C622<=C4 N6+1+N4=N2)
	C111+C222=C333 C333<=C444
    <- join/>-inversion C111+C222=C333 N0+1+N2=N1 _ C011+C2=C5 C333=C225
    <- plus-swap-succ N6+1+N4=N2 N6+N4+1=N2
    <- plus-associative-converse 
	N6+N4+1=N2 N0+1+N2=N1 N7 N0+1+N6=N7 N7+N4+1=N1
    <- plus-swap-succ-converse N7+N4+1=N1 N7+1+N4=N1
    <- leq/>-inversion C111<=C444 N7+1+N4=N1 C711<=C4
    <- disjoint-leq-implies-join-leq* 
	(disjoint/> C011^C2 N0+1+N6=N7) 
	C711<=C4 C622<=C4 
	(join/> C011+C2=C5 N0+1+N6=N7) C625<=C4
    <- eq-symmetric C333=C225 C225=C333
    <- leq-respects-eq 
	(leq/> C625<=C4 N6+1+N4=N2) C225=C333 eq/ C333<=C444.

%worlds (WORLDS) (disjoint-leq-implies-join-leq* _ _ _ _ _).
%total (L) (disjoint-leq-implies-join-leq* _ L _ _ _).

%{%
#ifdef DATA_JOIN_PRESERVES_LEQ
%}%

%theorem join-left-preserves-leq* :
	forall* {M1} {M2} {M3} {M4} {M5}
	forall {L1:leq M2 M4}
               {J:join M1 M2 M3} {JP:join M1 M4 M5}
	exists {L3:leq M3 M5}
	true.

- : join-left-preserves-leq* L join/L join/L L.

- : join-left-preserves-leq* _ join/L join/R leq/0.

- : join-left-preserves-leq* leq/0 join/R M1+M4=M5 M1<=M5
    <- join-implies-leq* M1+M4=M5 M1<=M5.

- : join-left-preserves-leq* (leq/= M2<=M4 D2<=D4 nat`eq/)
                             (join/= M1+M2=M3 D1+D2=D3 nat`eq/)
                             M111+M144=M M133<=M
    <- join/=-inversion M111+M144=M nat`eq/ D5 M5 D1+D4=D5 M1+M4=M5 M=M155
    <- eq-symmetric M=M155 M155=M
    <- meta-eq (map/+ N1 D5 M5) M M155=M
    <- data`join-left-preserves-leq* D2<=D4 D1+D2=D3 D1+D4=D5 D3<=D5
    <- join-left-preserves-leq* M2<=M4 M1+M2=M3 M1+M4=M5 M3<=M5
    <- leq-respects-eq (leq/= M3<=M5 D3<=D5 nat`eq/) eq/ M155=M M133<=M.

- : {M1+M044=M5:join M1 (map/+ N0 D4 M4) M5}
    {M115=M: eq (map/+ N1 D1 M5) M}
    join-left-preserves-leq* (leq/= M2<=M4 D2<=D4 nat`eq/)
                             (join/< M1+M022=M3 N0+1+N1=N2)
                             M111+M244=M M113<=M
    <- join/<-inversion M111+M244=M N0+1+N1=N2 M5 M1+M044=M5 M=M115
    <- eq-symmetric M=M115 M115=M
    <- meta-eq (map/+ N1 D1 M5) M M115=M
    <- join-left-preserves-leq* (leq/= M2<=M4 D2<=D4 nat`eq/) 
                                M1+M022=M3 M1+M044=M5 M3<=M5
    <- data`leq-reflexive _ D1<=D1
    <- leq-respects-eq (leq/= M3<=M5 D1<=D1 nat`eq/) eq/ M115=M M113<=M.

- : join-left-preserves-leq* (leq/= M2<=M4 D2<=D4 nat`eq/)
                             (join/> M311+M2=M3 N3+1+N2=N1)
                             M111+M244=M M223<=M
    <- join/>-inversion M111+M244=M N3+1+N2=N1 M5 M311+M4=M5 M=M245
    <- eq-symmetric M=M245 M245=M
    <- meta-eq (map/+ N2 D4 M5) M M245=M
    <- join-left-preserves-leq* M2<=M4 M311+M2=M3 M311+M4=M5 M3<=M5
    <- leq-respects-eq (leq/= M3<=M5 D2<=D4 nat`eq/) eq/ M245=M M223<=M.

- : join-left-preserves-leq* (leq/> M622<=M4 N6+1+N4=N2)
                             (join/= M1+M2=M3 D1+D2=D3 nat`eq/)   % N1=N2
                             M211+M444=M M233<=M
    <- join/>-inversion M211+M444=M N6+1+N4=N2 M5 M611+M4=M5 M=M445
    <- eq-symmetric M=M445 M445=M
    <- meta-eq (map/+ N4 D4 M5) M M445=M
    <- join-left-preserves-leq* M622<=M4 (join/= M1+M2=M3 D1+D2=D3 nat`eq/)
                                M611+M4=M5 M633<=M5
    <- leq-respects-eq (leq/> M633<=M5 N6+1+N4=N2) eq/ M445=M M233<=M.

- : join-left-preserves-leq* (leq/> M622<=M4 N6+1+N4=N2)
                             (join/< M1+M022=M3 N0+1+N4=N2)
                             (join/= M1+M4=M5 D1+D4=D5 nat`eq/)
                             (leq/= M3<=M5 D1<=D5 nat`eq/)
    <- nat`plus-right-cancels N6+1+N4=N2 N0+1+N4=N2 nat`eq/ nat`eq/ N6+1=N0+1
    <- nat`succ-cancels N6+1=N0+1 N6=N0
    <- map/+-preserves-eq N6=N0 data`eq/ eq/ M622=M022
    <- leq-respects-eq M622<=M4 M622=M022 eq/ M022<=M4
    <- join-left-preserves-leq* M022<=M4 M1+M022=M3 M1+M4=M5 M3<=M5
    <- data`join-implies-leq* D1+D4=D5 D1<=D5.

- : join-left-preserves-leq* (leq/> M622<=M4 N6+1+N4=N2)
                             (join/< M1+M022=M3 N0+1+N1=N2)
                             (join/< M1+M544=M5 N5+1+N1=N4)
                             (leq/= M3<=M5 D1<=D1 nat`eq/)
    <- nat`plus-swap-succ N5+1+N1=N4 N5+N1+1=N4
    <- nat`plus-associative-converse N5+N1+1=N4 N6+1+N4=N2 
                                     N0P N6+1+N5=N0P N0P+N1+1=N2
    <- nat`plus-swap-succ N0+1+N1=N2 N0+N1+1=N2
    <- nat`plus-right-cancels N0P+N1+1=N2 N0+N1+1=N2 nat`eq/ nat`eq/ N0P=N0
    <- nat`plus-respects-eq N6+1+N5=N0P nat`eq/ nat`eq/ N0P=N0 N6+1+N5=N0
    <- join-left-preserves-leq* (leq/> M622<=M4 N6+1+N5=N0) 
                                M1+M022=M3 M1+M544=M5 M3<=M5
    <- data`leq-reflexive _ D1<=D1.
       

% for some reason, twelf needs a lot of help inferring types here:
- : join-left-preserves-leq* ((leq/> M622<=M4 N6+1+N4=N2):leq (map/+ N2 D2 M2) (map/+ N4 D4 M4))
                             (join/< M1+M022=M3 N0+1+N1=N2)
                             (join/> M511+M4=M5 N5+1+N4=N1)
                             (leq/> M513<=M5 N5+1+N4=N1)
    <- nat`plus-swap-succ N5+1+N4=N1 N5+N4+1=N1
    <- nat`plus-associative-converse N5+N4+1=N1 N0+1+N1=N2 
                                     N6P N0+1+N5=N6P N6P+N4+1=N2
    <- nat`plus-swap-succ N6+1+N4=N2 N6+N4+1=N2
    <- nat`plus-right-cancels N6P+N4+1=N2 N6+N4+1=N2 nat`eq/ nat`eq/ N6P=N6
    <- nat`plus-respects-eq N0+1+N5=N6P nat`eq/ nat`eq/ N6P=N6 N0+1+N5=N6
    <- join-left-preserves-leq* M622<=M4 
       ((join/< M1+M022=M3 N0+1+N5=N6):join (map/+ N5 D1 M1) (map/+ N6 D2 M2)
                                            (map/+ N5 D1 M3))
                                M511+M4=M5 M513<=M5.

- : join-left-preserves-leq* (leq/> M622<=M4 N6+1+N4=N2)
                             (join/> M311+M2=M3 N3+1+N2=N1) 
                             M111+M444=M M223<=M
    <- nat`plus-swap-succ N6+1+N4=N2 N6+N4+1=N2
    <- nat`plus-associative-converse N6+N4+1=N2 N3+1+N2=N1 
                                     N5 N3+1+N6=N5 N5+N4+1=N1
    <- nat`plus-swap-succ-converse N5+N4+1=N1 N5+1+N4=N1 
    <- join/>-inversion M111+M444=M N5+1+N4=N1 M5 M511+M4=M5 M=M445
    <- eq-symmetric M=M445 M445=M
    <- meta-eq _ _ M445=M
    <- join-left-preserves-leq* M622<=M4 (join/> M311+M2=M3 N3+1+N6=N5)
                                M511+M4=M5 M623<=M5
    <- leq-respects-eq (leq/> M623<=M5 N6+1+N4=N2) eq/ M445=M M223<=M.
  
%worlds (WORLDS) (join-left-preserves-leq* _ _ _ _).
%total (J) (join-left-preserves-leq* _ _ J _).

%{%
#ifdef DATA_JOIN_NO_RIGHT_IDENTITY
#ifdef DATA_JOIN_LEFT_CANCELS
#ifdef DATA_JOIN_COMMUTATIVE
#ifdef DATA_JOIN_IMPLIES_LEQ
#ifdef LEQ_IMPLIES_JOIN
%}%

%theorem join-left-cancels-leq :
	forall* {M1} {M2} {M3} {M1P} {M2P} {M3P}
	forall {A:join M1 M2 M3} {AP:join M1P M2P M3P}
               {E:eq M1 M1P} {L:leq M3 M3P}
        exists {LP:leq M2 M2P}
	true.

- : join-left-cancels-leq M1+M2=M3 M1+M2P=M3P eq/ M3<=M3P M2<=M2P
    <- leq-implies-join M3<=M3P M M+M3=M3P
    <- join-commutative M+M3=M3P M3+M=M3P
    <- join-associative M1+M2=M3 M3+M=M3P M2M M2+M=M2M M1+M2M=M3P
    <- join-left-cancels M1+M2M=M3P M1+M2P=M3P eq/ eq/ M2M=M2P
    <- join-implies-leq* M2+M=M2M M2<=M2M
    <- leq-respects-eq M2<=M2M eq/ M2M=M2P M2<=M2P.

%worlds (WORLDS) (join-left-cancels-leq _ _ _ _ _).
%total { } (join-left-cancels-leq _ _ _ _ _).

%{%
#define OP_CANCELS 1
#define EQ eq
#endif
#endif
#endif
#endif
#endif

#define OPN join
#define OP(X,Y) X+Y
#define CMPN leq
#define CMP(X,Y) X<=Y
#ifdef DATA_JOIN_COMMUTATIVE
#define OP_COMMUTATIVE 1
#endif
#ifdef DATA_JOIN_TOTAL_STAR
#define OP_TOTAL 1
#endif
#define CMP_TRANSITIVE 1
BEGIN_ELF
#include "OPN-preserves-CMPN.i"
END_ELF
#undef CMP_TRANSITIVE
#undef OP_TOTAL
#undef OP_COMMUTATIVE
#undef CMP
#undef CMPN
#undef OP
#undef OPN
#undef CMP_IFF_OP
#undef OP_CANCELS

#endif /* DATA_JOIN_PRESERVES_LEQ */
/* but still MAP_LEQ */
%}%

%theorem disjoint-join-left-preserves-leq-converse:
	forall* {M1} {M2} {M3} {M}
    	forall	{M3<=M: leq M3 M}
		{M1UM2=M: join M1 M2 M}
    		{M2^M3: disjoint M1 M3}
	exists	{M3<=M1: leq M3 M2}
 	true.

- : disjoint-join-left-preserves-leq-converse
	leq/0 _ _ leq/0.

- : disjoint-join-left-preserves-leq-converse
	L join/L _ L.

- : disjoint-join-left-preserves-leq-converse
	L join/R D LP
    <- disjoint-respects-geq* D L M3^M3
    <- disjoint-anti-reflexive M3^M3 Z=M3
    <- leq-respects-eq leq/0 Z=M3 eq/ LP.

- : disjoint-join-left-preserves-leq-converse
	(leq/= L0 D3<=D nat`eq/) J DJ
	(leq/= L0P D3<=D2 N=N2)
    <- disjoint-symmetric DJ DJs
    <- disjoint-lookup-implies-fresh (lookup/= nat`eq/) DJs F1
    <- fresh-lookup-not-equal F1 (lookup/= nat`eq/) N<>N1
    <- nat`ne-symmetric N<>N1 N1<>N
    <- join/>-inversion* J N1<>N _ N2=N D2=D P J0
    <- disjoint/>-inversion DJ P DJ0
    <- disjoint-join-left-preserves-leq-converse L0 J0 DJ0 L0P
    <- data-eq-symmetric D2=D D=D2
    <- data`leq-respects-eq D3<=D data`eq/ D=D2 D3<=D2
    <- nat`eq-symmetric N2=N N=N2.
	
- : disjoint-join-left-preserves-leq-converse
	(leq/> L0 P3)
	(join/= J0 D1+D2=D nat`eq/) DJ
	(leq/> L0P P3)
    <- disjoint/<-inversion DJ P3 DJ0
    <- disjoint-join-left-preserves-leq-converse L0 J0 DJ0 L0P.

- : disjoint-join-left-preserves-leq-converse
	(leq/> L0 P3)
	(join/> J0 P0) DJ
	(leq/> L0P P3)
    <- plus-swap-succ P0 P0s
    <- plus-commutative P0s P0sc
    <- plus-swap-succ P3 P3s
    <- plus-commutative P3s P3sc
    <- shift-preserves-disjoint-converse DJ (shift/+ P0sc) (shift/+ P3sc) DJ0
    <- disjoint-join-left-preserves-leq-converse L0 J0 DJ0 L0P.

- : disjoint-join-left-preserves-leq-converse
	(leq/> L0 P3)
	(join/< J0 P0) DJ
	LP
    <- plus-swap-succ P0 P0s
    <- plus-commutative P0s P0sc
    <- plus-swap-succ P3 P3s
    <- plus-commutative P3s P3sc
    <- disjoint/<-inversion DJ P3 DJ0
    <- disjoint-join-left-preserves-leq-converse L0 J0 DJ0 L0P
    <- shift-preserves-leq* L0P (shift/+ P3sc) (shift/+ P0sc) LP.

%worlds (WORLDS) (disjoint-join-left-preserves-leq-converse _ _ _ _).
%total (J) (disjoint-join-left-preserves-leq-converse _ J _ _).


%theorem disjoint-join-right-preserves-leq-converse:
	forall* {M1} {M2} {M3} {M}
    	forall	{M3<=M: leq M3 M}
		{M1UM2=M: join M1 M2 M}
    		{M2^M3: disjoint M2 M3}
	exists	{M3<=M1: leq M3 M1}
 	true.

- : disjoint-join-right-preserves-leq-converse
	leq/0 _ _ leq/0.

- : disjoint-join-right-preserves-leq-converse
	L join/R _ L.

- : disjoint-join-right-preserves-leq-converse
	L join/L D LP
    <- disjoint-respects-geq* D L M3^M3
    <- disjoint-anti-reflexive M3^M3 Z=M3
    <- leq-respects-eq leq/0 Z=M3 eq/ LP.

- : disjoint-join-right-preserves-leq-converse
	(leq/= L0 D3<=D nat`eq/) J DJ
	(leq/= L0P D3<=D1 N=N1)
    <- disjoint-symmetric DJ DJs
    <- disjoint-lookup-implies-fresh (lookup/= nat`eq/) DJs F2
    <- fresh-lookup-not-equal F2 (lookup/= nat`eq/) N<>N2
    <- nat`ne-symmetric N<>N2 N2<>N
    <- join/<-inversion* J N2<>N _ N1=N D1=D P J0
    <- disjoint/>-inversion DJ P DJ0
    <- disjoint-join-right-preserves-leq-converse L0 J0 DJ0 L0P
    <- data-eq-symmetric D1=D D=D1
    <- data`leq-respects-eq D3<=D data`eq/ D=D1 D3<=D1
    <- nat`eq-symmetric N1=N N=N1.
	
- : disjoint-join-right-preserves-leq-converse
	(leq/> L0 P3)
	(join/= J0 D1+D2=D nat`eq/) DJ
	(leq/> L0P P3)
    <- disjoint/<-inversion DJ P3 DJ0
    <- disjoint-join-right-preserves-leq-converse L0 J0 DJ0 L0P.

- : disjoint-join-right-preserves-leq-converse
	(leq/> L0 P3)
	(join/< J0 P0) DJ
	(leq/> L0P P3)
    <- plus-swap-succ P0 P0s
    <- plus-commutative P0s P0sc
    <- plus-swap-succ P3 P3s
    <- plus-commutative P3s P3sc
    <- shift-preserves-disjoint-converse DJ (shift/+ P0sc) (shift/+ P3sc) DJ0
    <- disjoint-join-right-preserves-leq-converse L0 J0 DJ0 L0P.

- : disjoint-join-right-preserves-leq-converse
	(leq/> L0 P3)
	(join/> J0 P0) DJ
	LP
    <- plus-swap-succ P0 P0s
    <- plus-commutative P0s P0sc
    <- plus-swap-succ P3 P3s
    <- plus-commutative P3s P3sc
    <- disjoint/<-inversion DJ P3 DJ0
    <- disjoint-join-right-preserves-leq-converse L0 J0 DJ0 L0P
    <- shift-preserves-leq* L0P (shift/+ P3sc) (shift/+ P0sc) LP.

%worlds (WORLDS) (disjoint-join-right-preserves-leq-converse _ _ _ _).
%total (J) (disjoint-join-right-preserves-leq-converse _ J _ _).

%{%
#endif /* MAP_LEQ */
%}%
