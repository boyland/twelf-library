%%%%% set-remove.elf
%%%%% Set subtraction.
%%%%% This file is part of the set.elf signature


%%%% Definitions


%%% removal

remove : set -> set -> set -> type.

remove/ : leq C A -> disjoint B C -> union B C D -> leq A D -> remove A B C.



%%%% Theorems


%%% Theorems about remove


%theorem false-implies-remove :
	forall* {S1} {S2} {S3}
	forall {F:void}
	exists {D:remove S1 S2 S3}
	true.

%worlds () (false-implies-remove _ %{=>}% S1-S2=S3).
%total {} (false-implies-remove _ _).


%theorem remove-respects-eq :
	forall* {S1} {S2} {S3} {T1} {T2} {T3}
	forall {DS:remove S1 S2 S3}
               {E1:eq S1 T1} {E2:eq S2 T2} {E3:eq S3 T3}
	exists {DT:remove T1 T2 T3}
	true.

- : remove-respects-eq R eq/ eq/ eq/ R.

%worlds () (remove-respects-eq S1-S2=S3 S1=T1 S2=T2 S3=T3 %{=>}% T1-T2=T3).
%total {} (remove-respects-eq _ _ _ _ _).
%reduces R = RP (remove-respects-eq R _ _ _ RP).


%theorem remove-total* :
	forall {S1} {S2}
	exists {S3} {R:remove S1 S2 S3}
	true.

%abbrev remove-total = remove-total* _ _ _.

%theorem remove-total/L1 :
        forall* {A} {B} 
	forall	{N}
	       	{S: size A N}
        exists	{C} 
		{R:remove A B C}
	true.

%theorem remove-total/L2 :
        forall* {B} {A} {AP} {CP} {X} {B?}
	forall  {AD: add AP X A}
		{R: remove AP B CP}
		{D?: member? B X B?}
        exists	{C} 
		{R:remove A B C}
	true.

- : remove-total/L2 AP+X=A
	(remove/ CP<=AP B^CP BUCP=DP AP<=DP) (member?/in X<-B) CP 
	(remove/ CP<=A B^CP BUCP=DP A<=DP)
    <- add-implies-unit-union AP+X=A APUX=A
    <- union-implies-leq APUX=A AP<=A X<=A
    <- leq-transitive CP<=AP AP<=A CP<=A
    <- union-implies-leq BUCP=DP B<=DP CP<=DP
    <- member-respects-leq X<-B B<=DP X<-DP
    <- member-implies-unit-leq X<-DP X<=DP
    <- union-is-lub APUX=A AP<=DP X<=DP A<=DP.

- : remove-total/L2 AP+X=A
	(remove/ CP<=AP B^CP BUCP=DP AP<=DP) (member?/out X</-B) C 
	(remove/ C<=A B^C BUC=D A<=D)
    <- not-member-implies-unit-disjoint X</-B B^X
    <- union-total* CP (set/1 X) C CPUX=C
    <- union-right-preserves-disjoint* B^CP B^X CPUX=C B^C
    <- add-implies-unit-union AP+X=A APUX=A
    <- union-implies-leq APUX=A AP<=A X<=A
    <- leq-transitive CP<=AP AP<=A CP<=A
    <- union-is-lub CPUX=C CP<=A X<=A C<=A
    <- union-total* DP (set/1 X) D DPUX=D
    <- union-associative* BUCP=DP DPUX=D CPUX=C BUC=D
    <- union-implies-leq DPUX=D DP<=D X<=D
    <- leq-transitive AP<=DP DP<=D AP<=D
    <- union-is-lub APUX=A AP<=D X<=D A<=D.

%worlds () (remove-total/L2 _ _ _ _ _).
%total { } (remove-total/L2 _ _ _ _ _).

- : remove-total/L1 _ size/0 set/0 (remove/ leq/0 disjoint/R union/R leq/0).

- : remove-total/L1 _ SZ C A-B=C
    <- can-remove* SZ AP X _ SZP AP+X=A _
    <- remove-total/L1 _ SZP CP AP-B=CP
    <- member?-total M?
    <- remove-total/L2 AP+X=A AP-B=CP M? C A-B=C.

%worlds () (remove-total/L1 _ _ _ _).
%total (N) (remove-total/L1 N _ _ _).

- : remove-total R
    <- size-total SZ
    <- remove-total/L1 _ SZ _ R.

%worlds () (remove-total* _ _ _ _).
%total { } (remove-total* _ _ _ _).


%theorem remove-deterministic :
	forall* {S1} {S2} {S3} {T1} {T2} {T3}
	forall {RS:remove S1 S2 S3} {RT:remove T1 T2 T3}
	       {E1:eq S1 T1} {E2:eq S2 T2}
	exists {E3:eq S3 T3}
	true.

%abbrev remove-unique = remove-deterministic.

%theorem remove-deterministic/L :
	forall* {S1} {S2} {S3} {T3} {B}
	forall	{RS:remove S1 S2 S3} {RT:remove S1 S2 T3}
		{E: eq? S3 T3 B}
	exists	{E3:eq S3 T3}
	true.

- : remove-deterministic/L _ _ eq?/yes eq/.

- : remove-deterministic/L 
	(remove/ C<=A B^C BUC=D A<=D)
	(remove/ E<=F B^E BUE=F A<=F)
	(ne/< X<-C X</-E) C=E
    <- member-respects-leq X<-C C<=A X<-A
    <- member-respects-leq X<-A A<=F X<-F
    <- disjoint-symmetric B^C C^B
    <- disjoint-member-implies-not-member X<-C C^B X</-B
    <- not-member-union-left-preserves-member-converse X</-B X<-F BUE=F X<-E
    <- member-not-member-contradiction X<-E X</-E FALSE
    <- false-implies-eq FALSE C=E.

- : remove-deterministic/L 
	(remove/ E<=F B^E BUE=F A<=F)
	(remove/ C<=A B^C BUC=D A<=D)
	(ne/> X</-E X<-C) E=C
    <- member-respects-leq X<-C C<=A X<-A
    <- member-respects-leq X<-A A<=F X<-F
    <- disjoint-symmetric B^C C^B
    <- disjoint-member-implies-not-member X<-C C^B X</-B
    <- not-member-union-left-preserves-member-converse X</-B X<-F BUE=F X<-E
    <- member-not-member-contradiction X<-E X</-E FALSE
    <- false-implies-eq FALSE E=C.

%worlds () (remove-deterministic/L _ _ _ _).
%total { } (remove-deterministic/L _ _ _ _).

- : remove-deterministic R1 R2 eq/ eq/ EQ
    <- eq?-total EQ?
    <- remove-deterministic/L R1 R2 EQ? EQ.

%worlds () (remove-deterministic _ _ _ _ _).
%total { } (remove-deterministic _ _ _ _ _).


%theorem shift-preserves-remove :
	forall*	{A} {B} {C} {N} {AP} {BP}
	forall	{R: remove A B C}
		{SA: shift N A AP}
		{SB: shift N B BP}
	exists	{CP}
		{SC: shift N C CP}
		{RP: remove AP BP CP}
	true.

- : shift-preserves-remove 
	(remove/ C<=A B^C BUC=D A<=D) SA SB CP SC
	(remove/ CP<=AP BP^CP BPUCP=DP AP<=DP)
    <- shift-total* N C CP SC
    <- shift-preserves-leq C<=A SC SA CP<=AP
    <- shift-preserves-disjoint B^C SB SC BP^CP
    <- shift-preserves-union BUC=D SB SC _ SD BPUCP=DP
    <- shift-preserves-leq A<=D SA SD AP<=DP.

%worlds () (shift-preserves-remove _ _ _ _ _ _).
%total { } (shift-preserves-remove _ _ _ _ _ _).


%theorem remove-implies-leq :
	forall* {S1} {S2} {S3}
	forall {D:remove S1 S2 S3}
	exists {L:leq S3 S1}
	true.

- : remove-implies-leq (remove/ L _ _ _) L.

%worlds () (remove-implies-leq _ _).
%total { } (remove-implies-leq _ _).


%theorem remove-implies-leq-union :
	forall* {S1} {S2} {S3} {S4}
	forall {D:remove S1 S2 S3} {U:union S2 S3 S4}
	exists {L:leq S1 S4}
	true.

- : remove-implies-leq-union (remove/ C<=A B^C BUC=D A<=D) BUC=E A<=E
    <- union-unique BUC=D BUC=E eq/ eq/ D=E
    <- leq-respects-eq A<=D eq/ D=E A<=E.

%worlds () (remove-implies-leq-union _ _ _).
%total { } (remove-implies-leq-union _ _ _).


%theorem remove-implies-disjoint :
	forall* {S1} {S2} {S3}
	forall {D:remove S1 S2 S3}
	exists {I:disjoint S2 S3}
	true.

- : remove-implies-disjoint (remove/ _ B^C _ _) B^C.

%worlds () (remove-implies-disjoint _ _).
%total { } (remove-implies-disjoint _ _).


%theorem remove-implies-empty-intersection :
	forall* {S1} {S2} {S3}
	forall {D:remove S1 S2 S3}
	exists {I:intersection S2 S3 set/0}
	true.

- : remove-implies-empty-intersection R I
    <- remove-implies-disjoint R D
    <- disjoint-intersection-empty D I.

%worlds () (remove-implies-empty-intersection _ _).
%total { } (remove-implies-empty-intersection _ _).


%theorem leq-implies-remove-all :
	forall*	{A} {B}
	forall	{L: leq A B}
	exists	{R: remove A B set/0}
	true.

- : leq-implies-remove-all A<=B (remove/ leq/0 disjoint/R union/R A<=B).

%worlds () (leq-implies-remove-all _ _).
%total { } (leq-implies-remove-all _ _).


%theorem remove-characterization:
	forall*	{A} {B} {C} {D}
	forall	{L: leq C A}
		{I: disjoint B C}
		{U: union B C D}
		{G: leq A D}
	exists	{R: remove A B C}
	true.

- : remove-characterization D1 D2 D3 D4 (remove/ D1 D2 D3 D4).

%worlds () (remove-characterization _ _ _ _ _).
%total { } (remove-characterization _ _ _ _ _).


%theorem remove-associates-union* :
	forall*	{A} {B} {C} {D} {E} {F}
	forall	{R1: remove A B C}
		{R2: remove C D E}
		{U: union B D F}
	exists	{R3: remove A F E}
	true.

- : remove-associates-union* 
	(remove/ C<=A B^C BUC=G A<=G)
	(remove/ E<=C D^E DUE=H C<=H) BUD=F 
	(remove/ E<=A F^E FUE=I A<=I)
    <- leq-transitive E<=C C<=A E<=A
    <- union-total BUH=I
    <- union-associative-converse* DUE=H BUH=I BUD=F FUE=I
    <- leq-reflexive B B<=B
    <- disjoint-respects-geq B^C B<=B E<=C B^E
    <- union-preserves-disjoint* B^E D^E BUD=F F^E
    <- union-left-preserves-leq* C<=H BUC=G BUH=I G<=I
    <- leq-transitive A<=G G<=I A<=I.

%worlds () (remove-associates-union* _ _ _ _).
%total { } (remove-associates-union* _ _ _ _).


%theorem remove-associates-union :
	forall*	{A} {B} {C} {D} {E}
	forall	{R1: remove A B C}
		{R2: remove C D E}
	exists	{F}
		{U: union B D F}
		{R3: remove A F E}
	true.

- : remove-associates-union A-B=C C-D=E F BUD=F A-F=E
    <- union-total BUD=F
    <- remove-associates-union* A-B=C C-D=E BUD=F A-F=E.

%worlds () (remove-associates-union _ _ _ _ _).
%total { } (remove-associates-union _ _ _ _ _).


%theorem remove-associates-union-converse* :
	forall*	{A} {B} {C} {D} {E} {F}
	forall	{U: union B D F}
		{R3: remove A F E}
		{R1: remove A B C}
	exists	{R2: remove C D E}
	true.

- : remove-associates-union-converse* BUD=F A-F=E A-B=C C-D=E
    <- remove-total C-D=EP
    <- remove-associates-union* A-B=C C-D=EP BUD=F A-F=EP
    <- remove-unique A-F=EP A-F=E eq/ eq/ EP=E
    <- remove-respects-eq C-D=EP eq/ eq/ EP=E C-D=E.

%worlds () (remove-associates-union-converse* _ _ _ _).
%total { } (remove-associates-union-converse* _ _ _ _).


%theorem remove-associates-union-converse :
	forall*	{A} {B} {D} {E} {F}
	forall	{U: union B D F}
		{R3: remove A F E}
	exists	{C}
		{R1: remove A B C}
		{R2: remove C D E}
	true.

- : remove-associates-union-converse BUD=F A-F=E C A-B=C C-D=E
    <- remove-total A-B=C
    <- remove-associates-union-converse* BUD=F A-F=E A-B=C C-D=E.

%worlds () (remove-associates-union-converse _ _ _ _ _).
%total { } (remove-associates-union-converse _ _ _ _ _).


%theorem remove-reorders* :
	forall*	{A} {B} {C} {D} {E} {F}
	forall	{R1: remove A B C}
		{R2: remove C D E}
		{R3: remove A D F}
	exists	{R4: remove F B E}
	true.

- : remove-reorders* A-B=C C-D=E A-D=G G-B=E
    <- remove-associates-union A-B=C C-D=E F BUD=F A-F=E
    <- union-commutative BUD=F DUB=F
    <- remove-associates-union-converse* DUB=F A-F=E A-D=G G-B=E.

%worlds () (remove-reorders* _ _ _ _).
%total { } (remove-reorders* _ _ _ _).


%theorem remove-reorders :
	forall*	{A} {B} {C} {D} {E}
	forall	{R1: remove A B C}
		{R2: remove C D E}
	exists	{F}
		{R3: remove A D F}
		{R4: remove F B E}
	true.

- : remove-reorders A-B=C C-D=E G A-D=G G-B=E
    <- remove-total A-D=G
    <- remove-reorders* A-B=C C-D=E A-D=G G-B=E.

%worlds () (remove-reorders _ _ _ _ _).
%total { } (remove-reorders _ _ _ _ _).


%theorem disjoint-implies-remove-nothing :
	forall* {A} {B}
	forall	{D: disjoint A B}
	exists	{R: remove A B A}
	true.

- : disjoint-implies-remove-nothing A^B A-B=A
    <- leq-reflexive A A<=A
    <- union-total* B A C BUA=C
    <- union-implies-leq BUA=C B<=C A<=C
    <- disjoint-symmetric A^B B^A
    <- remove-characterization A<=A B^A BUA=C A<=C A-B=A.

%worlds () (disjoint-implies-remove-nothing _ _).
%total { } (disjoint-implies-remove-nothing _ _).


%theorem remove-subset-implies-union :
	forall*	{A} {B} {C}
	forall	{R: remove A B C}
		{L: leq B A}
	exists	{U: union B C A}
	true.

- : remove-subset-implies-union A-B=C B<=A BUC=A
    <- remove-implies-leq A-B=C C<=A
    <- union-total* B C AX BUC=AX
    <- remove-implies-leq-union A-B=C BUC=AX A<=AX
    <- union-is-lub BUC=AX B<=A C<=A AX<=A
    <- leq-anti-symmetric AX<=A A<=AX AX=A
    <- union-respects-eq BUC=AX eq/ eq/ AX=A BUC=A.

%worlds () (remove-subset-implies-union _ _ _).
%total { } (remove-subset-implies-union _ _ _).


%theorem union-right-commutes-remove* :
	forall*	{A} {B} {C} {D} {E} {F} {G}
	forall	{R: remove A B C}
		{U1: union A G D}
		{R2: remove B G E}
		{U3: union C G F}
	exists	{EP: remove D E F}
	true.

- : union-right-commutes-remove* A-B=C AUG=D B-G=E CUG=F D-E=F
    <- remove-implies-leq A-B=C C<=A
    <- union-implies-leq AUG=D A<=D G<=D
    <- leq-transitive C<=A A<=D C<=D
    <- union-is-lub CUG=F C<=D G<=D F<=D
    <- remove-implies-disjoint B-G=E G^E
    <- remove-implies-disjoint A-B=C B^C
    <- remove-implies-leq B-G=E E<=B
    <- disjoint-respects-geq* B^C E<=B E^C
    <- disjoint-symmetric E^C C^E
    <- union-preserves-disjoint* C^E G^E CUG=F F^E
    <- disjoint-symmetric F^E E^F
    <- union-total* B C I BUC=I
    <- union-total* E F H EUF=H
    <- union-commutative EUF=H FUE=H
    <- union-associative CUG=F FUE=H J GUE=J CUJ=H
    <- union-implies-leq GUE=J G<=J E<=J
    <- union-implies-leq CUJ=H C<=H J<=H
    <- remove-implies-leq-union A-B=C BUC=I A<=I 
    <- remove-implies-leq-union B-G=E GUE=J B<=J
    <- leq-transitive B<=J J<=H B<=H
    <- leq-transitive G<=J J<=H G<=H
    <- union-is-lub BUC=I B<=H C<=H I<=H
    <- leq-transitive A<=I I<=H A<=H
    <- union-is-lub AUG=D A<=H G<=H D<=H
    <- remove-characterization F<=D E^F EUF=H D<=H D-E=F.
	
%worlds () (union-right-commutes-remove* _ _ _ _ _).
%total { } (union-right-commutes-remove* _ _ _ _ _).


%theorem union-right-commutes-remove :
	forall*	{A} {B} {C} {G}
	forall	{R: remove A B C}
	exists	{D} {E} {F}
		{U1: union A G D}
		{R2: remove B G E}
		{U3: union C G F}
		{EP: remove D E F}
	true.

- : union-right-commutes-remove A-B=C D E F AUG=D B-G=E CUG=F D-E=F
    <- union-total AUG=D
    <- remove-total B-G=E
    <- union-total CUG=F
    <- union-right-commutes-remove* A-B=C AUG=D B-G=E CUG=F D-E=F.

%worlds () (union-right-commutes-remove _ _ _ _ _ _ _ _).
%total { } (union-right-commutes-remove _ _ _ _ _ _ _ _).


%theorem union-right-preserves-remove :
	forall*	{A} {B} {C} {D} {G}
	forall	{R: remove A B C}
		{U1: union A G D}
		{R2: leq G B}
	exists	{EP: remove D B C}
	true.

- : union-right-preserves-remove A-B=C AUG=D G<=B D-B=C
    <- union-total* B C E BUC=E
    <- remove-implies-leq A-B=C C<=A
    <- remove-implies-leq-union A-B=C BUC=E A<=E
    <- remove-implies-disjoint A-B=C B^C
    <- union-implies-leq AUG=D A<=D G<=D
    <- union-implies-leq BUC=E B<=E C<=E
    <- leq-transitive C<=A A<=D C<=D
    <- leq-transitive G<=B B<=E G<=E
    <- union-is-lub AUG=D A<=E G<=E D<=E
    <- remove-characterization C<=D B^C BUC=E D<=E D-B=C.

%worlds () (union-right-preserves-remove _ _ _ _).
%total { } (union-right-preserves-remove _ _ _ _).


%theorem remove-right-distributes-over-union* :
	forall*	{A} {B} {C} {D} {E} {F} {G}
	forall	{U: union A B C}
		{R: remove C D E}
		{RA: remove A D F}
		{RB: remove B D G}
	exists	{U: union F G E}
	true.

%theorem remove-right-distributes-over-union/L :
	forall*	{A} {B} {C} {D} {E} {F} {G} {B?} {EP}
	forall	{U: union A B C}
		{R: remove C D E}
		{RA: remove A D F}
		{RB: remove B D G}
		{U: union F G EP}
		{E?: eq? EP E B?}
	exists	{E: eq EP E}
	true.

- : remove-right-distributes-over-union/L _ _ _ _ _ eq?/yes eq/.

- : remove-right-distributes-over-union/L AUB=C
	(remove/ E<=C D^E DUE=H C<=H)
	(remove/ F<=A D^F DUF=I A<=I)
	(remove/ G<=B D^G DUG=J B<=J) FUG=EP
	(ne/> X</-EP X<-E) EQ
    <- union-preserves-not-member-converse* X</-EP FUG=EP X</-F X</-G
    <- member-respects-leq X<-E E<=C X<-C
    <- disjoint-symmetric D^E E^D
    <- disjoint-member-implies-not-member X<-E E^D X</-D
    <- union-preserves-not-member* X</-D X</-F DUF=I X</-I
    <- union-preserves-not-member* X</-D X</-G DUG=J X</-J
    <- not-member-respects-geq X</-I A<=I X</-A
    <- not-member-respects-geq X</-J B<=J X</-B
    <- union-preserves-not-member* X</-A X</-B AUB=C X</-C
    <- member-not-member-contradiction X<-C X</-C FALSE
    <- false-implies-eq FALSE EQ.

%theorem remove-right-distributes-over-union/L/< :
	forall*	{A} {B} {C} {D} {E} {F} {G} {X} {FB} {EP}
	forall	{U: union A B C}
		{R: remove C D E}
		{RA: remove A D F}
		{RB: remove B D G}
		{U: union F G EP}
		{M: member EP X}
		{FM?: member? F X FB}
	exists	{EM: member E X}
	true.

- : remove-right-distributes-over-union/L/< AUB=C
	(remove/ E<=C D^E DUE=H C<=H)
	(remove/ F<=A D^F DUF=I A<=I)
	(remove/ G<=B D^G DUG=J B<=J) FUG=EP X<-EP 
	(member?/in X<-F) X<-E
    <- member-respects-leq X<-F F<=A X<-A
    <- union-right-preserves-member* X<-A AUB=C X<-C
    <- member-respects-leq X<-C C<=H X<-H
    <- disjoint-symmetric D^F F^D
    <- disjoint-member-implies-not-member X<-F F^D X</-D
    <- not-member-union-left-preserves-member-converse X</-D X<-H DUE=H X<-E.

- : remove-right-distributes-over-union/L/< AUB=C
	(remove/ E<=C D^E DUE=H C<=H)
	(remove/ F<=A D^F DUF=I A<=I)
	(remove/ G<=B D^G DUG=J B<=J) FUG=EP X<-EP 
	(member?/out X</-F) X<-E
    <- not-member-union-left-preserves-member-converse X</-F X<-EP FUG=EP X<-G
    <- member-respects-leq X<-G G<=B X<-B
    <- union-left-preserves-member* X<-B AUB=C X<-C
    <- member-respects-leq X<-C C<=H X<-H
    <- disjoint-symmetric D^G G^D
    <- disjoint-member-implies-not-member X<-G G^D X</-D
    <- not-member-union-left-preserves-member-converse X</-D X<-H DUE=H X<-E.

%worlds () (remove-right-distributes-over-union/L/< _ _ _ _ _ _ _ _).
%total { } (remove-right-distributes-over-union/L/< _ _ _ _ _ _ _ _).

- : remove-right-distributes-over-union/L AUB=C C-D=E A-D=F B-D=G FUG=EP
	(ne/< X<-EP X</-E) EQ
    <- member?-total M?
    <- remove-right-distributes-over-union/L/<
	AUB=C C-D=E A-D=F B-D=G FUG=EP X<-EP M? X<-E
    <- member-not-member-contradiction X<-E X</-E FALSE
    <- false-implies-eq FALSE EQ.

%worlds () (remove-right-distributes-over-union/L _ _ _ _ _ _ _).
%total { } (remove-right-distributes-over-union/L _ _ _ _ _ _ _).

- : remove-right-distributes-over-union* U1 R1 R2 R3 U2
    <- union-total U2P
    <- eq?-total EQ?
    <- remove-right-distributes-over-union/L U1 R1 R2 R3 U2P EQ? EQ
    <- union-respects-eq U2P eq/ eq/ EQ U2.

%worlds () (remove-right-distributes-over-union* _ _ _ _ _).
%total { } (remove-right-distributes-over-union* _ _ _ _ _).


%theorem remove-right-distributes-over-union :
	forall*	{A} {B} {C} {D} {E}
	forall	{U: union A B C}
		{R: remove C D E}
	exists	{F} {G}
		{RA: remove A D F}
		{RB: remove B D G}
		{U: union F G E}
	true.

- : remove-right-distributes-over-union U R1 _ _ R2 R3 UP
    <- remove-total R2
    <- remove-total R3
    <- remove-right-distributes-over-union* U R1 R2 R3 UP.

%worlds () (remove-right-distributes-over-union _ _ _ _ _ _ _).
%total { } (remove-right-distributes-over-union _ _ _ _ _ _ _).


%theorem remove-right-preserves-leq :
	forall*	{A} {B} {C} {D} {E}
	forall	{L: leq A D}
		{R: remove A B C}
		{RP: remove D B E}
	exists	{LP: leq C E}
	true.

- : remove-right-preserves-leq A<=D A-B=C D-B=E C<=E
    <- remove-implies-leq A-B=C C<=A
    <- leq-transitive C<=A A<=D C<=D
    <- union-total* B E F BUE=F
    <- remove-implies-leq-union D-B=E BUE=F D<=F
    <- leq-transitive C<=D D<=F C<=F
    <- remove-implies-disjoint A-B=C B^C
    <- disjoint-symmetric B^C C^B
    <- leq-union-disjoint-implies-leq C<=F BUE=F C^B C<=E.

%worlds () (remove-right-preserves-leq _ _ _ _).
%total { } (remove-right-preserves-leq _ _ _ _).


%theorem remove-right-preserves-disjoint :
	forall*	{A} {B} {C} {D} {E}
	forall	{L: disjoint A D}
		{R: remove A B C}
		{RP: remove D B E}
	exists	{LP: disjoint C E}
	true.

- : remove-right-preserves-disjoint A^D A-B=C D-B=E C^E
    <- remove-implies-leq A-B=C C<=A
    <- remove-implies-leq D-B=E E<=D
    <- disjoint-respects-geq A^D C<=A E<=D C^E.

%worlds () (remove-right-preserves-disjoint _ _ _ _).
%total { } (remove-right-preserves-disjoint _ _ _ _).


%theorem remove-right-distributes-over-intersection* :
	forall*	{A} {B} {C} {D} {E} {F} {G}
	forall	{U: intersection A B C}
		{R: remove C D E}
		{RA: remove A D F}
		{RB: remove B D G}
	exists	{U: intersection F G E}
	true.

%theorem remove-right-distributes-over-intersection/L :
	forall*	{A} {B} {C} {D} {E} {F} {G} {X} {Y} {B?}
	forall	{U: intersection A B C}
		{R: remove C D E}
		{RA: remove A D F}
		{RB: remove B D G}
		{RF: remove F E X}
		{RG: remove G E Y}
		{D?: disjoint? X Y B?}
	exists	{D: disjoint X Y}
	true.

- : remove-right-distributes-over-intersection/L _ _ _ _ _ _ 
	(disjoint?/yes D) D.

- : remove-right-distributes-over-intersection/L
	A^B=C C-D=E A-D=F B-D=G F-E=X G-E=Y (disjoint?/no XM YM) X^Y
    <- remove-implies-disjoint F-E=X E^X
    <- disjoint-symmetric E^X X^E
    <- disjoint-member-implies-not-member XM X^E ENM
    <- remove-implies-leq F-E=X X<=F
    <- remove-implies-leq G-E=Y Y<=G
    <- member-respects-leq XM X<=F FM
    <- member-respects-leq YM Y<=G GM
    <- remove-implies-disjoint A-D=F D^F
    <- disjoint-symmetric D^F F^D
    <- disjoint-member-implies-not-member FM F^D DNM
    <- union-total* D E H DUE=H
    <- union-preserves-not-member* DNM ENM DUE=H HNM
    <- remove-implies-leq-union C-D=E DUE=H C<=H
    <- not-member-respects-geq HNM C<=H CNM
    <- remove-implies-leq A-D=F F<=A
    <- remove-implies-leq B-D=G G<=B
    <- member-respects-leq FM F<=A AM
    <- member-respects-leq GM G<=B BM
    <- intersection-preserves-member AM BM A^B=C CM
    <- member-not-member-contradiction CM CNM FALSE
    <- false-implies-disjoint FALSE X^Y.
		
%worlds () (remove-right-distributes-over-intersection/L _ _ _ _ _ _ _ _).
%total { } (remove-right-distributes-over-intersection/L _ _ _ _ _ _ _ _).

- : remove-right-distributes-over-intersection* A^B=C C-D=E A-D=F B-D=G F^G=E
    <- intersection-implies-leq A^B=C C<=A C<=B
    <- remove-right-preserves-leq C<=A C-D=E A-D=F E<=F
    <- remove-right-preserves-leq C<=B C-D=E B-D=G E<=G
    <- remove-total* F E X F-E=X
    <- remove-total* G E Y G-E=Y 
    <- remove-subset-implies-union F-E=X E<=F EUX=F
    <- remove-subset-implies-union G-E=Y E<=G EUY=G
    <- disjoint?-total X^Y?
    <- remove-right-distributes-over-intersection/L 
	A^B=C C-D=E A-D=F B-D=G F-E=X G-E=Y X^Y? X^Y
    <- intersection-characterization EUX=F EUY=G X^Y F^G=E.

%worlds () (remove-right-distributes-over-intersection* _ _ _ _ _).
%total { } (remove-right-distributes-over-intersection* _ _ _ _ _).


%theorem remove-right-distributes-over-intersection :
	forall*	{A} {B} {C} {D} {E}
	forall	{U: intersection A B C}
		{R: remove C D E}
	exists	{F} {G}
		{RA: remove A D F}
		{RB: remove B D G}
		{U: intersection F G E}
	true.

- : remove-right-distributes-over-intersection U R _ _ RA RB UP
    <- remove-total RA
    <- remove-total RB
    <- remove-right-distributes-over-intersection* U R RA RB UP.

%worlds () (remove-right-distributes-over-intersection _ _ _ _ _ _ _).
%total { } (remove-right-distributes-over-intersection _ _ _ _ _ _ _).

